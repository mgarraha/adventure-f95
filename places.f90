module places
   implicit none

   public
   integer, parameter :: LOCSIZ = 150
   integer, dimension(LOCSIZ) :: ATLOC
   integer, dimension(200) :: LINK
   integer, dimension(100) :: PLACE
   integer, dimension(100) :: FIXED
   integer :: HOLDNG

contains

   subroutine DSTROY(OBJECT)

!  PERMANENTLY ELIMINATE "OBJECT" BY MOVING TO A NON-EXISTENT LOCATION.

      INTEGER OBJECT

      CALL MOVE(OBJECT,0)
      RETURN
   end subroutine


   subroutine JUGGLE(OBJECT)

!  JUGGLE AN OBJECT BY PICKING IT UP AND PUTTING IT DOWN AGAIN, THE PURPOSE
!  BEING TO GET THE OBJECT TO THE FRONT OF THE CHAIN OF THINGS AT ITS LOC.

      INTEGER OBJECT
      INTEGER I,J

      I=PLACE(OBJECT)
      J=FIXED(OBJECT)
      CALL MOVE(OBJECT,I)
      CALL MOVE(OBJECT+100,J)
      RETURN
   end subroutine


   subroutine MOVE(OBJECT,WHERE)

!  PLACE ANY OBJECT ANYWHERE BY PICKING IT UP AND DROPPING IT.  MAY ALREADY BE
!  TOTING, IN WHICH CASE THE CARRY IS A NO-OP.  MUSTN'T PICK UP OBJECTS WHICH
!  ARE NOT AT ANY LOC, SINCE CARRY WANTS TO REMOVE OBJECTS FROM ATLOC CHAINS.

      INTEGER OBJECT,WHERE
      INTEGER FROM

      IF (OBJECT.LE.100) THEN
         FROM=PLACE(OBJECT)
      ELSE
         FROM=FIXED(OBJECT-100)
      END IF
      IF(FROM.GT.0.AND.FROM.LE.300)CALL CARRY(OBJECT,FROM)
      CALL DROP(OBJECT,WHERE)
      RETURN
   end subroutine


   integer function PUT(OBJECT,WHERE,PVAL)

!  PUT IS THE SAME AS MOVE, EXCEPT IT RETURNS A VALUE USED TO SET UP THE
!  NEGATED PROP VALUES FOR THE REPOSITORY OBJECTS.

      INTEGER OBJECT,WHERE,PVAL

      CALL MOVE(OBJECT,WHERE)
      PUT=(-1)-PVAL
      RETURN
   end function


   subroutine CARRY(OBJECT,WHERE)

!  START TOTING AN OBJECT, REMOVING IT FROM THE LIST OF THINGS AT ITS FORMER
!  LOCATION.  INCR HOLDNG UNLESS IT WAS ALREADY BEING TOTED.  IF OBJECT>100
!  (MOVING "FIXED" SECOND LOC), DON'T CHANGE PLACE OR HOLDNG.

      INTEGER OBJECT,WHERE
      INTEGER TEMP

      IF (OBJECT.LE.100) THEN
         IF(PLACE(OBJECT).EQ.-1)RETURN
         PLACE(OBJECT)=-1
         HOLDNG=HOLDNG+1
      END IF
      IF (ATLOC(WHERE).EQ.OBJECT) THEN
         ATLOC(WHERE)=LINK(OBJECT)
         RETURN
      END IF
      TEMP=ATLOC(WHERE)
      DO WHILE (LINK(TEMP).NE.OBJECT)
         TEMP=LINK(TEMP)
      END DO
      LINK(TEMP)=LINK(OBJECT)
      RETURN
   end subroutine


   subroutine DROP(OBJECT,WHERE)

!  PLACE AN OBJECT AT A GIVEN LOC, PREFIXING IT ONTO THE ATLOC LIST.  DECR
!  HOLDNG IF THE OBJECT WAS BEING TOTED.

      INTEGER OBJECT,WHERE
      
      IF (OBJECT.LE.100) THEN
         IF(PLACE(OBJECT).EQ.-1)HOLDNG=HOLDNG-1
         PLACE(OBJECT)=WHERE
      ELSE
         FIXED(OBJECT-100)=WHERE
      END IF
      IF(WHERE.LE.0)RETURN
      LINK(OBJECT)=ATLOC(WHERE)
      ATLOC(WHERE)=OBJECT
      RETURN
   end subroutine

end module
