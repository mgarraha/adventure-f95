module wizcom

   implicit none
   integer :: WKDAY,WKEND,HOLID,HBEGIN,HEND,  &
              SHORT,MAGIC,MAGNM,LATNCY,SAVED,SAVET,SETUP
   integer, dimension(4) :: HNAME
   data SETUP /0/

   public :: SHORT, LATNCY, SAVED, SAVET, SETUP
   private :: WKDAY, WKEND, HOLID, HBEGIN, HEND, HNAME, MAGIC, MAGNM

   public :: START, MAINT, HOURS, MOTD, POOF
   private :: WIZARD, HOURSX, NEWHRX

contains

!  WIZARDRY ROUTINES (START, MAINT, WIZARD, HOURS(X), NEWHRS(X), MOTD, POOF)

   logical function START()

!  CHECK TO SEE IF THIS IS "PRIME TIME".  IF SO, ONLY WIZARDS MAY PLAY, THOUGH
!  OTHERS MAY BE ALLOWED A SHORT GAME FOR DEMONSTRATION PURPOSES.  IF SETUP<0,
!  WE'RE CONTINUING FROM A SAVED GAME, SO CHECK FOR SUITABLE LATENCY.  RETURN
!  TRUE IF THIS IS A DEMO GAME (VALUE IS IGNORED FOR RESTARTS).

      INTEGER D,T,PRIMTM,DELAY
      LOGICAL PTIME,SOON,YESM

!  FIRST FIND OUT WHETHER IT IS PRIME TIME (SAVE IN PTIME) AND, IF RESTARTING,
!  WHETHER IT'S TOO SOON (SAVE IN SOON).  PRIME-TIME SPECS ARE IN WKDAY, WKEND,
!  AND HOLID; SEE MAINT ROUTINE FOR DETAILS.  LATNCY IS REQUIRED DELAY BEFORE
!  RESTARTING.  WIZARDS MAY CUT THIS TO A THIRD.

      CALL DATIME(D,T)
      PRIMTM=WKDAY
      IF(MOD(D,7).LE.1)PRIMTM=WKEND
      IF(D.GE.HBEGIN.AND.D.LE.HEND)PRIMTM=HOLID
      PTIME=IAND(PRIMTM,ISHFT(1,T/60)).NE.0
      SOON=.FALSE.
      IF(SETUP.GE.0)GOTO 20
      DELAY=(D-SAVED)*1440+(T-SAVET)
      IF(DELAY.GE.LATNCY)GOTO 20
      PRINT 10,DELAY
10    FORMAT(' THIS ADVENTURE WAS SUSPENDED A MERE',I3,' MINUTES AGO.')
      SOON=.TRUE.
      IF(DELAY.GE.LATNCY/3)GOTO 20
      CALL MSPEAK(2)
      STOP

!  IF NEITHER TOO SOON NOR PRIME TIME, NO PROBLEM.  ELSE SPECIFY WHAT'S WRONG.

20    START=.FALSE.
      IF(SOON)GOTO 30
      IF(PTIME)GOTO 25
22    SAVED=-1
      RETURN

!  COME HERE IF NOT RESTARTING TOO SOON (MAYBE NOT RESTARTING AT ALL), BUT IT'S
!  PRIME TIME.  GIVE OUR HOURS AND SEE IF HE'S A WIZARD.  IF NOT, THEN CAN'T
!  RESTART, BUT IF JUST BEGINNING THEN WE CAN OFFER A SHORT GAME.

25    CALL MSPEAK(3)
      CALL HOURS
      CALL MSPEAK(4)
      IF(WIZARD())GOTO 22
      IF(SETUP.LT.0)GOTO 33
      START=YESM(5,7,7)
      IF(START)GOTO 22
      STOP

!  COME HERE IF RESTARTING TOO SOON.  IF HE'S A WIZARD, LET HIM GO (AND NOTE
!  THAT IT THEN DOESN'T MATTER WHETHER IT'S PRIME TIME).  ELSE, TOUGH BEANS.

30    CALL MSPEAK(8)
      IF(WIZARD())GOTO 22
33    CALL MSPEAK(9)
      STOP
   end function START


   subroutine MAINT

!  SOMEONE SAID THE MAGIC WORD TO INVOKE MAINTENANCE MODE.  MAKE SURE HE'S A
!  WIZARD.  IF SO, LET HIM TWEAK ALL SORTS OF RANDOM THINGS, THEN EXIT SO CAN
!  SAVE TWEAKED VERSION.  SINCE MAGIC WORD MUST BE FIRST COMMAND GIVEN, ONLY
!  THING WHICH NEEDS TO BE FIXED UP IS ABB(1).
      INTEGER D,T,X,Y
      COMMON /BLKCOM/ BLKLIN
      LOGICAL BLKLIN
      COMMON /ABBCOM/ ABB
      integer, dimension(150) :: ABB
      integer, external :: IA5
      logical, external :: YESM

      IF(.NOT.WIZARD())RETURN
      BLKLIN=.FALSE.
      IF(YESM(10,0,0))CALL HOURS
      IF(YESM(11,0,0))CALL NEWHRS
      IF (YESM(26,0,0)) THEN
         CALL MSPEAK(27)
         READ 1,HBEGIN
1        FORMAT(I4)
         CALL MSPEAK(28)
         READ 1,HEND
         CALL DATIME(D,T)
         HBEGIN=HBEGIN+D
         HEND=HBEGIN+HEND-1
         CALL MSPEAK(29)
         READ 2,HNAME
2        FORMAT(4A5)
      END IF
      PRINT 12,SHORT
12    FORMAT(' LENGTH OF SHORT GAME (NULL TO LEAVE AT',I3,'):')
      READ 1,X
      IF(X.GT.0)SHORT=X
      CALL MSPEAK(12)
      CALL GETIN(X,Y,Y,Y)
      IF(X.NE.IA5(' '))MAGIC=X
      CALL MSPEAK(13)
      READ 1,X
      IF(X.GT.0)MAGNM=X
      PRINT 16,LATNCY
16    FORMAT(' LATENCY FOR RESTART (NULL TO LEAVE AT',I3,'):')
      READ 1,X
      IF(X.GT.0.AND.X.LT.45)CALL MSPEAK(30)
      IF(X.GT.0)LATNCY=MAX(45,X)
      IF(YESM(14,0,0))CALL MOTD(.TRUE.)
      SAVED=0
      SETUP=2
      ABB(1)=0
      CALL MSPEAK(15)
      BLKLIN=.TRUE.
      CALL CIAO
   end subroutine MAINT


   logical function WIZARD()

!  ASK IF HE'S A WIZARD.  IF HE SAYS YES, MAKE HIM PROVE IT.  RETURN TRUE IF HE
!  REALLY IS A WIZARD.

      integer :: WORD,X,Y,Z,D,T
      integer, dimension(5) :: VAL
      logical, external :: YESM
      integer, external :: IA5
      character*5, external :: A5I

      WIZARD=YESM(16,0,7)
      IF(.NOT.WIZARD)RETURN

!  HE SAYS HE IS.  FIRST STEP: DOES HE KNOW ANYTHING MAGICAL?

      CALL MSPEAK(17)
      CALL GETIN(WORD,X,Y,Z)
      IF(WORD.NE.MAGIC)GOTO 99

!  HE DOES.  GIVE HIM A RANDOM CHALLENGE AND CHECK HIS REPLY.

      CALL DATIME(D,T)
      T=T*2+1
      WORD=IA5('@@@@@')
      DO Y=1,5
         X=79+MOD(D,5)
         D=D/5
         DO Z=1,X
            T=MOD(T*1027,1048576)
         END DO
         VAL(Y)=(T*26)/1048576+1
         WORD=WORD+ISHFT(VAL(Y),36-7*Y)
      END DO
      IF(YESM(18,0,0))GOTO 99
      PRINT 18,A5I(WORD)
18    FORMAT(/1X,A5)
      CALL GETIN(WORD,X,Y,Z)
      CALL DATIME(D,T)
      T=(T/60)*40+(T/10)*10
      D=MAGNM
      DO Y=1,5
         Z=MOD(Y,5)+1
!  mkg made puzzle time independent
         X=MOD(ABS(VAL(Y)-VAL(Z))*MOD(D,10),26)+1
         T=T/10
         D=D/10
         WORD=WORD-ISHFT(X,36-7*Y)
      END DO
      IF(WORD.NE.IA5('@@@@@'))GOTO 99

!  BY GEORGE, HE REALLY *IS* A WIZARD!

      CALL MSPEAK(19)
      RETURN

!  AHA!  AN IMPOSTOR!

99    CALL MSPEAK(20)
      WIZARD=.FALSE.
      RETURN
   end function WIZARD


   subroutine HOURS

!  ANNOUNCE THE CURRENT HOURS WHEN THE CAVE IS OPEN FOR ADVENTURING.  THIS INFO
!  IS STORED IN WKDAY, WKEND, AND HOLID, WHERE BIT ISHFT(1,N) IS ON IFF THE
!  HOUR FROM N:00 TO N:59 IS "PRIME TIME" (CAVE CLOSED).  WKDAY IS FOR
!  WEEKDAYS, WKEND FOR WEEKENDS, HOLID FOR HOLIDAYS.  NEXT HOLIDAY IS FROM
!  HBEGIN TO HEND.

      INTEGER D,T
      integer, dimension(5) :: VAL
      integer, external :: IA5

      PRINT 1
1     FORMAT()
      CALL HOURSX(WKDAY,'MON -',' FRI:')
      CALL HOURSX(WKEND,'SAT -',' SUN:')
      CALL HOURSX(HOLID,'HOLID','AYS: ')
      CALL DATIME(D,T)
      IF(HEND.LT.D.OR.HEND.LT.HBEGIN)RETURN
      IF (HBEGIN.LE.D) THEN
         PRINT 5,HNAME
5        FORMAT(/' TODAY IS A HOLIDAY, NAMELY ',4A5)
         RETURN
      END IF
      D=HBEGIN-D
      T=IA5('DAYS,')
      IF(D.EQ.1)T=IA5('DAY, ')
      PRINT 15,D,T,HNAME
15    FORMAT(/' THE NEXT HOLIDAY WILL BE IN',I3,' ',A5,' NAMELY ',4A5)
      RETURN
   end subroutine HOURS


   subroutine HOURSX(H,DAY1,DAY2)

!  USED BY HOURS (ABOVE) TO PRINT HOURS FOR EITHER WEEKDAYS OR WEEKENDS.

      integer, intent(in) :: H
      character*5, intent(in) :: DAY1, DAY2
      LOGICAL FIRST
      INTEGER FROM,TILL

      FIRST=.TRUE.
      FROM=-1
      IF (H.EQ.0) THEN
         PRINT 2,DAY1,DAY2
2        FORMAT(10X,2A5,'  OPEN ALL DAY')
         RETURN
      END IF
10    FROM=FROM+1
      IF(IAND(H,ISHFT(1,FROM)).NE.0)GOTO 10
      IF (FROM.LT.24) THEN
         TILL=FROM
14       TILL=TILL+1
         IF(IAND(H,ISHFT(1,TILL)).EQ.0.AND.TILL.NE.24)GOTO 14
         IF (FIRST) THEN
            PRINT 16,DAY1,DAY2,FROM,TILL
16          FORMAT(10X,2A5,I4,':00 TO',I3,':00')
         ELSE
            PRINT 18,FROM,TILL
18          FORMAT(20X,I4,':00 TO',I3,':00')
         END IF
         FIRST=.FALSE.
         FROM=TILL
         GOTO 10
      END IF
20    IF(FIRST)PRINT 22,DAY1,DAY2
22    FORMAT(10X,2A5,'  CLOSED ALL DAY')
      RETURN
   end subroutine HOURSX


   subroutine NEWHRS

!  SET UP NEW HOURS FOR THE CAVE.  SPECIFIED AS INVERSE--I.E., WHEN IS IT
!  CLOSED DUE TO PRIME TIME?  SEE HOURS (ABOVE) FOR DESC OF VARIABLES.

      CALL MSPEAK(21)
      WKDAY=NEWHRX('WEEKD','AYS: ')
      WKEND=NEWHRX('WEEKE','NDS: ')
      HOLID=NEWHRX('HOLID','AYS: ')
      CALL MSPEAK(22)
      CALL HOURS
      RETURN
   end subroutine NEWHRS


   integer function NEWHRX(DAY1,DAY2)

!  INPUT PRIME TIME SPECS AND SET UP A WORD OF INTERNAL FORMAT.

      character*5, intent(in) :: DAY1, DAY2
      INTEGER FROM,TILL,I

      NEWHRX=0
      PRINT 1,DAY1,DAY2
1     FORMAT(' PRIME TIME ON ',2A5)
10    PRINT 2
2     FORMAT(' FROM:')
      READ 3,FROM
3     FORMAT(I4)
      IF(FROM.LT.0.OR.FROM.GE.24)RETURN
      PRINT 4
4     FORMAT(' TILL:')
      READ 3,TILL
      TILL=TILL-1
      IF(TILL.LT.FROM.OR.TILL.GE.24)RETURN
      DO I=FROM,TILL
         NEWHRX=IOR(NEWHRX,ISHFT(1,I))
      END DO
      GOTO 10
   end function NEWHRX


   subroutine MOTD(ALTER)

!  HANDLES MESSAGE OF THE DAY.  IF ALTER IS TRUE, READ A NEW MESSAGE FROM THE
!  WIZARD.  ELSE PRINT THE CURRENT ONE.  MESSAGE IS INITIALLY NULL.

      logical, intent(in) :: ALTER
      integer :: I, K, M
      integer, dimension(100) :: MSG
      DATA MSG/100*-1/
      integer, external :: IA5

      IF(ALTER)GOTO 50

      K=1
10    IF(MSG(K).LT.0)RETURN
      PRINT 20,(MSG(I),I=K+1,MSG(K)-1)
20    FORMAT(' ',14A5)
      K=MSG(K)
      GOTO 10

50    M=1
      CALL MSPEAK(23)
55    READ 56,(MSG(I),I=M+1,M+14),K
56    FORMAT(15A5)
      IF(K.EQ.IA5('     '))GOTO 60
      CALL MSPEAK(24)
      GOTO 55
60    DO I=1,14
         K=M+15-I
         IF(MSG(K).NE.IA5('     '))GOTO 65
      END DO
      GOTO 90
65    MSG(M)=K+1
      M=K+1
      IF(M+14.LT.100)GOTO 55
      CALL MSPEAK(25)
90    MSG(M)=-1
      RETURN
   end subroutine MOTD


   subroutine POOF

!  AS PART OF DATABASE INITIALISATION, WE CALL POOF TO SET UP SOME DUMMY
!  PRIME-TIME SPECS, MAGIC WORDS, ETC.

      integer, external :: IA5

      WKDAY=O"00777400"
      WKEND=0
      HOLID=0
      HBEGIN=0
      HEND=-1
      SHORT=30
      MAGIC=IA5('DWARF')
      MAGNM=11111
      LATNCY=90
      RETURN
   end subroutine POOF

end module wizcom
