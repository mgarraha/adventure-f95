!  ADVENTURES

!  CURRENT LIMITS:
!    9650 WORDS OF MESSAGE TEXT (LINES, LINSIZ).
!     750 TRAVEL OPTIONS (TRAVEL, TRVSIZ).
!     300 VOCABULARY WORDS (KTAB, ATAB, TABSIZ).
!     150 LOCATIONS (LTEXT, STEXT, KEY, COND, ABB, ATLOC, LOCSIZ).
!     100 OBJECTS (PLAC, PLACE, FIXD, FIXED, LINK (TWICE), PTEXT, PROP).
!      35 "ACTION" VERBS (ACTSPK, VRBSIZ).
!     205 RANDOM MESSAGES (RTEXT, RTXSIZ).
!      12 DIFFERENT PLAYER CLASSIFICATIONS (CTEXT, CVAL, CLSMAX).
!      20 HINTS, LESS 3 (HINTLC, HINTED, HINTS, HNTSIZ).
!      35 MAGIC MESSAGES (MTEXT, MAGSIZ).
!  THERE ARE ALSO LIMITS WHICH CANNOT BE EXCEEDED DUE TO THE STRUCTURE OF
!  THE DATABASE.  (E.G., THE VOCABULARY USES N/1000 TO DETERMINE WORD TYPE,
!  SO THERE CAN'T BE MORE THAN 1000 WORDS.)  THESE UPPER LIMITS ARE:
!    1000 NON-SYNONYMOUS VOCABULARY WORDS
!     300 LOCATIONS
!     100 OBJECTS

program advent
      use wizcom
      implicit none
      CHARACTER*80 TXT
      CHARACTER*5 WORD
      logical, external :: YES
      integer, external :: IA5,VOCAB,PUT,RANI

      COMMON /TXTCOM/ RTEXT,LINES
      INTEGER RTEXT,LINES
      DIMENSION RTEXT(205),LINES(9650)

      COMMON /BLKCOM/ BLKLIN
      LOGICAL BLKLIN
      DATA BLKLIN/.TRUE./

      COMMON /VOCCOM/ KTAB,ATAB,TABSIZ
      INTEGER KTAB,ATAB,TABSIZ
      DIMENSION KTAB(300),ATAB(300)
      DATA TABSIZ/300/

      COMMON /PLACOM/ ATLOC,LINK,PLACE,FIXED,HOLDNG
      INTEGER ATLOC,LINK,PLACE,FIXED,HOLDNG
      DIMENSION ATLOC(150),LINK(200),PLACE(100),FIXED(100)

      COMMON /MTXCOM/ MTEXT
      INTEGER MTEXT
      DIMENSION MTEXT(35)

      COMMON /PTXCOM/ PTEXT
      INTEGER PTEXT
      DIMENSION PTEXT(100)

      COMMON /ABBCOM/ ABB
      INTEGER ABB
      DIMENSION ABB(150)

      INTEGER TRAVEL,LTEXT,STEXT,KEY,COND
      INTEGER PLAC,FIXD,PROP
      INTEGER ACTSPK,CTEXT,CVAL
      INTEGER HINTLC,HINTS
      LOGICAL HINTED
      INTEGER TK,DLOC,ODLOC
      LOGICAL DSEEN
      CHARACTER*1 CAT
      DIMENSION TRAVEL(750)
      DIMENSION LTEXT(150),STEXT(150),KEY(150),COND(150)
      DIMENSION PLAC(100),FIXD(100),PROP(100)
      DIMENSION ACTSPK(35)
      DIMENSION CTEXT(12),CVAL(12)
      DIMENSION HINTLC(20),HINTED(20),HINTS(20,4)
      DIMENSION TK(20),DSEEN(6),DLOC(6),ODLOC(6)
      DIMENSION CAT(20)

      INTEGER LINSIZ,TRVSIZ,LOCSIZ,VRBSIZ,RTXSIZ,CLSMAX,HNTSIZ,MAGSIZ
      DATA LINSIZ/9650/,TRVSIZ/750/,LOCSIZ/150/,  &
              VRBSIZ/35/,RTXSIZ/205/,CLSMAX/12/,HNTSIZ/20/,MAGSIZ/35/

      INTEGER FOO,FOOBAR
      INTEGER I,J,K,L,M,N
      INTEGER KK,K2,KQ,LL
      INTEGER EOL,IOE,TAB
      INTEGER ABBNUM,ATTACK,BONUS,CLSSES,DETAIL
      INTEGER OBJ,OLDOBJ
      INTEGER LOC,CHLOC,CHLOC2,KNFLOC,NEWLOC,OLDLOC,OLDLC2
      INTEGER CLOCK1,CLOCK2
      INTEGER DFLAG,DKILL,DTOTAL,DALTLC,STICK
      INTEGER NUMDIE,MAXDIE
      INTEGER SCORE,MXSCOR
      INTEGER HINT,HNTMAX
      INTEGER WD1,WD1X,WD2,WD2X
      INTEGER BOTTLE,WATER,OIL
      INTEGER IWEST,LIMIT,LINUSE,MAXTRS,NULL,SECT,SPK,TABNDX
      INTEGER TALLY,TALLY2,TRVS,TURNS,VERB
      INTEGER XXD,XXT,YYD,YYT

      INTEGER AXE,BACK,BATTER,BEAR,BIRD,CAGE,CAVE,CHAIN,CHASM,CHEST
      INTEGER CLAM,COINS,DOOR,DPRSSN,DRAGON,DWARF,EGGS,EMRALD,ENTRNC
      INTEGER FIND,FISSUR,FOOD,GRATE,INVENT,KEYS,KNIFE,LAMP,LOCK,LOOK
      INTEGER MAGZIN,MESSAG,MIRROR,NUGGET,OYSTER,PEARL,PILLOW,PLANT
      INTEGER PLANT2,PYRAM,ROD,ROD2,RUG,SAY,SNAKE,SPICES,STEPS,TABLET
      INTEGER THROW,TRIDNT,TROLL,TROLL2,VASE,VEND

!  WZDARK SAYS WHETHER THE LOC HE'S LEAVING WAS DARK
!  LMWARN SAYS WHETHER HE'S BEEN WARNED ABOUT LAMP GOING DIM
!  CLOSNG SAYS WHETHER ITS CLOSING TIME YET
!  PANIC SAYS WHETHER HE'S FOUND OUT HE'S TRAPPED IN THE CAVE
!  CLOSED SAYS WHETHER WE'RE ALL THE WAY CLOSED
!  GAVEUP SAYS WHETHER HE EXITED VIA "QUIT"
!  SCORNG INDICATES TO THE SCORE ROUTINE WHETHER WE'RE DOING A "SCORE" COMMAND
!  DEMO IS TRUE IF THIS IS A PRIME-TIME DEMONSTRATION GAME
!  YEA IS RANDOM YES/NO REPLY

      LOGICAL WZDARK,LMWARN,CLOSNG,PANIC,CLOSED,GAVEUP,SCORNG,DEMO,YEA

!  DESCRIPTION OF THE DATABASE FORMAT
!
!
!  THE DATA FILE CONTAINS SEVERAL SECTIONS.  EACH BEGINS WITH A LINE CONTAINING
!  A NUMBER IDENTIFYING THE SECTION, AND ENDS WITH A LINE CONTAINING "-1".
!
!  SECTION 1: LONG FORM DESCRIPTIONS.  EACH LINE CONTAINS A LOCATION NUMBER,
!     A TAB, AND A LINE OF TEXT.  THE SET OF (NECESSARILY ADJACENT) LINES
!     WHOSE NUMBERS ARE X FORM THE LONG DESCRIPTION OF LOCATION X.
!  SECTION 2: SHORT FORM DESCRIPTIONS.  SAME FORMAT AS LONG FORM.  NOT ALL
!     PLACES HAVE SHORT DESCRIPTIONS.
!  SECTION 3: TRAVEL TABLE.  EACH LINE CONTAINS A LOCATION NUMBER (X), A SECOND
!     LOCATION NUMBER (Y), AND A LIST OF MOTION NUMBERS (SEE SECTION 4).
!     EACH MOTION REPRESENTS A VERB WHICH WILL GO TO Y IF CURRENTLY AT X.
!     Y, IN TURN, IS INTERPRETED AS FOLLOWS.  LET M=Y/1000, N=Y MOD 1000.
!           IF N<=300       IT IS THE LOCATION TO GO TO.
!           IF 300<N<=500   N-300 IS USED IN A COMPUTED GOTO TO
!                                 A SECTION OF SPECIAL CODE.
!           IF N>500        MESSAGE N-500 FROM SECTION 6 IS PRINTED,
!                                 AND HE STAYS WHEREVER HE IS.
!     MEANWHILE, M SPECIFIES THE CONDITIONS ON THE MOTION.
!           IF M=0          IT'S UNCONDITIONAL.
!           IF 0<M<100      IT IS DONE WITH M% PROBABILITY.
!           IF M=100        UNCONDITIONAL, BUT FORBIDDEN TO DWARVES.
!           IF 100<M<=200   HE MUST BE CARRYING OBJECT M-100.
!           IF 200<M<=300   MUST BE CARRYING OR IN SAME ROOM AS M-200.
!           IF 300<M<=400   PROP(M MOD 100) MUST *NOT* BE 0.
!           IF 400<M<=500   PROP(M MOD 100) MUST *NOT* BE 1.
!           IF 500<M<=600   PROP(M MOD 100) MUST *NOT* BE 2, ETC.
!     IF THE CONDITION (IF ANY) IS NOT MET, THEN THE NEXT *DIFFERENT*
!     "DESTINATION" VALUE IS USED (UNLESS IT FAILS TO MEET *ITS* CONDITIONS,
!     IN WHICH CASE THE NEXT IS FOUND, ETC.).  TYPICALLY, THE NEXT DEST WILL
!     BE FOR ONE OF THE SAME VERBS, SO THAT ITS ONLY USE IS AS THE ALTERNATE
!     DESTINATION FOR THOSE VERBS.  FOR INSTANCE:
!           15      110022  29      31      34      35      23      43
!           15      14      29
!     THIS SAYS THAT, FROM LOC 15, ANY OF THE VERBS 29, 31, ETC., WILL TAKE
!     HIM TO 22 IF HE'S CARRYING OBJECT 10, AND OTHERWISE WILL GO TO 14.
!           11      303008  49
!           11      9       50
!     THIS SAYS THAT, FROM 11, 49 TAKES HIM TO 8 UNLESS PROP(3)=0, IN WHICH
!     CASE HE GOES TO 9.  VERB 50 TAKES HIM TO 9 REGARDLESS OF PROP(3).
!  SECTION 4: VOCABULARY.  EACH LINE CONTAINS A NUMBER (N), A TAB, AND A
!     FIVE-LETTER WORD.  CALL M=N/1000.  IF M=0, THEN THE WORD IS A MOTION
!     VERB FOR USE IN TRAVELLING (SEE SECTION 3).  ELSE, IF M=1, THE WORD IS
!     AN OBJECT.  ELSE, IF M=2, THE WORD IS AN ACTION VERB (SUCH AS "CARRY"
!     OR "ATTACK").  ELSE, IF M=3, THE WORD IS A SPECIAL CASE VERB (SUCH AS
!     "DIG") AND N MOD 1000 IS AN INDEX INTO SECTION 6.  OBJECTS FROM 50 TO
!     (CURRENTLY, ANYWAY) 79 ARE CONSIDERED TREASURES (FOR PIRATE, CLOSEOUT).
!  SECTION 5: OBJECT DESCRIPTIONS.  EACH LINE CONTAINS A NUMBER (N), A TAB,
!     AND A MESSAGE.  IF N IS FROM 1 TO 100, THE MESSAGE IS THE "INVENTORY"
!     MESSAGE FOR OBJECT N.  OTHERWISE, N SHOULD BE 000, 100, 200, ETC., AND
!     THE MESSAGE SHOULD BE THE DESCRIPTION OF THE PRECEDING OBJECT WHEN ITS
!     PROP VALUE IS N/100.  THE N/100 IS USED ONLY TO DISTINGUISH MULTIPLE
!     MESSAGES FROM MULTI-LINE MESSAGES; THE PROP INFO ACTUALLY REQUIRES ALL
!     MESSAGES FOR AN OBJECT TO BE PRESENT AND CONSECUTIVE.  PROPERTIES WHICH
!     PRODUCE NO MESSAGE SHOULD BE GIVEN THE MESSAGE ">$<".
!  SECTION 6: ARBITRARY MESSAGES.  SAME FORMAT AS SECTIONS 1, 2, AND 5, EXCEPT
!     THE NUMBERS BEAR NO RELATION TO ANYTHING (EXCEPT FOR SPECIAL VERBS
!     IN SECTION 4).
!  SECTION 7: OBJECT LOCATIONS.  EACH LINE CONTAINS AN OBJECT NUMBER AND ITS
!     INITIAL LOCATION (ZERO (OR OMITTED) IF NONE).  IF THE OBJECT IS
!     IMMOVABLE, THE LOCATION IS FOLLOWED BY A "-1".  IF IT HAS TWO LOCATIONS
!     (E.G. THE GRATE) THE FIRST LOCATION IS FOLLOWED WITH THE SECOND, AND
!     THE OBJECT IS ASSUMED TO BE IMMOVABLE.
!  SECTION 8: ACTION DEFAULTS.  EACH LINE CONTAINS AN "ACTION-VERB" NUMBER AND
!     THE INDEX (IN SECTION 6) OF THE DEFAULT MESSAGE FOR THE VERB.
!  SECTION 9: LIQUID ASSETS, ETC.  EACH LINE CONTAINS A NUMBER (N) AND UP TO 20
!     LOCATION NUMBERS.  BIT N (WHERE 0 IS THE UNITS BIT) IS SET IN COND(LOC)
!     FOR EACH LOC GIVEN.  THE COND BITS CURRENTLY ASSIGNED ARE:
!           0     LIGHT
!           1     IF BIT 2 IS ON: ON FOR OIL, OFF FOR WATER
!           2     LIQUID ASSET, SEE BIT 1
!           3     PIRATE DOESN'T GO HERE UNLESS FOLLOWING PLAYER
!     OTHER BITS ARE USED TO INDICATE AREAS OF INTEREST TO "HINT" ROUTINES:
!           4     TRYING TO GET INTO CAVE
!           5     TRYING TO CATCH BIRD
!           6     TRYING TO DEAL WITH SNAKE
!           7     LOST IN MAZE
!           8     PONDERING DARK ROOM
!           9     AT WITT'S END
!     COND(LOC) IS SET TO 2, OVERRIDING ALL OTHER BITS, IF LOC HAS FORCED
!     MOTION.
!  SECTION 10: CLASS MESSAGES.  EACH LINE CONTAINS A NUMBER (N), A TAB, AND A
!     MESSAGE DESCRIBING A CLASSIFICATION OF PLAYER.  THE SCORING SECTION
!     SELECTS THE APPROPRIATE MESSAGE, WHERE EACH MESSAGE IS CONSIDERED TO
!     APPLY TO PLAYERS WHOSE SCORES ARE HIGHER THAN THE PREVIOUS N BUT NOT
!     HIGHER THAN THIS N.  NOTE THAT THESE SCORES PROBABLY CHANGE WITH EVERY
!     MODIFICATION (AND PARTICULARLY EXPANSION) OF THE PROGRAM.
!  SECTION 11: HINTS.  EACH LINE CONTAINS A HINT NUMBER (CORRESPONDING TO A
!     COND BIT, SEE SECTION 9), THE NUMBER OF TURNS HE MUST BE AT THE RIGHT
!     LOC(S) BEFORE TRIGGERING THE HINT, THE POINTS DEDUCTED FOR TAKING THE
!     HINT, THE MESSAGE NUMBER (SECTION 6) OF THE QUESTION, AND THE MESSAGE
!     NUMBER OF THE HINT.  THESE VALUES ARE STASHED IN THE "HINTS" ARRAY.
!     HNTMAX IS SET TO THE MAX HINT NUMBER (<= HNTSIZ).  NUMBERS 1-3 ARE
!     UNUSABLE SINCE COND BITS ARE OTHERWISE ASSIGNED, SO 2 IS USED TO
!     REMEMBER IF HE'S READ THE CLUE IN THE REPOSITORY, AND 3 IS USED TO
!     REMEMBER WHETHER HE ASKED FOR INSTRUCTIONS (GETS MORE TURNS, BUT LOSES
!     POINTS).
!  SECTION 12: MAGIC MESSAGES. IDENTICAL TO SECTION 6 EXCEPT PUT IN A SEPARATE
!     SECTION FOR EASIER REFERENCE.  MAGIC MESSAGES ARE USED BY THE STARTUP,
!     MAINTENANCE MODE, AND RELATED ROUTINES.
!  SECTION 0: END OF DATABASE.
!  READ THE DATABASE IF WE HAVE NOT YET DONE SO

      IF(SETUP.NE.0)GOTO 1100
      PRINT 1000
1000  FORMAT(' INITIALISING...')

!  CLEAR OUT THE VARIOUS TEXT-POINTER ARRAYS.  ALL TEXT IS STORED IN ARRAY
!  LINES; EACH LINE IS PRECEDED BY A WORD POINTING TO THE NEXT POINTER (I.E.
!  THE WORD FOLLOWING THE END OF THE LINE).  THE POINTER IS NEGATIVE IF THIS IS
!  FIRST LINE OF A MESSAGE.  THE TEXT-POINTER ARRAYS CONTAIN INDICES OF
!  POINTER-WORDS IN LINES.  STEXT(N) IS SHORT DESCRIPTION OF LOCATION N.
!  LTEXT(N) IS LONG DESCRIPTION.  PTEXT(N) POINTS TO MESSAGE FOR PROP(N)=0.
!  SUCCESSIVE PROP MESSAGES ARE FOUND BY CHASING POINTERS.  RTEXT CONTAINS
!  SECTION 6'S STUFF.  CTEXT(N) POINTS TO A PLAYER-CLASS MESSAGE.  MTEXT IS FOR
!  SECTION 12.  WE ALSO CLEAR COND.  SEE DESCRIPTION OF SECTION 9 FOR DETAILS.

      DO I=1,300
         IF(I.LE.100)PTEXT(I)=0
         IF(I.LE.RTXSIZ)RTEXT(I)=0
         IF(I.LE.CLSMAX)CTEXT(I)=0
         IF(I.LE.MAGSIZ)MTEXT(I)=0
         IF (I.GT.LOCSIZ) CYCLE
         STEXT(I)=0
         LTEXT(I)=0
         COND(I)=0
         KEY(I)=0
      END DO
      PLAC=0
      FIXD=0

      OPEN(1,FILE='adven.dat')
      SETUP=1
      LINUSE=1
      TRVS=1
      CLSSES=1

!  START NEW DATA SECTION.  SECT IS THE SECTION NUMBER.

1002  READ(1,1003)SECT
1003  FORMAT(I2)
      OLDLOC=-1
      select case (SECT)
         case (0)
            GOTO 1100
         case (3)
            GOTO 1030
         case (4)
            GOTO 1040
         case (7)
            GOTO 1050
         case (8)
            GOTO 1060
         case (9)
            GOTO 1070
         case (11)
            GOTO 1080
         case (1, 2, 5, 6, 10, 12)
            GOTO 1004
         case default
            CALL BUG(9)
      end select

!  SECTIONS 1, 2, 5, 6, 10, 12.  READ MESSAGES AND SET UP POINTERS.

1004  READ(1,'(A80)')TXT
      TAB=INDEX(TXT,ACHAR(9))
      IF(TAB.EQ.0)TAB=LEN_TRIM(TXT)+1
      EOL=LEN_TRIM(TXT)
      READ(TXT(:TAB-1),'(I4)')LOC
      IF(LOC.EQ.-1)GOTO 1002
      DO J=1,15
         K=TAB+5*J
         IF (K-4.GT.EOL) THEN
            LINES(LINUSE+J)=IA5('     ')
         ELSE
            WORD=TXT(K-4:K)
            LINES(LINUSE+J)=IA5(WORD)
         END IF
      END DO
      KK=LINES(LINUSE+15)
!      READ(TXT(TAB+1:),1005)(LINES(J),J=LINUSE+1,LINUSE+14),KK
!1005  FORMAT(15A5)
      IF(KK.NE.IA5('     '))CALL BUG(0)
      DO K=1,14
         KK=LINUSE+15-K
         IF(LINES(KK).NE.IA5('     '))GOTO 1007
      END DO
      IF(LOC.EQ.0)GOTO 1004
!  ABOVE KLUGE IS TO AVOID F40 BUG IF CRLF BROKEN ACROSS RECORD BOUNDARY
      CALL BUG(1)
1007  LINES(LINUSE)=KK+1
      IF (LOC.NE.OLDLOC) THEN
         LINES(LINUSE)=-LINES(LINUSE)
         select case (SECT)
            case (1)
               LTEXT(LOC)=LINUSE
            case (5)
               IF(LOC.GT.0.AND.LOC.LE.100)PTEXT(LOC)=LINUSE
            case (6)
               IF(LOC.GT.RTXSIZ)CALL BUG(6)
               RTEXT(LOC)=LINUSE
            case (10)
               CTEXT(CLSSES)=LINUSE
               CVAL(CLSSES)=LOC
               CLSSES=CLSSES+1
            case (12)
               IF(LOC.GT.MAGSIZ)CALL BUG(6)
               MTEXT(LOC)=LINUSE
            case default
               STEXT(LOC)=LINUSE
         end select
      END IF

      LINUSE=KK+1
      LINES(LINUSE)=-1
      OLDLOC=LOC
      IF(LINUSE+14.GT.LINSIZ)CALL BUG(2)
      GOTO 1004

!  THE STUFF FOR SECTION 3 IS ENCODED HERE.  EACH "FROM-LOCATION" GETS A
!  CONTIGUOUS SECTION OF THE "TRAVEL" ARRAY.  EACH ENTRY IN TRAVEL IS
!  NEWLOC*1000 + KEYWORD (FROM SECTION 4, MOTION VERBS), AND IS NEGATED IF
!  THIS IS THE LAST ENTRY FOR THIS LOCATION.  KEY(N) IS THE INDEX IN TRAVEL
!  OF THE FIRST OPTION AT LOCATION N.

1030  READ(1,'(A80)')TXT
      TK=0
      READ(TXT,*,IOSTAT=IOE)LOC,NEWLOC,TK
1031  FORMAT(99I4)
      IF(LOC.EQ.0)GOTO 1030
!  ABOVE KLUGE IS TO AVOID AFOREMENTIONED F40 BUG
      IF(LOC.EQ.-1)GOTO 1002
      IF (KEY(LOC).EQ.0) THEN
         KEY(LOC)=TRVS
      ELSE
         TRAVEL(TRVS-1)=-TRAVEL(TRVS-1)
      END IF
      DO L=1,20
         IF (TK(L).EQ.0) EXIT
         TRAVEL(TRVS)=NEWLOC*1000+TK(L)
         TRVS=TRVS+1
         IF(TRVS.EQ.TRVSIZ)CALL BUG(3)
      END DO
      TRAVEL(TRVS-1)=-TRAVEL(TRVS-1)
      GOTO 1030

!  HERE WE READ IN THE VOCABULARY.  KTAB(N) IS THE WORD NUMBER, ATAB(N) IS
!  THE CORRESPONDING WORD.  THE -1 AT THE END OF SECTION 4 IS LEFT IN KTAB
!  AS AN END-MARKER.  THE WORDS ARE GIVEN A MINIMAL HASH TO MAKE READING THE
!  CORE-IMAGE HARDER.  NOTE THAT '/7-08' HAD BETTER NOT BE IN THE LIST, SINCE
!  IT COULD HASH TO -1.

1040  DO TABNDX=1,TABSIZ
1043     READ(1,'(A80)')TXT
         READ(TXT,*,IOSTAT=IOE)KTAB(TABNDX),WORD
         ATAB(TABNDX)=IA5(WORD)
1041     FORMAT(I2,A5)
         IF(KTAB(TABNDX).EQ.0)GOTO 1043
!  ABOVE KLUGE IS TO AVOID AFOREMENTIONED F40 BUG
         IF(KTAB(TABNDX).EQ.-1)GOTO 1002
         ATAB(TABNDX)=IEOR(ATAB(TABNDX),IA5('PHROG'))
      END DO
      CALL BUG(4)

!  READ IN THE INITIAL LOCATIONS FOR EACH OBJECT.  ALSO THE IMMOVABILITY INFO.
!  PLAC CONTAINS INITIAL LOCATIONS OF OBJECTS.  FIXD IS -1 FOR IMMOVABLE
!  OBJECTS (INCLUDING THE SNAKE), OR = SECOND LOC FOR TWO-PLACED OBJECTS.

1050  READ(1,'(A80)')TXT
      K=0
      READ(TXT,*,IOSTAT=IOE)OBJ,J,K
      IF(OBJ.EQ.-1)GOTO 1002
      PLAC(OBJ)=J
      FIXD(OBJ)=K
      GOTO 1050

!  READ DEFAULT MESSAGE NUMBERS FOR ACTION VERBS, STORE IN ACTSPK.

1060  READ(1,'(A80)')TXT
      READ(TXT,*,IOSTAT=IOE)VERB,J
      IF(VERB.EQ.-1)GOTO 1002
      ACTSPK(VERB)=J
      GOTO 1060

!  READ INFO ABOUT AVAILABLE LIQUIDS AND OTHER CONDITIONS, STORE IN COND.

1070  READ(1,'(A80)')TXT
      TK=0
      READ(TXT,*,IOSTAT=IOE)K,TK
      IF(K.EQ.-1)GOTO 1002
      DO I=1,20
         LOC=TK(I)
         IF (LOC.EQ.0) EXIT
         IF(BITSET(LOC,K))CALL BUG(8)
         COND(LOC)=COND(LOC)+ISHFT(1,K)
      END DO
      GOTO 1070

!  READ DATA FOR HINTS.

1080  HNTMAX=0
1081  READ(1,'(A80)')TXT
      TK=0
      READ(TXT,*,IOSTAT=IOE)K,TK
      IF(K.EQ.-1)GOTO 1002
      IF(K.EQ.0)GOTO 1081
      IF(K.LT.0.OR.K.GT.HNTSIZ)CALL BUG(7)
      DO I=1,4
         HINTS(K,I)=TK(I)
      END DO
      HNTMAX=MAX(HNTMAX,K)
      GOTO 1081
!  FINISH CONSTRUCTING INTERNAL DATA FORMAT

!  IF SETUP=2 WE DON'T NEED TO DO THIS.  IT'S ONLY NECESSARY IF WE HAVEN'T DONE
!  IT AT ALL OR IF THE PROGRAM HAS BEEN RUN SINCE THEN.

1100  IF(SETUP.EQ.2)GOTO 1
      IF(SETUP.EQ.-1)GOTO 8305

!  HAVING READ IN THE DATABASE, CERTAIN THINGS ARE NOW CONSTRUCTED.  PROPS ARE
!  SET TO ZERO.  WE FINISH SETTING UP COND BY CHECKING FOR FORCED-MOTION TRAVEL
!  ENTRIES.  THE PLAC AND FIXD ARRAYS ARE USED TO SET UP ATLOC(N) AS THE FIRST
!  OBJECT AT LOCATION N, AND LINK(OBJ) AS THE NEXT OBJECT AT THE SAME LOCATION
!  AS OBJ.  (OBJ>100 INDICATES THAT FIXED(OBJ-100)=LOC; LINK(OBJ) IS STILL THE
!  CORRECT LINK TO USE.)  ABB IS ZEROED; IT CONTROLS WHETHER THE ABBREVIATED
!  DESCRIPTION IS PRINTED.  COUNTS MOD 5 UNLESS "LOOK" IS USED.

      DO I=1,100
         PLACE(I)=0
         PROP(I)=0
         LINK(I)=0
         LINK(I+100)=0
      END DO

      DO I=1,LOCSIZ
         ABB(I)=0
         IF (LTEXT(I).NE.0 .AND. KEY(I).NE.0) THEN
            K=KEY(I)
            IF(MOD(ABS(TRAVEL(K)),1000).EQ.1)COND(I)=2
         END IF
         ATLOC(I)=0
      END DO

!  SET UP THE ATLOC AND LINK ARRAYS AS DESCRIBED ABOVE.  WE'LL USE THE DROP
!  SUBROUTINE, WHICH PREFACES NEW OBJECTS ON THE LISTS.  SINCE WE WANT THINGS
!  IN THE OTHER ORDER, WE'LL RUN THE LOOP BACKWARDS.  IF THE OBJECT IS IN TWO
!  LOCS, WE DROP IT TWICE.  THIS ALSO SETS UP "PLACE" AND "FIXED" AS COPIES OF
!  "PLAC" AND "FIXD".  ALSO, SINCE TWO-PLACED OBJECTS ARE TYPICALLY BEST
!  DESCRIBED LAST, WE'LL DROP THEM FIRST.

      DO I=1,100
         K=101-I
         IF (FIXD(K).LE.0) CYCLE
         CALL DROP(K+100,FIXD(K))
         CALL DROP(K,PLAC(K))
      END DO

      DO I=1,100
         K=101-I
         FIXED(K)=FIXD(K)
         IF(PLAC(K).NE.0.AND.FIXD(K).LE.0)CALL DROP(K,PLAC(K))
      END DO

!  TREASURES, AS NOTED EARLIER, ARE OBJECTS 50 THROUGH MAXTRS (CURRENTLY 79).
!  THEIR PROPS ARE INITIALLY -1, AND ARE SET TO 0 THE FIRST TIME THEY ARE
!  DESCRIBED.  TALLY KEEPS TRACK OF HOW MANY ARE NOT YET FOUND, SO WE KNOW
!  WHEN TO CLOSE THE CAVE.  TALLY2 COUNTS HOW MANY CAN NEVER BE FOUND (E.G. IF
!  LOST BIRD OR BRIDGE).

      MAXTRS=79
      TALLY=0
      TALLY2=0
      DO I=50,MAXTRS
         IF(PTEXT(I).NE.0)PROP(I)=-1
         TALLY=TALLY-PROP(I)
      END DO

!  CLEAR THE HINT STUFF.  HINTLC(I) IS HOW LONG HE'S BEEN AT LOC WITH COND BIT
!  I.  HINTED(I) IS TRUE IFF HINT I HAS BEEN USED.

      DO I=1,HNTMAX
         HINTED(I)=.FALSE.
         HINTLC(I)=0
      END DO

!  DEFINE SOME HANDY MNEMONICS.  THESE CORRESPOND TO OBJECT NUMBERS.

!  TODO IA5(EACH LITERAL)
      KEYS=VOCAB(IA5('KEYS '),1)
      LAMP=VOCAB(IA5('LAMP '),1)
      GRATE=VOCAB(IA5('GRATE'),1)
      CAGE=VOCAB(IA5('CAGE '),1)
      ROD=VOCAB(IA5('ROD  '),1)
      ROD2=ROD+1
      STEPS=VOCAB(IA5('STEPS'),1)
      BIRD=VOCAB(IA5('BIRD '),1)
      DOOR=VOCAB(IA5('DOOR '),1)
      PILLOW=VOCAB(IA5('PILLO'),1)
      SNAKE=VOCAB(IA5('SNAKE'),1)
      FISSUR=VOCAB(IA5('FISSU'),1)
      TABLET=VOCAB(IA5('TABLE'),1)
      CLAM=VOCAB(IA5('CLAM '),1)
      OYSTER=VOCAB(IA5('OYSTE'),1)
      MAGZIN=VOCAB(IA5('MAGAZ'),1)
      DWARF=VOCAB(IA5('DWARF'),1)
      KNIFE=VOCAB(IA5('KNIFE'),1)
      FOOD=VOCAB(IA5('FOOD '),1)
      BOTTLE=VOCAB(IA5('BOTTL'),1)
      WATER=VOCAB(IA5('WATER'),1)
      OIL=VOCAB(IA5('OIL  '),1)
      PLANT=VOCAB(IA5('PLANT'),1)
      PLANT2=PLANT+1
      AXE=VOCAB(IA5('AXE  '),1)
      MIRROR=VOCAB(IA5('MIRRO'),1)
      DRAGON=VOCAB(IA5('DRAGO'),1)
      CHASM=VOCAB(IA5('CHASM'),1)
      TROLL=VOCAB(IA5('TROLL'),1)
      TROLL2=TROLL+1
      BEAR=VOCAB(IA5('BEAR '),1)
      MESSAG=VOCAB(IA5('MESSA'),1)
      VEND=VOCAB(IA5('VENDI'),1)
      BATTER=VOCAB(IA5('BATTE'),1)

!  OBJECTS FROM 50 THROUGH WHATEVER ARE TREASURES.  HERE ARE A FEW.

      NUGGET=VOCAB(IA5('GOLD '),1)
      COINS=VOCAB(IA5('COINS'),1)
      CHEST=VOCAB(IA5('CHEST'),1)
      EGGS=VOCAB(IA5('EGGS '),1)
      TRIDNT=VOCAB(IA5('TRIDE'),1)
      VASE=VOCAB(IA5('VASE '),1)
      EMRALD=VOCAB(IA5('EMERA'),1)
      PYRAM=VOCAB(IA5('PYRAM'),1)
      PEARL=VOCAB(IA5('PEARL'),1)
      RUG=VOCAB(IA5('RUG  '),1)
      CHAIN=VOCAB(IA5('CHAIN'),1)
      SPICES=VOCAB(IA5('SPICE'),1)

!  THESE ARE MOTION-VERB NUMBERS.

      BACK=VOCAB(IA5('BACK '),0)
      LOOK=VOCAB(IA5('LOOK '),0)
      CAVE=VOCAB(IA5('CAVE '),0)
      NULL=VOCAB(IA5('NULL '),0)
      ENTRNC=VOCAB(IA5('ENTRA'),0)
      DPRSSN=VOCAB(IA5('DEPRE'),0)

!  AND SOME ACTION VERBS.

      SAY=VOCAB(IA5('SAY  '),2)
      LOCK=VOCAB(IA5('LOCK '),2)
      THROW=VOCAB(IA5('THROW'),2)
      FIND=VOCAB(IA5('FIND '),2)
      INVENT=VOCAB(IA5('INVEN'),2)

!  INITIALISE THE DWARVES.  DLOC IS LOC OF DWARVES, HARD-WIRED IN.  ODLOC IS
!  PRIOR LOC OF EACH DWARF, INITIALLY GARBAGE.  DALTLC IS ALTERNATE INITIAL LOC
!  FOR DWARF, IN CASE ONE OF THEM STARTS OUT ON TOP OF THE ADVENTURER.  (NO 2
!  OF THE 5 INITIAL LOCS ARE ADJACENT.)  DSEEN IS TRUE IF DWARF HAS SEEN HIM.
!  DFLAG CONTROLS THE LEVEL OF ACTIVATION OF ALL THIS:
!     0     NO DWARF STUFF YET (WAIT UNTIL REACHES HALL OF MISTS)
!     1     REACHED HALL OF MISTS, BUT HASN'T MET FIRST DWARF
!     2     MET FIRST DWARF, OTHERS START MOVING, NO KNIVES THROWN YET
!     3     A KNIFE HAS BEEN THROWN (FIRST SET ALWAYS MISSES)
!     3+    DWARVES ARE MAD (INCREASES THEIR ACCURACY)
!  SIXTH DWARF IS SPECIAL (THE PIRATE).  HE ALWAYS STARTS AT HIS CHEST'S
!  EVENTUAL LOCATION INSIDE THE MAZE.  THIS LOC IS SAVED IN CHLOC FOR REF.
!  THE DEAD END IN THE OTHER MAZE HAS ITS LOC STORED IN CHLOC2.

      CHLOC=114
      CHLOC2=140
      DO I=1,6
         DSEEN(I)=.FALSE.
      END DO
      DFLAG=0
      DLOC(1)=19
      DLOC(2)=27
      DLOC(3)=33
      DLOC(4)=44
      DLOC(5)=64
      DLOC(6)=CHLOC
      DALTLC=18

!  OTHER RANDOM FLAGS AND COUNTERS, AS FOLLOWS:
!     TURNS   TALLIES HOW MANY COMMANDS HE'S GIVEN (IGNORES YES/NO)
!     LIMIT   LIFETIME OF LAMP (NOT SET HERE)
!     IWEST   HOW MANY TIMES HE'S SAID "WEST" INSTEAD OF "W"
!     KNFLOC  0 IF NO KNIFE HERE, LOC IF KNIFE HERE, -1 AFTER CAVEAT
!     DETAIL  HOW OFTEN WE'VE SAID "NOT ALLOWED TO GIVE MORE DETAIL"
!     ABBNUM  HOW OFTEN WE SHOULD PRINT NON-ABBREVIATED DESCRIPTIONS
!     MAXDIE  NUMBER OF REINCARNATION MESSAGES AVAILABLE (UP TO 5)
!     NUMDIE  NUMBER OF TIMES KILLED SO FAR
!     HOLDNG  NUMBER OF OBJECTS BEING CARRIED
!     DKILL   NUMBER OF DWARVES KILLED (UNUSED IN SCORING, NEEDED FOR MSG)
!     FOOBAR  CURRENT PROGRESS IN SAYING "FEE FIE FOE FOO".
!     BONUS   USED TO DETERMINE AMOUNT OF BONUS IF HE REACHES CLOSING
!     CLOCK1  NUMBER OF TURNS FROM FINDING LAST TREASURE TILL CLOSING
!     CLOCK2  NUMBER OF TURNS FROM FIRST WARNING TILL BLINDING FLASH
!     LOGICALS WERE EXPLAINED EARLIER

      TURNS=0
      LMWARN=.FALSE.
      IWEST=0
      KNFLOC=0
      DETAIL=0
      ABBNUM=5
      DO I=0,4
         IF(RTEXT(2*I+81).NE.0)MAXDIE=I+1
      END DO
      NUMDIE=0
      HOLDNG=0
      DKILL=0
      FOOBAR=0
      BONUS=0
      CLOCK1=30
      CLOCK2=50
      SAVED=0
      CLOSNG=.FALSE.
      PANIC=.FALSE.
      CLOSED=.FALSE.
      GAVEUP=.FALSE.
      SCORNG=.FALSE.

!  IF SETUP=1, REPORT ON AMOUNT OF ARRAYS ACTUALLY USED, TO PERMIT REDUCTIONS.

      IF(SETUP.NE.1)GOTO 1
      SETUP=2

      DO K=1,LOCSIZ
         KK=LOCSIZ+1-K
         IF(LTEXT(KK).NE.0) EXIT
      END DO

      OBJ=0
      DO K=1,100
         IF(PTEXT(K).NE.0)OBJ=OBJ+1
      END DO

      DO K=1,TABNDX
         IF(KTAB(K)/1000.EQ.2)VERB=KTAB(K)-2000
      END DO

      DO K=1,RTXSIZ
         J=RTXSIZ+1-K
         IF (RTEXT(J).NE.0) EXIT
      END DO

      DO K=1,MAGSIZ
         I=MAGSIZ+1-K
         IF (MTEXT(I).NE.0) EXIT
      END DO

      K=100
      PRINT 1999,LINUSE,LINSIZ,TRVS,TRVSIZ,TABNDX,TABSIZ,KK  &
              ,LOCSIZ,OBJ,K,VERB,VRBSIZ,J,RTXSIZ,CLSSES,CLSMAX  &
              ,HNTMAX,HNTSIZ,I,MAGSIZ
1999  FORMAT (' TABLE SPACE USED:'/  &
              ' ',I6,' OF ',I6,' WORDS OF MESSAGES'/  &
              ' ',I6,' OF ',I6,' TRAVEL OPTIONS'/  &
              ' ',I6,' OF ',I6,' VOCABULARY WORDS'/  &
              ' ',I6,' OF ',I6,' LOCATIONS'/  &
              ' ',I6,' OF ',I6,' OBJECTS'/  &
              ' ',I6,' OF ',I6,' ACTION VERBS'/  &
              ' ',I6,' OF ',I6,' RTEXT MESSAGES'/  &
              ' ',I6,' OF ',I6,' CLASS MESSAGES'/  &
              ' ',I6,' OF ',I6,' HINTS'/  &
              ' ',I6,' OF ',I6,' MAGIC MESSAGES'/  &
              )

!  FINALLY, SINCE WE'RE CLEARLY SETTING THINGS UP FOR THE FIRST TIME...

      CALL POOF

!  START-UP, DWARF STUFF

1     DEMO=START()
      CALL MOTD(.FALSE.)
      I=RANI(1)
      HINTED(3)=YES(65,1,0)
      NEWLOC=1
      LOC=1
      SETUP=3
      LIMIT=330
      IF(HINTED(3))LIMIT=1000

!  CAN'T LEAVE CAVE ONCE IT'S CLOSING (EXCEPT BY MAIN OFFICE).

2     IF (NEWLOC.LT.9.AND.NEWLOC.NE.0.AND.CLOSNG) THEN
         CALL RSPEAK(130)
         NEWLOC=LOC
         IF(.NOT.PANIC)CLOCK2=15
         PANIC=.TRUE.
      END IF

!  SEE IF A DWARF HAS SEEN HIM AND HAS COME FROM WHERE HE WANTS TO GO.  IF SO,
!  THE DWARF'S BLOCKING HIS WAY.  IF COMING FROM PLACE FORBIDDEN TO PIRATE
!  (DWARVES ROOTED IN PLACE) LET HIM GET OUT (AND ATTACKED).

      IF (.NOT.(NEWLOC.EQ.LOC.OR.FORCED(LOC).OR.BITSET(LOC,3))) THEN
         DO I=1,5
            IF (ODLOC(I).NE.NEWLOC.OR..NOT.DSEEN(I)) CYCLE
            NEWLOC=LOC
            CALL RSPEAK(2)
            EXIT
         END DO
      END IF
      LOC=NEWLOC

!  DWARF STUFF.  SEE EARLIER COMMENTS FOR DESCRIPTION OF VARIABLES.  REMEMBER
!  SIXTH DWARF IS PIRATE AND IS THUS VERY DIFFERENT EXCEPT FOR MOTION RULES.

!  FIRST OFF, DON'T LET THE DWARVES FOLLOW HIM INTO A PIT OR A WALL.  ACTIVATE
!  THE WHOLE MESS THE FIRST TIME HE GETS AS FAR AS THE HALL OF MISTS (LOC 15).
!  IF NEWLOC IS FORBIDDEN TO PIRATE (IN PARTICULAR, IF IT'S BEYOND THE TROLL
!  BRIDGE), BYPASS DWARF STUFF.  THAT WAY PIRATE CAN'T STEAL RETURN TOLL, AND
!  DWARVES CAN'T MEET THE BEAR.  ALSO MEANS DWARVES WON'T FOLLOW HIM INTO DEAD
!  END IN MAZE, BUT C'EST LA VIE.  THEY'LL WAIT FOR HIM OUTSIDE THE DEAD END.

      IF(LOC.EQ.0.OR.FORCED(LOC).OR.BITSET(NEWLOC,3))GOTO 2000
      IF(DFLAG.NE.0)GOTO 6000
      IF(LOC.GE.15)DFLAG=1
      GOTO 2000

!  WHEN WE ENCOUNTER THE FIRST DWARF, WE KILL 0, 1, OR 2 OF THE 5 DWARVES.  IF
!  ANY OF THE SURVIVORS IS AT LOC, REPLACE HIM WITH THE ALTERNATE.

6000  IF (DFLAG.EQ.1) THEN
         IF(LOC.LT.15.OR.PCT(95))GOTO 2000
         DFLAG=2
         DO I=1,2
            J=1+RANI(5)
!  IF SAVED NOT = -1, HE BYPASSED THE "START" CALL.
            IF(PCT(50).AND.SAVED.EQ.-1)DLOC(J)=0
         END DO
         DO I=1,5
            IF(DLOC(I).EQ.LOC)DLOC(I)=DALTLC
            ODLOC(I)=DLOC(I)
         END DO
         CALL RSPEAK(3)
         CALL DROP(AXE,LOC)
         GOTO 2000
      END IF

!  THINGS ARE IN FULL SWING.  MOVE EACH DWARF AT RANDOM, EXCEPT IF HE'S SEEN US
!  HE STICKS WITH US.  DWARVES NEVER GO TO LOCS <15.  IF WANDERING AT RANDOM,
!  THEY DON'T BACK UP UNLESS THERE'S NO ALTERNATIVE.  IF THEY DON'T HAVE TO
!  MOVE, THEY ATTACK.  AND, OF COURSE, DEAD DWARVES DON'T DO MUCH OF ANYTHING.

6010  DTOTAL=0
      ATTACK=0
      STICK=0
      DO 6030 I=1,6
      IF(DLOC(I).EQ.0)GOTO 6030
      J=1
      KK=DLOC(I)
      KK=KEY(KK)
      IF (KK.NE.0) THEN
         DO
            NEWLOC=MOD(ABS(TRAVEL(KK))/1000,1000)
            IF(.NOT.(NEWLOC.GT.300.OR.NEWLOC.LT.15.OR.NEWLOC.EQ.ODLOC(I)  &
                  .OR.(J.GT.1.AND.NEWLOC.EQ.TK(J-1)).OR.J.GE.20  &
                  .OR.NEWLOC.EQ.DLOC(I).OR.FORCED(NEWLOC)  &
                  .OR.(I.EQ.6.AND.BITSET(NEWLOC,3))  &
                  .OR.ABS(TRAVEL(KK))/1000000.EQ.100)) THEN
               TK(J)=NEWLOC
               J=J+1
            END IF
            KK=KK+1
            IF (TRAVEL(KK-1).LT.0) EXIT
         END DO
      END IF
      TK(J)=ODLOC(I)
      IF(J.GE.2)J=J-1
      J=1+RANI(J)
      ODLOC(I)=DLOC(I)
      DLOC(I)=TK(J)
      DSEEN(I)=(DSEEN(I).AND.LOC.GE.15)  &
              .OR.(DLOC(I).EQ.LOC.OR.ODLOC(I).EQ.LOC)
      IF(.NOT.DSEEN(I))GOTO 6030
      DLOC(I)=LOC
      IF(I.NE.6)GOTO 6027

!  THE PIRATE'S SPOTTED HIM.  HE LEAVES HIM ALONE ONCE WE'VE FOUND CHEST.
!  K COUNTS IF A TREASURE IS HERE.  IF NOT, AND TALLY=TALLY2 PLUS ONE FOR AN
!  UNSEEN CHEST, LET THE PIRATE BE SPOTTED.  USE PLACE(MESSAG) TO DETERMINE IF
!  PIRATE'S BEEN SEEN, SINCE PLACE(CHEST)=0 COULD MEAN HE THREW IT TO TROLL.

      IF(LOC.EQ.CHLOC.OR.PROP(CHEST).GE.0)GOTO 6030
      K=0
      DO J=50,MAXTRS
!  PIRATE WON'T TAKE PYRAMID FROM PLOVER ROOM OR DARK ROOM (TOO EASY!).
         IF (J.NE.PYRAM.OR.(LOC.NE.PLAC(PYRAM)  &
               .AND.LOC.NE.PLAC(EMRALD))) THEN
            IF(TOTING(J))GOTO 6022
         END IF
         IF(HERE(J))K=1
      END DO
      IF(TALLY.EQ.TALLY2+1.AND.K.EQ.0.AND.PLACE(MESSAG).EQ.0  &
              .AND.HERE(LAMP).AND.PROP(LAMP).EQ.1)GOTO 6025
      IF(ODLOC(6).NE.DLOC(6).AND.PCT(20))CALL RSPEAK(127)
      GOTO 6030

6022  CALL RSPEAK(128)
      IF(PLACE(MESSAG).EQ.0)CALL MOVE(CHEST,CHLOC)
      CALL MOVE(MESSAG,CHLOC2)
      DO J=50,MAXTRS
         IF(J.EQ.PYRAM.AND.(LOC.EQ.PLAC(PYRAM)  &
               .OR.LOC.EQ.PLAC(EMRALD))) CYCLE
         IF(AT(J).AND.FIXED(J).EQ.0)CALL CARRY(J,LOC)
         IF(TOTING(J))CALL DROP(J,CHLOC)
      END DO
6024  DLOC(6)=CHLOC
      ODLOC(6)=CHLOC
      DSEEN(6)=.FALSE.
      GOTO 6030

6025  CALL RSPEAK(186)
      CALL MOVE(CHEST,CHLOC)
      CALL MOVE(MESSAG,CHLOC2)
      GOTO 6024

!  THIS THREATENING LITTLE DWARF IS IN THE ROOM WITH HIM!

6027  DTOTAL=DTOTAL+1
      IF (ODLOC(I).EQ.DLOC(I)) THEN
         ATTACK=ATTACK+1
         IF(KNFLOC.GE.0)KNFLOC=LOC
         IF(RANI(1000).LT.95*(DFLAG-2))STICK=STICK+1
      END IF
6030  CONTINUE

!  NOW WE KNOW WHAT'S HAPPENING.  LET'S TELL THE POOR SUCKER ABOUT IT.

      IF(DTOTAL.EQ.0)GOTO 2000
      IF (DTOTAL.NE.1) THEN
         PRINT 67,DTOTAL
67       FORMAT(/' THERE ARE ',I1,' THREATENING LITTLE DWARVES IN THE'  &
               ,' ROOM WITH YOU.')
      ELSE
         CALL RSPEAK(4)
      END IF
      IF(ATTACK.EQ.0)GOTO 2000
      IF(DFLAG.EQ.2)DFLAG=3
!  IF SAVED NOT = -1, HE BYPASSED THE "START" CALL.  DWARVES GET *VERY* MAD!
      IF(SAVED.NE.-1)DFLAG=20
      IF (ATTACK.EQ.1) THEN
         CALL RSPEAK(5)
         K=52
      ELSE
         PRINT 78,ATTACK
78       FORMAT(/' ',I1,' OF THEM THROW KNIVES AT YOU!')
         K=6
      END IF
      IF (STICK.LE.1) THEN
         CALL RSPEAK(K+STICK)
         IF(STICK.EQ.0)GOTO 2000
      ELSE
         PRINT 68,STICK
68       FORMAT(/' ',I1,' OF THEM GET YOU!')
      END IF
      OLDLC2=LOC
      GOTO 99

!  DESCRIBE THE CURRENT LOCATION AND (MAYBE) GET NEXT COMMAND.

!  PRINT TEXT FOR CURRENT LOC.

2000  IF(LOC.EQ.0)GOTO 99
      KK=STEXT(LOC)
      IF(MOD(ABB(LOC),ABBNUM).EQ.0.OR.KK.EQ.0)KK=LTEXT(LOC)
      IF (.NOT.FORCED(LOC) .AND. DARK()) THEN
         IF(WZDARK.AND.PCT(35))GOTO 90
         KK=RTEXT(16)
      END IF
      IF(TOTING(BEAR))CALL RSPEAK(141)
      CALL SPEAK(KK)
      K=1
      IF(FORCED(LOC))GOTO 8
      IF(LOC.EQ.33.AND.PCT(25).AND..NOT.CLOSNG)CALL RSPEAK(8)

!  PRINT OUT DESCRIPTIONS OF OBJECTS AT THIS LOCATION.  IF NOT CLOSING AND
!  PROPERTY VALUE IS NEGATIVE, TALLY OFF ANOTHER TREASURE.  RUG IS SPECIAL
!  CASE; ONCE SEEN, ITS PROP IS 1 (DRAGON ON IT) TILL DRAGON IS KILLED.
!  SIMILARLY FOR CHAIN; PROP IS INITIALLY 1 (LOCKED TO BEAR).  THESE HACKS
!  ARE BECAUSE PROP=0 IS NEEDED TO GET FULL SCORE.

      IF(DARK())GOTO 2012
      ABB(LOC)=ABB(LOC)+1
      I=ATLOC(LOC)
2004  IF(I.EQ.0)GOTO 2012
      OBJ=I
      IF(OBJ.GT.100)OBJ=OBJ-100
      IF(OBJ.EQ.STEPS.AND.TOTING(NUGGET))GOTO 2008
      IF(PROP(OBJ).GE.0)GOTO 2006
      IF(CLOSED)GOTO 2008
      PROP(OBJ)=0
      IF(OBJ.EQ.RUG.OR.OBJ.EQ.CHAIN)PROP(OBJ)=1
      TALLY=TALLY-1
!  IF REMAINING TREASURES TOO ELUSIVE, ZAP HIS LAMP.
      IF(TALLY.EQ.TALLY2.AND.TALLY.NE.0)LIMIT=MIN(35,LIMIT)
2006  KK=PROP(OBJ)
      IF(OBJ.EQ.STEPS.AND.LOC.EQ.FIXED(STEPS))KK=1
      CALL PSPEAK(OBJ,KK)
2008  I=LINK(I)
      GOTO 2004

2009  K=54
2010  SPK=K
2011  CALL RSPEAK(SPK)

2012  VERB=0
      OLDOBJ=OBJ
      OBJ=0

!  CHECK IF THIS LOC IS ELIGIBLE FOR ANY HINTS.  IF BEEN HERE LONG ENOUGH,
!  BRANCH TO HELP SECTION (ON LATER PAGE).  HINTS ALL COME BACK HERE EVENTUALLY
!  TO FINISH THE LOOP.  IGNORE "HINTS" < 4 (SPECIAL STUFF, SEE DATABASE NOTES).

2600  DO HINT=4,HNTMAX
         IF (HINTED(HINT)) CYCLE
         IF(.NOT.BITSET(LOC,HINT))HINTLC(HINT)=-1
         HINTLC(HINT)=HINTLC(HINT)+1
         IF(HINTLC(HINT).GE.HINTS(HINT,1))GOTO 40000
      END DO
2602  CONTINUE

!  KICK THE RANDOM NUMBER GENERATOR JUST TO ADD VARIETY TO THE CHASE.  ALSO,
!  IF CLOSING TIME, CHECK FOR ANY OBJECTS BEING TOTED WITH PROP < 0 AND SET
!  THE PROP TO -1-PROP.  THIS WAY OBJECTS WON'T BE DESCRIBED UNTIL THEY'VE
!  BEEN PICKED UP AND PUT DOWN SEPARATE FROM THEIR RESPECTIVE PILES.  DON'T
!  TICK CLOCK1 UNLESS WELL INTO CAVE (AND NOT AT Y2).

      IF (CLOSED) THEN
         IF(PROP(OYSTER).LT.0.AND.TOTING(OYSTER))  &
               CALL PSPEAK(OYSTER,1)
         DO I=1,100
            IF(TOTING(I).AND.PROP(I).LT.0)PROP(I)=-1-PROP(I)
         END DO
      END IF
      WZDARK=DARK()
      IF(KNFLOC.GT.0.AND.KNFLOC.NE.LOC)KNFLOC=0
      I=RANI(1)
      CALL GETIN(WD1,WD1X,WD2,WD2X)

!  EVERY INPUT, CHECK "FOOBAR" FLAG.  IF ZERO, NOTHING'S GOING ON.  IF POS,
!  MAKE NEG.  IF NEG, HE SKIPPED A WORD, SO MAKE IT ZERO.

2608  FOOBAR=MIN(0,-FOOBAR)
      IF(TURNS.EQ.0.AND.WD1.EQ.IA5('MAGIC').AND.WD2.EQ.IA5('MODE '))  &
              CALL MAINT
      TURNS=TURNS+1
      IF(DEMO.AND.TURNS.GE.SHORT)GOTO 13000
      IF(TURNS.EQ.3)CALL DATIME(XXD,XXT)
      IF (TURNS.EQ.45) THEN
!  SEE IF TIMER UUO HAS BEEN ZAPPED; IF SO, HE'S CHEATING.
         CALL DATIME(YYD,YYT)
         IF(XXD.EQ.YYD.AND.XXT.EQ.YYT)SAVED=0
      END IF
      IF(VERB.EQ.SAY.AND.WD2.NE.0)VERB=0
      IF(VERB.EQ.SAY)GOTO 4090
      IF(TALLY.EQ.0.AND.LOC.GE.15.AND.LOC.NE.33)CLOCK1=CLOCK1-1
      IF(CLOCK1.EQ.0)GOTO 10000
      IF(CLOCK1.LT.0)CLOCK2=CLOCK2-1
      IF(CLOCK2.EQ.0)GOTO 11000
      IF(PROP(LAMP).EQ.1)LIMIT=LIMIT-1
      IF(LIMIT.LE.30.AND.HERE(BATTER).AND.PROP(BATTER).EQ.0  &
              .AND.HERE(LAMP))GOTO 12000
      IF(LIMIT.EQ.0)GOTO 12400
      IF(LIMIT.LT.0.AND.LOC.LE.8)GOTO 12600
      IF(LIMIT.LE.30)GOTO 12200
19999 K=43
      IF(LIQLOC(LOC).EQ.WATER)K=70
      IF(WD1.EQ.IA5('ENTER').AND.(WD2.EQ.IA5('STREA')  &
              .OR.WD2.EQ.IA5('WATER')))GOTO 2010
      IF(WD1.EQ.IA5('ENTER').AND.WD2.NE.0)GOTO 2800
      IF ((WD1.EQ.IA5('WATER').OR.WD1.EQ.IA5('OIL  '))  &
            .AND.(WD2.EQ.IA5('PLANT').OR.WD2.EQ.IA5('DOOR '))) THEN
         IF(AT(VOCAB(WD2,1)))WD2=IA5('POUR ')
      END IF
2610  IF (WD1.EQ.IA5('WEST ')) THEN
         IWEST=IWEST+1
         IF(IWEST.EQ.10)CALL RSPEAK(17)
      END IF
2630  I=VOCAB(WD1,-1)
      IF(I.EQ.-1)GOTO 3000
      K=MOD(I,1000)
      KQ=I/1000+1
      GOTO (8,5000,4000,2010)KQ
      CALL BUG(22)

!  GET SECOND WORD FOR ANALYSIS.

2800  WD1=WD2
      WD1X=WD2X
      WD2=0
      GOTO 2610

!  GEE, I DON'T UNDERSTAND.

3000  CALL A5TOA1(WD1,WD1X,IA5('".   '),CAT,K)
      PRINT 3001,(CAT(I),I=1,K)
3001  FORMAT(/' SORRY, I DON''T KNOW THE WORD "',20A1)
      GOTO 2600

!  ANALYSE A VERB.  REMEMBER WHAT IT WAS, GO BACK FOR OBJECT IF SECOND WORD
!  UNLESS VERB IS "SAY", WHICH SNARFS ARBITRARY SECOND WORD.

4000  VERB=K
      SPK=ACTSPK(VERB)
      IF(WD2.NE.0.AND.VERB.NE.SAY)GOTO 2800
      IF(VERB.EQ.SAY)OBJ=WD2
      IF (OBJ.EQ.0) THEN

!  ANALYSE AN INTRANSITIVE VERB (IE, NO OBJECT GIVEN YET).

         select case (VERB)
            case (1)  ! TAKE
               GOTO 8010
            case (4, 6)  ! OPEN, LOCK
               GOTO 8040
            case (5)  ! NOTHING
               GOTO 2009
            case (7)  ! ON
               GOTO 9070
            case (8)  ! OFF
               GOTO 9080
            case (11)  ! WALK
               GOTO 2011
            case (12)  ! KILL
               GOTO 9120
            case (13)  ! POUR
               GOTO 9130
            case (14)  ! EAT
               GOTO 8140
            case (15)  ! DRINK
               GOTO 9150
            case (18)  ! QUIT
               GOTO 8180
            case (20)  ! INVEN
               GOTO 8200
            case (22)  ! FILL
               GOTO 9220
            case (23)  ! BLAST
               GOTO 9230
            case (24)  ! SCORE
               GOTO 8240
            case (25)  ! FOO
               GOTO 8250
            case (26)  ! BRIEF
               GOTO 8260
            case (27)  ! READ
               GOTO 8270
            case (30)  ! SUSPEND
               GOTO 8300
            case (31)  ! HOURS
               GOTO 8310
            case (2, 3, 9, 10, 16, 17, 19, 21, 28, 29)
               GOTO 8000
            case default
               CALL BUG(23)
         end select

      ELSE

!  ANALYSE A TRANSITIVE VERB.

4090     select case (VERB)
            case (1)  ! TAKE
               GOTO 9010
            case (2)  ! DROP
               GOTO 9020
            case (3)  ! SAY
               GOTO 9030
            case (4, 6)  ! OPEN, LOCK
               GOTO 9040
            case (5)  ! NOTHING
               GOTO 2009
            case (7)  ! ON
               GOTO 9070
            case (8)  ! OFF
               GOTO 9080
            case (9)  ! WAVE
               GOTO 9090
            case (12)  ! KILL
               GOTO 9120
            case (13)  ! POUR
               GOTO 9130
            case (14)  ! EAT
               GOTO 9140
            case (15)  ! DRINK
               GOTO 9150
            case (16)  ! RUB
               GOTO 9160
            case (17)  ! TOSS
               GOTO 9170
            case (19,20)  ! FIND, INVEN
               GOTO 9190
            case (21)  ! FEED
               GOTO 9210
            case (22)  ! FILL
               GOTO 9220
            case (23)  ! BLAST
               GOTO 9230
            case (27)  ! READ
               GOTO 9270
            case (28)  ! BREAK
               GOTO 9280
            case (29)  ! WAKE
               GOTO 9290
            case (10, 11, 18, 24, 25, 26, 30, 31)
               GOTO 2011
            case default
               CALL BUG(24)
         end select

      END IF

!  ANALYSE AN OBJECT WORD.  SEE IF THE THING IS HERE, WHETHER WE'VE GOT A VERB
!  YET, AND SO ON.  OBJECT MUST BE HERE UNLESS VERB IS "FIND" OR "INVENT(ORY)"
!  (AND NO NEW VERB YET TO BE ANALYSED).  WATER AND OIL ARE ALSO FUNNY, SINCE
!  THEY ARE NEVER ACTUALLY DROPPED AT ANY LOCATION, BUT MIGHT BE HERE INSIDE
!  THE BOTTLE OR AS A FEATURE OF THE LOCATION.

5000  OBJ=K
      IF(FIXED(K).NE.LOC.AND..NOT.HERE(K))GOTO 5100
5010  IF(WD2.NE.0)GOTO 2800
      IF(VERB.NE.0)GOTO 4090
      CALL A5TOA1(WD1,WD1X,IA5('?'),CAT,K)
      PRINT 5015,(CAT(I),I=1,K)
5015  FORMAT(/' WHAT DO YOU WANT TO DO WITH THE ',20A1)
      GOTO 2600

5100  IF (K.EQ.GRATE) THEN
         IF(LOC.EQ.1.OR.LOC.EQ.4.OR.LOC.EQ.7)K=DPRSSN
         IF(LOC.GT.9.AND.LOC.LT.15)K=ENTRNC
         IF(K.NE.GRATE)GOTO 8
      ELSE IF (K.EQ.DWARF) THEN
         DO I=1,5
            IF(DLOC(I).EQ.LOC.AND.DFLAG.GE.2)GOTO 5010
         END DO
      END IF
      IF((LIQ().EQ.K.AND.HERE(BOTTLE)).OR.K.EQ.LIQLOC(LOC))GOTO 5010
      IF (OBJ.EQ.PLANT.AND.AT(PLANT2).AND.PROP(PLANT2).NE.0) THEN
         OBJ=PLANT2
         GOTO 5010
      ELSE IF (OBJ.EQ.KNIFE.AND.KNFLOC.EQ.LOC) THEN
         KNFLOC=-1
         SPK=116
         GOTO 2011
      ELSE IF (OBJ.EQ.ROD.AND.HERE(ROD2)) THEN
         OBJ=ROD2
         GOTO 5010
      END IF
5190  IF((VERB.EQ.FIND.OR.VERB.EQ.INVENT).AND.WD2.EQ.0)GOTO 5010
      CALL A5TOA1(WD1,WD1X,IA5('HERE.'),CAT,K)
      PRINT 5199,(CAT(I),I=1,K)
5199  FORMAT(/' I SEE NO ',20A1)
      GOTO 2012
!  FIGURE OUT THE NEW LOCATION
!
!  GIVEN THE CURRENT LOCATION IN "LOC", AND A MOTION VERB NUMBER IN "K", PUT
!  THE NEW LOCATION IN "NEWLOC".  THE CURRENT LOC IS SAVED IN "OLDLOC" IN CASE
!  HE WANTS TO RETREAT.  THE CURRENT OLDLOC IS SAVED IN OLDLC2, IN CASE HE
!  DIES.  (IF HE DOES, NEWLOC WILL BE LIMBO, AND OLDLOC WILL BE WHAT KILLED
!  HIM, SO WE NEED OLDLC2, WHICH IS THE LAST PLACE HE WAS SAFE.)

8     KK=KEY(LOC)
      NEWLOC=LOC
      IF(KK.EQ.0)CALL BUG(26)
      IF(K.EQ.NULL)GOTO 2
      IF(K.EQ.BACK)GOTO 20
      IF(K.EQ.LOOK)GOTO 30
      IF(K.EQ.CAVE)GOTO 40
      OLDLC2=OLDLOC
      OLDLOC=LOC

9     LL=ABS(TRAVEL(KK))
      IF(MOD(LL,1000).EQ.1.OR.MOD(LL,1000).EQ.K)GOTO 10
      IF(TRAVEL(KK).LT.0)GOTO 50
      KK=KK+1
      GOTO 9

10    LL=LL/1000
11    NEWLOC=LL/1000
      K=MOD(NEWLOC,100)
      IF(NEWLOC.LE.300)GOTO 13
      IF(PROP(K).NE.NEWLOC/100-3)GOTO 16
12    IF(TRAVEL(KK).LT.0)CALL BUG(25)
      KK=KK+1
      NEWLOC=ABS(TRAVEL(KK))/1000
      IF(NEWLOC.EQ.LL)GOTO 12
      LL=NEWLOC
      GOTO 11

13    IF(NEWLOC.LE.100)GOTO 14
      IF(TOTING(K).OR.(NEWLOC.GT.200.AND.AT(K)))GOTO 16
      GOTO 12

14    IF(NEWLOC.NE.0.AND..NOT.PCT(NEWLOC))GOTO 12
16    NEWLOC=MOD(LL,1000)
      IF(NEWLOC.LE.300)GOTO 2
      IF(NEWLOC.LE.500)GOTO 30000
      CALL RSPEAK(NEWLOC-500)
      NEWLOC=LOC
      GOTO 2

!  SPECIAL MOTIONS COME HERE.  LABELLING CONVENTION: STATEMENT NUMBERS NNNXX
!  (XX=00-99) ARE USED FOR SPECIAL CASE NUMBER NNN (NNN=301-500).

30000 NEWLOC=NEWLOC-300
      select case (NEWLOC)
         case (1)
            GOTO 30100
         case (2)
            GOTO 30200
         case (3)
            GOTO 30300
         case default
            CALL BUG(20)
      end select

!  TRAVEL 301.  PLOVER-ALCOVE PASSAGE.  CAN CARRY ONLY EMERALD.  NOTE: TRAVEL
!  TABLE MUST INCLUDE "USELESS" ENTRIES GOING THROUGH PASSAGE, WHICH CAN NEVER
!  BE USED FOR ACTUAL MOTION, BUT CAN BE SPOTTED BY "GO BACK".

30100 NEWLOC=99+100-LOC
      IF (.NOT.(HOLDNG.EQ.0.OR.(HOLDNG.EQ.1.AND.TOTING(EMRALD)))) THEN
         NEWLOC=LOC
         CALL RSPEAK(117)
      END IF
      GOTO 2

!  TRAVEL 302.  PLOVER TRANSPORT.  DROP THE EMERALD (ONLY USE SPECIAL TRAVEL IF
!  TOTING IT), SO HE'S FORCED TO USE THE PLOVER-PASSAGE TO GET IT OUT.  HAVING
!  DROPPED IT, GO BACK AND PRETEND HE WASN'T CARRYING IT AFTER ALL.

30200 CALL DROP(EMRALD,LOC)
      GOTO 12

!  TRAVEL 303.  TROLL BRIDGE.  MUST BE DONE ONLY AS SPECIAL MOTION SO THAT
!  DWARVES WON'T WANDER ACROSS AND ENCOUNTER THE BEAR.  (THEY WON'T FOLLOW THE
!  PLAYER THERE BECAUSE THAT REGION IS FORBIDDEN TO THE PIRATE.)  IF
!  PROP(TROLL)=1, HE'S CROSSED SINCE PAYING, SO STEP OUT AND BLOCK HIM.
!  (STANDARD TRAVEL ENTRIES CHECK FOR PROP(TROLL)=0.)  SPECIAL STUFF FOR BEAR.

30300 IF (PROP(TROLL).EQ.1) THEN
         CALL PSPEAK(TROLL,1)
         PROP(TROLL)=0
         CALL MOVE(TROLL2,0)
         CALL MOVE(TROLL2+100,0)
         CALL MOVE(TROLL,PLAC(TROLL))
         CALL MOVE(TROLL+100,FIXD(TROLL))
         CALL JUGGLE(CHASM)
         NEWLOC=LOC
         GOTO 2
      ELSE
         NEWLOC=PLAC(TROLL)+FIXD(TROLL)-LOC
         IF(PROP(TROLL).EQ.0)PROP(TROLL)=1
         IF(.NOT.TOTING(BEAR))GOTO 2
         CALL RSPEAK(162)
         PROP(CHASM)=1
         PROP(TROLL)=2
         CALL DROP(BEAR,NEWLOC)
         FIXED(BEAR)=-1
         PROP(BEAR)=3
         IF(PROP(SPICES).LT.0)TALLY2=TALLY2+1
         OLDLC2=NEWLOC
         GOTO 99
      END IF

!  END OF SPECIALS.

!  HANDLE "GO BACK".  LOOK FOR VERB WHICH GOES FROM LOC TO OLDLOC, OR TO OLDLC2
!  IF OLDLOC HAS FORCED-MOTION.  K2 SAVES ENTRY -> FORCED LOC -> PREVIOUS LOC.

20    K=OLDLOC
      IF(FORCED(K))K=OLDLC2
      OLDLC2=OLDLOC
      OLDLOC=LOC
      K2=0
      IF (K.EQ.LOC) THEN
         CALL RSPEAK(91)
         GOTO 2
      END IF

21    LL=MOD((ABS(TRAVEL(KK))/1000),1000)
      IF (LL.NE.K) THEN
         IF (LL.LE.300) THEN
            J=KEY(LL)
            IF(FORCED(LL).AND.MOD((ABS(TRAVEL(J))/1000),1000).EQ.K)K2=KK
         END IF
         IF (TRAVEL(KK).GE.0) THEN
            KK=KK+1
            GOTO 21
         END IF

         KK=K2
         IF (KK.EQ.0) THEN
            CALL RSPEAK(140)
            GOTO 2
         END IF
      END IF

      K=MOD(ABS(TRAVEL(KK)),1000)
      KK=KEY(LOC)
      GOTO 9

!  LOOK.  CAN'T GIVE MORE DETAIL.  PRETEND IT WASN'T DARK (THOUGH IT MAY "NOW"
!  BE DARK) SO HE WON'T FALL INTO A PIT WHILE STARING INTO THE GLOOM.

30    IF(DETAIL.LT.3)CALL RSPEAK(15)
      DETAIL=DETAIL+1
      WZDARK=.FALSE.
      ABB(LOC)=0
      GOTO 2

!  CAVE.  DIFFERENT MESSAGES DEPENDING ON WHETHER ABOVE GROUND.

40    IF(LOC.LT.8)CALL RSPEAK(57)
      IF(LOC.GE.8)CALL RSPEAK(58)
      GOTO 2

!  NON-APPLICABLE MOTION.  VARIOUS MESSAGES DEPENDING ON WORD GIVEN.

50    SPK=12
      IF(K.GE.43.AND.K.LE.50)SPK=9
      IF(K.EQ.29.OR.K.EQ.30)SPK=9
      IF(K.EQ.7.OR.K.EQ.36.OR.K.EQ.37)SPK=10
      IF(K.EQ.11.OR.K.EQ.19)SPK=11
      IF(VERB.EQ.FIND.OR.VERB.EQ.INVENT)SPK=59
      IF(K.EQ.62.OR.K.EQ.65)SPK=42
      IF(K.EQ.17)SPK=80
      CALL RSPEAK(SPK)
      GOTO 2
!  "YOU'RE DEAD, JIM."
!
!  IF THE CURRENT LOC IS ZERO, IT MEANS THE CLOWN GOT HIMSELF KILLED.  WE'LL
!  ALLOW THIS MAXDIE TIMES.  MAXDIE IS AUTOMATICALLY SET BASED ON THE NUMBER OF
!  SNIDE MESSAGES AVAILABLE.  EACH DEATH RESULTS IN A MESSAGE (81, 83, ETC.)
!  WHICH OFFERS REINCARNATION; IF ACCEPTED, THIS RESULTS IN MESSAGE 82, 84,
!  ETC.  THE LAST TIME, IF HE WANTS ANOTHER CHANCE, HE GETS A SNIDE REMARK AS
!  WE EXIT.  WHEN REINCARNATED, ALL OBJECTS BEING CARRIED GET DROPPED AT OLDLC2
!  (PRESUMABLY THE LAST PLACE PRIOR TO BEING KILLED) WITHOUT CHANGE OF PROPS.
!  THE LOOP RUNS BACKWARDS TO ASSURE THAT THE BIRD IS DROPPED BEFORE THE CAGE.
!  (THIS KLUGE COULD BE CHANGED ONCE WE'RE SURE ALL REFERENCES TO BIRD AND CAGE
!  ARE DONE BY KEYWORDS.)  THE LAMP IS A SPECIAL CASE (IT WOULDN'T DO TO LEAVE
!  IT IN THE CAVE).  IT IS TURNED OFF AND LEFT OUTSIDE THE BUILDING (ONLY IF HE
!  WAS CARRYING IT, OF COURSE).  HE HIMSELF IS LEFT INSIDE THE BUILDING (AND
!  HEAVEN HELP HIM IF HE TRIES TO XYZZY BACK INTO THE CAVE WITHOUT THE LAMP!).
!  OLDLOC IS ZAPPED SO HE CAN'T JUST "RETREAT".

!  THE EASIEST WAY TO GET KILLED IS TO FALL INTO A PIT IN PITCH DARKNESS.

90    CALL RSPEAK(23)
      OLDLC2=LOC

!  OKAY, HE'S DEAD.  LET'S GET ON WITH IT.

99    IF (.NOT.CLOSNG) THEN
         YEA=YES(81+NUMDIE*2,82+NUMDIE*2,54)
         NUMDIE=NUMDIE+1
         IF(NUMDIE.EQ.MAXDIE.OR..NOT.YEA)GOTO 20000
         PLACE(WATER)=0
         PLACE(OIL)=0
         IF(TOTING(LAMP))PROP(LAMP)=0
         DO J=1,100
            I=101-J
            IF (.NOT.TOTING(I)) CYCLE
            K=OLDLC2
            IF(I.EQ.LAMP)K=1
            CALL DROP(I,K)
         END DO
         LOC=3
         OLDLOC=LOC
         GOTO 2000
      ELSE

!  HE DIED DURING CLOSING TIME.  NO RESURRECTION.  TALLY UP A DEATH AND EXIT.

         CALL RSPEAK(131)
         NUMDIE=NUMDIE+1
         GOTO 20000
      END IF

!  ROUTINES FOR PERFORMING THE VARIOUS ACTION VERBS

!  STATEMENT NUMBERS IN THIS SECTION ARE 8000 FOR INTRANSITIVE VERBS, 9000 FOR
!  TRANSITIVE, PLUS TEN TIMES THE VERB NUMBER.  MANY INTRANSITIVE VERBS USE THE
!  TRANSITIVE CODE, AND SOME VERBS USE CODE FOR OTHER VERBS, AS NOTED BELOW.

!  RANDOM INTRANSITIVE VERBS COME HERE.  CLEAR OBJ JUST IN CASE (SEE "ATTACK").

8000  CALL A5TOA1(WD1,WD1X,IA5('WHAT?'),CAT,K)
      PRINT 8002,(CAT(I),I=1,K)
8002  FORMAT(/' ',20A1)
      OBJ=0
      GOTO 2600

!  CARRY, NO OBJECT GIVEN YET.  OK IF ONLY ONE OBJECT PRESENT.

8010  IF(ATLOC(LOC).EQ.0.OR.LINK(ATLOC(LOC)).NE.0)GOTO 8000
      DO I=1,5
         IF(DLOC(I).EQ.LOC.AND.DFLAG.GE.2)GOTO 8000
      END DO
      OBJ=ATLOC(LOC)

!  CARRY AN OBJECT.  SPECIAL CASES FOR BIRD AND CAGE (IF BIRD IN CAGE, CAN'T
!  TAKE ONE WITHOUT THE OTHER).  LIQUIDS ALSO SPECIAL, SINCE THEY DEPEND ON
!  STATUS OF BOTTLE.  ALSO VARIOUS SIDE EFFECTS, ETC.

9010  IF(TOTING(OBJ))GOTO 2011
      SPK=25
      IF(OBJ.EQ.PLANT.AND.PROP(PLANT).LE.0)SPK=115
      IF(OBJ.EQ.BEAR.AND.PROP(BEAR).EQ.1)SPK=169
      IF(OBJ.EQ.CHAIN.AND.PROP(BEAR).NE.0)SPK=170
      IF(FIXED(OBJ).NE.0)GOTO 2011
      IF(OBJ.NE.WATER.AND.OBJ.NE.OIL)GOTO 9017
      IF(HERE(BOTTLE).AND.LIQ().EQ.OBJ)GOTO 9018
      OBJ=BOTTLE
      IF(TOTING(BOTTLE).AND.PROP(BOTTLE).EQ.1)GOTO 9220
      IF(PROP(BOTTLE).NE.1)SPK=105
      IF(.NOT.TOTING(BOTTLE))SPK=104
      GOTO 2011
9018  OBJ=BOTTLE
9017  IF (HOLDNG.GE.7) THEN
         CALL RSPEAK(92)
         GOTO 2012
      END IF
      IF (OBJ.EQ.BIRD .AND. PROP(BIRD).EQ.0) THEN
         IF (TOTING(ROD)) THEN
            CALL RSPEAK(26)
            GOTO 2012
         END IF
         IF (.NOT.TOTING(CAGE)) THEN
            CALL RSPEAK(27)
            GOTO 2012
         END IF
         PROP(BIRD)=1
      END IF
      IF((OBJ.EQ.BIRD.OR.OBJ.EQ.CAGE).AND.PROP(BIRD).NE.0)  &
              CALL CARRY(BIRD+CAGE-OBJ,LOC)
      CALL CARRY(OBJ,LOC)
      K=LIQ()
      IF(OBJ.EQ.BOTTLE.AND.K.NE.0)PLACE(K)=-1
      GOTO 2009

!  DISCARD OBJECT.  "THROW" ALSO COMES HERE FOR MOST OBJECTS.  SPECIAL CASES FOR
!  BIRD (MIGHT ATTACK SNAKE OR DRAGON) AND CAGE (MIGHT CONTAIN BIRD) AND VASE.
!  DROP COINS AT VENDING MACHINE FOR EXTRA BATTERIES.

9020  IF(TOTING(ROD2).AND.OBJ.EQ.ROD.AND..NOT.TOTING(ROD))OBJ=ROD2
      IF(.NOT.TOTING(OBJ))GOTO 2011
      IF(OBJ.NE.BIRD.OR..NOT.HERE(SNAKE))GOTO 9024
      CALL RSPEAK(30)
      IF(CLOSED)GOTO 19000
      CALL DSTROY(SNAKE)
!  SET PROP FOR USE BY TRAVEL OPTIONS
      PROP(SNAKE)=1
9021  K=LIQ()
      IF(K.EQ.OBJ)OBJ=BOTTLE
      IF(OBJ.EQ.BOTTLE.AND.K.NE.0)PLACE(K)=0
      IF(OBJ.EQ.CAGE.AND.PROP(BIRD).NE.0)CALL DROP(BIRD,LOC)
      IF(OBJ.EQ.BIRD)PROP(BIRD)=0
      CALL DROP(OBJ,LOC)
      GOTO 2012

9024  IF (OBJ.EQ.COINS.AND.HERE(VEND)) THEN
         CALL DSTROY(COINS)
         CALL DROP(BATTER,LOC)
         CALL PSPEAK(BATTER,0)
         GOTO 2012
      ELSE IF (OBJ.EQ.BIRD.AND.AT(DRAGON).AND.PROP(DRAGON).EQ.0) THEN
         CALL RSPEAK(154)
         CALL DSTROY(BIRD)
         PROP(BIRD)=0
         IF(PLACE(SNAKE).EQ.PLAC(SNAKE))TALLY2=TALLY2+1
         GOTO 2012
      ELSE IF (OBJ.EQ.BEAR.AND.AT(TROLL)) THEN
         CALL RSPEAK(163)
         CALL MOVE(TROLL,0)
         CALL MOVE(TROLL+100,0)
         CALL MOVE(TROLL2,PLAC(TROLL))
         CALL MOVE(TROLL2+100,FIXD(TROLL))
         CALL JUGGLE(CHASM)
         PROP(TROLL)=2
         GOTO 9021
      ELSE IF (OBJ.EQ.VASE.AND.LOC.NE.PLAC(PILLOW)) THEN
         PROP(VASE)=2
         IF(AT(PILLOW))PROP(VASE)=0
         CALL PSPEAK(VASE,PROP(VASE)+1)
         IF(PROP(VASE).NE.0)FIXED(VASE)=-1
         GOTO 9021
      ELSE
         CALL RSPEAK(54)
         GOTO 9021
      END IF

!  SAY.  ECHO WD2 (OR WD1 IF NO WD2 (SAY WHAT?, ETC.).)  MAGIC WORDS OVERRIDE.

9030  CALL A5TOA1(WD2,WD2X,IA5('".'),CAT,K)
      IF(WD2.EQ.0)CALL A5TOA1(WD1,WD1X,IA5('".'),CAT,K)
      IF(WD2.NE.0)WD1=WD2
      I=VOCAB(WD1,-1)
      IF(.NOT.(I.EQ.62.OR.I.EQ.65.OR.I.EQ.71.OR.I.EQ.2025)) THEN
         PRINT 9032,(CAT(I),I=1,K)
9032     FORMAT(/' OKAY, "',20A1)
         GOTO 2012
      ELSE
         WD2=0
         OBJ=0
         GOTO 2630
      END IF

!  LOCK, UNLOCK, NO OBJECT GIVEN.  ASSUME VARIOUS THINGS IF PRESENT.

8040  SPK=28
      IF(HERE(CLAM))OBJ=CLAM
      IF(HERE(OYSTER))OBJ=OYSTER
      IF(AT(DOOR))OBJ=DOOR
      IF(AT(GRATE))OBJ=GRATE
      IF(OBJ.NE.0.AND.HERE(CHAIN))GOTO 8000
      IF(HERE(CHAIN))OBJ=CHAIN
      IF(OBJ.EQ.0)GOTO 2011

!  LOCK, UNLOCK OBJECT.  SPECIAL STUFF FOR OPENING CLAM/OYSTER AND FOR CHAIN.

9040  IF(OBJ.EQ.CLAM.OR.OBJ.EQ.OYSTER)GOTO 9046
      IF(OBJ.EQ.DOOR)SPK=111
      IF(OBJ.EQ.DOOR.AND.PROP(DOOR).EQ.1)SPK=54
      IF(OBJ.EQ.CAGE)SPK=32
      IF(OBJ.EQ.KEYS)SPK=55
      IF(OBJ.EQ.GRATE.OR.OBJ.EQ.CHAIN)SPK=31
      IF(SPK.NE.31.OR..NOT.HERE(KEYS))GOTO 2011
      IF(OBJ.EQ.CHAIN)GOTO 9048
      IF (CLOSNG) THEN
         K=130
         IF(.NOT.PANIC)CLOCK2=15
         PANIC=.TRUE.
         GOTO 2010
      END IF

9043  K=34+PROP(GRATE)
      PROP(GRATE)=1
      IF(VERB.EQ.LOCK)PROP(GRATE)=0
      K=K+2*PROP(GRATE)
      GOTO 2010

!  CLAM/OYSTER.
9046  K=0
      IF(OBJ.EQ.OYSTER)K=1
      SPK=124+K
      IF(TOTING(OBJ))SPK=120+K
      IF(.NOT.TOTING(TRIDNT))SPK=122+K
      IF(VERB.EQ.LOCK)SPK=61
      IF(SPK.NE.124)GOTO 2011
      CALL DSTROY(CLAM)
      CALL DROP(OYSTER,LOC)
      CALL DROP(PEARL,105)
      GOTO 2011

!  CHAIN.
9048  IF (VERB.NE.LOCK) THEN
         SPK=171
         IF(PROP(BEAR).EQ.0)SPK=41
         IF(PROP(CHAIN).EQ.0)SPK=37
         IF(SPK.NE.171)GOTO 2011
         PROP(CHAIN)=0
         FIXED(CHAIN)=0
         IF(PROP(BEAR).NE.3)PROP(BEAR)=2
         FIXED(BEAR)=2-PROP(BEAR)
         GOTO 2011
      ELSE
         SPK=172
         IF(PROP(CHAIN).NE.0)SPK=34
         IF(LOC.NE.PLAC(CHAIN))SPK=173
         IF(SPK.NE.172)GOTO 2011
         PROP(CHAIN)=2
         IF(TOTING(CHAIN))CALL DROP(CHAIN,LOC)
         FIXED(CHAIN)=-1
         GOTO 2011
      END IF

!  LIGHT LAMP

9070  IF(.NOT.HERE(LAMP))GOTO 2011
      SPK=184
      IF(LIMIT.LT.0)GOTO 2011
      PROP(LAMP)=1
      CALL RSPEAK(39)
      IF(WZDARK)GOTO 2000
      GOTO 2012

!  LAMP OFF

9080  IF(.NOT.HERE(LAMP))GOTO 2011
      PROP(LAMP)=0
      CALL RSPEAK(40)
      IF (DARK()) CALL RSPEAK(16)
      GOTO 2012

!  WAVE.  NO EFFECT UNLESS WAVING ROD AT FISSURE.

9090  IF((.NOT.TOTING(OBJ)).AND.(OBJ.NE.ROD.OR..NOT.TOTING(ROD2)))  &
              SPK=29
      IF(OBJ.NE.ROD.OR..NOT.AT(FISSUR).OR..NOT.TOTING(OBJ)  &
              .OR.CLOSNG)GOTO 2011
      PROP(FISSUR)=1-PROP(FISSUR)
      CALL PSPEAK(FISSUR,2-PROP(FISSUR))
      GOTO 2012

!  ATTACK.  ASSUME TARGET IF UNAMBIGUOUS.  "THROW" ALSO LINKS HERE.  ATTACKABLE
!  OBJECTS FALL INTO TWO CATEGORIES: ENEMIES (SNAKE, DWARF, ETC.)  AND OTHERS
!  (BIRD, CLAM).  AMBIGUOUS IF TWO ENEMIES, OR IF NO ENEMIES BUT TWO OTHERS.

9120  DO I=1,5
         IF(DLOC(I).EQ.LOC.AND.DFLAG.GE.2)GOTO 9122
      END DO
      I=0
9122  IF (OBJ.EQ.0) THEN
         IF(I.NE.0)OBJ=DWARF
         IF(HERE(SNAKE))OBJ=OBJ*100+SNAKE
         IF(AT(DRAGON).AND.PROP(DRAGON).EQ.0)OBJ=OBJ*100+DRAGON
         IF(AT(TROLL))OBJ=OBJ*100+TROLL
         IF(HERE(BEAR).AND.PROP(BEAR).EQ.0)OBJ=OBJ*100+BEAR
         IF(OBJ.GT.100)GOTO 8000
      END IF
      IF (OBJ.EQ.0) THEN
!  CAN'T ATTACK BIRD BY THROWING AXE.
         IF(HERE(BIRD).AND.VERB.NE.THROW)OBJ=BIRD
!  CLAM AND OYSTER BOTH TREATED AS CLAM FOR INTRANSITIVE CASE; NO HARM DONE.
         IF(HERE(CLAM).OR.HERE(OYSTER))OBJ=100*OBJ+CLAM
         IF(OBJ.GT.100)GOTO 8000
      END IF
      IF (OBJ.EQ.BIRD) THEN
         SPK=137
         IF(CLOSED)GOTO 2011
         CALL DSTROY(BIRD)
         PROP(BIRD)=0
         IF(PLACE(SNAKE).EQ.PLAC(SNAKE))TALLY2=TALLY2+1
         SPK=45
      END IF
      IF(OBJ.EQ.0)SPK=44
      IF(OBJ.EQ.CLAM.OR.OBJ.EQ.OYSTER)SPK=150
      IF(OBJ.EQ.SNAKE)SPK=46
      IF(OBJ.EQ.DWARF)SPK=49
      IF(OBJ.EQ.DWARF.AND.CLOSED)GOTO 19000
      IF(OBJ.EQ.DRAGON)SPK=167
      IF(OBJ.EQ.TROLL)SPK=157
      IF(OBJ.EQ.BEAR)SPK=165+(PROP(BEAR)+1)/2
      IF(OBJ.NE.DRAGON.OR.PROP(DRAGON).NE.0)GOTO 2011
!  FUN STUFF FOR DRAGON.  IF HE INSISTS ON ATTACKING IT, WIN!  SET PROP TO DEAD,
!  MOVE DRAGON TO CENTRAL LOC (STILL FIXED), MOVE RUG THERE (NOT FIXED), AND
!  MOVE HIM THERE, TOO.  THEN DO A NULL MOTION TO GET NEW DESCRIPTION.
      CALL RSPEAK(49)
      VERB=0
      OBJ=0
      CALL GETIN(WD1,WD1X,WD2,WD2X)
      IF(WD1.NE.IA5('Y    ').AND.WD1.NE.IA5('YES  '))GOTO 2608
      CALL PSPEAK(DRAGON,1)
      PROP(DRAGON)=2
      PROP(RUG)=0
      K=(PLAC(DRAGON)+FIXD(DRAGON))/2
      CALL MOVE(DRAGON+100,-1)
      CALL MOVE(RUG+100,0)
      CALL MOVE(DRAGON,K)
      CALL MOVE(RUG,K)
      DO OBJ=1,100
         IF(PLACE(OBJ).EQ.PLAC(DRAGON).OR.PLACE(OBJ).EQ.FIXD(DRAGON))  &
               CALL MOVE(OBJ,K)
      END DO
      LOC=K
      K=NULL
      GOTO 8

!  POUR.  IF NO OBJECT, OR OBJECT IS BOTTLE, ASSUME CONTENTS OF BOTTLE.
!  SPECIAL TESTS FOR POURING WATER OR OIL ON PLANT OR RUSTY DOOR.

9130  IF(OBJ.EQ.BOTTLE.OR.OBJ.EQ.0)OBJ=LIQ()
      IF(OBJ.EQ.0)GOTO 8000
      IF(.NOT.TOTING(OBJ))GOTO 2011
      SPK=78
      IF(OBJ.NE.OIL.AND.OBJ.NE.WATER)GOTO 2011
      PROP(BOTTLE)=1
      PLACE(OBJ)=0
      SPK=77

      IF (AT(PLANT)) THEN
         SPK=112
         IF(OBJ.NE.WATER)GOTO 2011
         CALL PSPEAK(PLANT,PROP(PLANT)+1)
         PROP(PLANT)=MOD(PROP(PLANT)+2,6)
         PROP(PLANT2)=PROP(PLANT)/2
         K=NULL
         GOTO 8
      ELSE IF (AT(DOOR)) THEN
         PROP(DOOR)=0
         IF(OBJ.EQ.OIL)PROP(DOOR)=1
         SPK=113+PROP(DOOR)
         GOTO 2011
      ELSE
         GOTO 2011
      END IF

!  EAT.  INTRANSITIVE: ASSUME FOOD IF PRESENT, ELSE ASK WHAT.  TRANSITIVE: FOOD
!  OK, SOME THINGS LOSE APPETITE, REST ARE RIDICULOUS.

8140  IF(.NOT.HERE(FOOD))GOTO 8000
8142  CALL DSTROY(FOOD)
      SPK=72
      GOTO 2011

9140  IF(OBJ.EQ.FOOD)GOTO 8142
      IF(OBJ.EQ.BIRD.OR.OBJ.EQ.SNAKE.OR.OBJ.EQ.CLAM.OR.OBJ.EQ.OYSTER  &
              .OR.OBJ.EQ.DWARF.OR.OBJ.EQ.DRAGON.OR.OBJ.EQ.TROLL  &
              .OR.OBJ.EQ.BEAR)SPK=71
      GOTO 2011

!  DRINK.  IF NO OBJECT, ASSUME WATER AND LOOK FOR IT HERE.  IF WATER IS IN
!  THE BOTTLE, DRINK THAT, ELSE MUST BE AT A WATER LOC, SO DRINK STREAM.

9150  IF(OBJ.EQ.0.AND.LIQLOC(LOC).NE.WATER.AND.(LIQ().NE.WATER  &
              .OR..NOT.HERE(BOTTLE)))GOTO 8000
      IF(OBJ.NE.0.AND.OBJ.NE.WATER)SPK=110
      IF(SPK.EQ.110.OR.LIQ().NE.WATER.OR..NOT.HERE(BOTTLE))GOTO 2011
      PROP(BOTTLE)=1
      PLACE(WATER)=0
      SPK=74
      GOTO 2011

!  RUB.  YIELDS VARIOUS SNIDE REMARKS.

9160  IF(OBJ.NE.LAMP)SPK=76
      GOTO 2011

!  THROW.  SAME AS DISCARD UNLESS AXE.  THEN SAME AS ATTACK EXCEPT IGNORE BIRD,
!  AND IF DWARF IS PRESENT THEN ONE MIGHT BE KILLED.  (ONLY WAY TO DO SO!)
!  AXE ALSO SPECIAL FOR DRAGON, BEAR, AND TROLL.  TREASURES SPECIAL FOR TROLL.

9170  IF(TOTING(ROD2).AND.OBJ.EQ.ROD.AND..NOT.TOTING(ROD))OBJ=ROD2
      IF(.NOT.TOTING(OBJ))GOTO 2011
      IF(OBJ.GE.50.AND.OBJ.LE.MAXTRS.AND.AT(TROLL))GOTO 9178
      IF(OBJ.EQ.FOOD.AND.HERE(BEAR))GOTO 9177
      IF(OBJ.NE.AXE)GOTO 9020
      DO I=1,5
!  NEEDN'T CHECK DFLAG IF AXE IS HERE.
         IF(DLOC(I).EQ.LOC)GOTO 9172
      END DO
      SPK=152
      IF(AT(DRAGON).AND.PROP(DRAGON).EQ.0)GOTO 9175
      SPK=158
      IF(AT(TROLL))GOTO 9175
      IF(HERE(BEAR).AND.PROP(BEAR).EQ.0)GOTO 9176
      OBJ=0
      GOTO 9120

9172  SPK=48
!  IF SAVED NOT = -1, HE BYPASSED THE "START" CALL.
      IF(RANI(3).EQ.0.OR.SAVED.NE.-1)GOTO 9175
      DSEEN(I)=.FALSE.
      DLOC(I)=0
      SPK=47
      DKILL=DKILL+1
      IF(DKILL.EQ.1)SPK=149
9175  CALL RSPEAK(SPK)
      CALL DROP(AXE,LOC)
      K=NULL
      GOTO 8

!  THIS'LL TEACH HIM TO THROW THE AXE AT THE BEAR!
9176  SPK=164
      CALL DROP(AXE,LOC)
      FIXED(AXE)=-1
      PROP(AXE)=1
      CALL JUGGLE(BEAR)
      GOTO 2011

!  BUT THROWING FOOD IS ANOTHER STORY.
9177  OBJ=BEAR
      GOTO 9210

9178  SPK=159
!  SNARF A TREASURE FOR THE TROLL.
      CALL DROP(OBJ,0)
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL DROP(TROLL2,PLAC(TROLL))
      CALL DROP(TROLL2+100,FIXD(TROLL))
      CALL JUGGLE(CHASM)
      GOTO 2011

!  QUIT.  INTRANSITIVE ONLY.  VERIFY INTENT AND EXIT IF THAT'S WHAT HE WANTS.

8180  GAVEUP=YES(22,54,54)
8185  IF(GAVEUP)GOTO 20000
      GOTO 2012

!  FIND.  MIGHT BE CARRYING IT, OR IT MIGHT BE HERE.  ELSE GIVE CAVEAT.

9190  IF(AT(OBJ).OR.(LIQ().EQ.OBJ.AND.AT(BOTTLE))  &
              .OR.K.EQ.LIQLOC(LOC))SPK=94
      DO I=1,5
         IF(DLOC(I).EQ.LOC.AND.DFLAG.GE.2.AND.OBJ.EQ.DWARF)SPK=94
      END DO
      IF(CLOSED)SPK=138
      IF(TOTING(OBJ))SPK=24
      GOTO 2011

!  INVENTORY.  IF OBJECT, TREAT SAME AS FIND.  ELSE REPORT ON CURRENT BURDEN.

8200  SPK=98
      DO I=1,100
         IF (I.EQ.BEAR.OR..NOT.TOTING(I)) CYCLE
         IF(SPK.EQ.98)CALL RSPEAK(99)
         BLKLIN=.FALSE.
         CALL PSPEAK(I,-1)
         BLKLIN=.TRUE.
         SPK=0
      END DO
      IF(TOTING(BEAR))SPK=141
      GOTO 2011

!  FEED.  IF BIRD, NO SEED.  SNAKE, DRAGON, TROLL: QUIP.  IF DWARF, MAKE HIM
!  MAD.  BEAR, SPECIAL.

9210  IF (OBJ.EQ.BIRD) THEN
         SPK=100
      ELSE IF (OBJ.EQ.SNAKE.OR.OBJ.EQ.DRAGON.OR.OBJ.EQ.TROLL) THEN
         SPK=102
         IF(OBJ.EQ.DRAGON.AND.PROP(DRAGON).NE.0)SPK=110
         IF(OBJ.EQ.TROLL)SPK=182
         IF(OBJ.NE.SNAKE.OR.CLOSED.OR..NOT.HERE(BIRD))GOTO 2011
         SPK=101
         CALL DSTROY(BIRD)
         PROP(BIRD)=0
         TALLY2=TALLY2+1
      ELSE IF (OBJ.EQ.DWARF) THEN
         IF(.NOT.HERE(FOOD))GOTO 2011
         SPK=103
         DFLAG=DFLAG+1
      ELSE IF (OBJ.EQ.BEAR) THEN
         IF(PROP(BEAR).EQ.0)SPK=102
         IF(PROP(BEAR).EQ.3)SPK=110
         IF(.NOT.HERE(FOOD))GOTO 2011
         CALL DSTROY(FOOD)
         PROP(BEAR)=1
         FIXED(AXE)=0
         PROP(AXE)=0
         SPK=168
      ELSE
         SPK=14
      END IF
      GOTO 2011

!  FILL.  BOTTLE MUST BE EMPTY, AND SOME LIQUID AVAILABLE.  (VASE IS NASTY.)

9220  IF (OBJ.NE.VASE) THEN
         IF(OBJ.NE.0.AND.OBJ.NE.BOTTLE)GOTO 2011
         IF(OBJ.EQ.0.AND..NOT.HERE(BOTTLE))GOTO 8000
         SPK=107
         IF(LIQLOC(LOC).EQ.0)SPK=106
         IF(LIQ().NE.0)SPK=105
         IF(SPK.NE.107)GOTO 2011
         PROP(BOTTLE)=MOD(COND(LOC),4)/2*2
         K=LIQ()
         IF(TOTING(BOTTLE))PLACE(K)=-1
         IF(K.EQ.OIL)SPK=108
         GOTO 2011
      ELSE
         SPK=29
         IF(LIQLOC(LOC).EQ.0)SPK=144
         IF(LIQLOC(LOC).EQ.0.OR..NOT.TOTING(VASE))GOTO 2011
         CALL RSPEAK(145)
         PROP(VASE)=2
         FIXED(VASE)=-1
         GOTO 9021
      END IF

!  BLAST.  NO EFFECT UNLESS YOU'VE GOT DYNAMITE, WHICH IS A NEAT TRICK!

9230  IF(PROP(ROD2).LT.0.OR..NOT.CLOSED)GOTO 2011
      BONUS=133
      IF(LOC.EQ.115)BONUS=134
      IF(HERE(ROD2))BONUS=135
      CALL RSPEAK(BONUS)
      GOTO 20000

!  SCORE.  GO TO SCORING SECTION, WHICH WILL RETURN TO 8241 IF SCORNG IS TRUE.

8240  SCORNG=.TRUE.
      GOTO 20000

8241  SCORNG=.FALSE.
      PRINT 8243,SCORE,MXSCOR
8243  FORMAT(/' IF YOU WERE TO QUIT NOW, YOU WOULD SCORE',I4  &
              ,' OUT OF A POSSIBLE',I4,'.')
      GAVEUP=YES(143,54,54)
      GOTO 8185

!  FEE FIE FOE FOO (AND FUM).  ADVANCE TO NEXT STATE IF GIVEN IN PROPER ORDER.
!  LOOK UP WD1 IN SECTION 3 OF VOCAB TO DETERMINE WHICH WORD WE'VE GOT.  LAST
!  WORD ZIPS THE EGGS BACK TO THE GIANT ROOM (UNLESS ALREADY THERE).

8250  K=VOCAB(WD1,3)
      SPK=42
      IF (FOOBAR.NE.1-K) THEN
         IF(FOOBAR.NE.0)SPK=151
         GOTO 2011
      END IF

      FOOBAR=K
      IF(K.NE.4)GOTO 2009
      FOOBAR=0
      IF(PLACE(EGGS).EQ.PLAC(EGGS)  &
              .OR.(TOTING(EGGS).AND.LOC.EQ.PLAC(EGGS)))GOTO 2011
!  BRING BACK TROLL IF WE STEAL THE EGGS BACK FROM HIM BEFORE CROSSING.
      IF(PLACE(EGGS).EQ.0.AND.PLACE(TROLL).EQ.0.AND.PROP(TROLL).EQ.0)  &
              PROP(TROLL)=1
      K=2
      IF(HERE(EGGS))K=1
      IF(LOC.EQ.PLAC(EGGS))K=0
      CALL MOVE(EGGS,PLAC(EGGS))
      CALL PSPEAK(EGGS,K)
      GOTO 2012

!  BRIEF.  INTRANSITIVE ONLY.  SUPPRESS LONG DESCRIPTIONS AFTER FIRST TIME.

8260  SPK=156
      ABBNUM=10000
      DETAIL=3
      GOTO 2011

!  READ.  MAGAZINES IN DWARVISH, MESSAGE WE'VE SEEN, AND . . . OYSTER?

8270  IF(HERE(MAGZIN))OBJ=MAGZIN
      IF(HERE(TABLET))OBJ=OBJ*100+TABLET
      IF(HERE(MESSAG))OBJ=OBJ*100+MESSAG
      IF(CLOSED.AND.TOTING(OYSTER))OBJ=OYSTER
      IF(OBJ.GT.100.OR.OBJ.EQ.0.OR.DARK())GOTO 8000

9270  IF(DARK())GOTO 5190
      IF(OBJ.EQ.MAGZIN)SPK=190
      IF(OBJ.EQ.TABLET)SPK=196
      IF(OBJ.EQ.MESSAG)SPK=191
      IF(OBJ.EQ.OYSTER.AND.HINTED(2).AND.TOTING(OYSTER))SPK=194
      IF(OBJ.NE.OYSTER.OR.HINTED(2).OR..NOT.TOTING(OYSTER)  &
              .OR..NOT.CLOSED)GOTO 2011
      HINTED(2)=YES(192,193,54)
      GOTO 2012

!  BREAK.  ONLY WORKS FOR MIRROR IN REPOSITORY AND, OF COURSE, THE VASE.

9280  IF(OBJ.EQ.MIRROR)SPK=148
      IF (OBJ.NE.VASE.OR.PROP(VASE).NE.0) THEN
         IF(OBJ.NE.MIRROR.OR..NOT.CLOSED)GOTO 2011
         CALL RSPEAK(197)
         GOTO 19000
      ELSE
         SPK=198
         IF(TOTING(VASE))CALL DROP(VASE,LOC)
         PROP(VASE)=2
         FIXED(VASE)=-1
         GOTO 2011
      END IF

!  WAKE.  ONLY USE IS TO DISTURB THE DWARVES.

9290  IF(OBJ.NE.DWARF.OR..NOT.CLOSED)GOTO 2011
      CALL RSPEAK(199)
      GOTO 19000

!  SUSPEND.  OFFER TO EXIT LEAVING THINGS RESTARTABLE, BUT REQUIRING A DELAY
!  BEFORE RESTARTING (SO CAN'T SAVE THE WORLD BEFORE TRYING SOMETHING RISKY).
!  UPON RESTARTING, SETUP=-1 CAUSES RETURN TO 8305 TO PICK UP AGAIN.

8300  SPK=201
      IF(DEMO)GOTO 2011
      PRINT 8302,LATNCY
8302  FORMAT(/' I CAN SUSPEND YOUR ADVENTURE FOR YOU SO THAT YOU CAN',  &
              ' RESUME LATER, BUT'/' YOU WILL HAVE TO WAIT AT LEAST',  &
              I3,' MINUTES BEFORE CONTINUING.')
      IF(.NOT.YES(200,54,54))GOTO 2012
      CALL DATIME(SAVED,SAVET)
      SETUP=-1
      CALL CIAO

8305  YEA=START()
      SETUP=3
      K=NULL
      GOTO 8

!  HOURS.  REPORT CURRENT NON-PRIME-TIME HOURS.

8310  CALL MSPEAK(6)
      CALL HOURS
      GOTO 2012
!  HINTS

!  COME HERE IF HE'S BEEN LONG ENOUGH AT REQUIRED LOC(S) FOR SOME UNUSED HINT.
!  HINT NUMBER IS IN VARIABLE "HINT".  BRANCH TO QUICK TEST FOR ADDITIONAL
!  CONDITIONS, THEN COME BACK TO DO NEAT STUFF.  GOTO 40010 IF CONDITIONS ARE
!  MET AND WE WANT TO OFFER THE HINT.  GOTO 40020 TO CLEAR HINTLC BACK TO ZERO,
!  40030 TO TAKE NO ACTION YET.

40000 select case (HINT)
!     GOTO (40400,40500,40600,40700,40800,40900)(HINT-3)
!           CAVE  BIRD  SNAKE MAZE  DARK  WITT
         case (4)
            GOTO 40400
         case (5)
            GOTO 40500
         case (6)
            GOTO 40600
         case (7)
            GOTO 40700
         case (8)
            GOTO 40800
         case (9)
            GOTO 40900
         case default
            CALL BUG(27)
      end select

40010 HINTLC(HINT)=0
      IF(.NOT.YES(HINTS(HINT,3),0,54))GOTO 2602
      PRINT 40012,HINTS(HINT,2)
40012 FORMAT(/' I AM PREPARED TO GIVE YOU A HINT, BUT IT WILL COST YOU',  &
              I2,' POINTS.')
      HINTED(HINT)=YES(175,HINTS(HINT,4),54)
      IF(HINTED(HINT).AND.LIMIT.GT.30)LIMIT=LIMIT+30*HINTS(HINT,2)
40020 HINTLC(HINT)=0
40030 GOTO 2602

!  NOW FOR THE QUICK TESTS.  SEE DATABASE DESCRIPTION FOR ONE-LINE NOTES.

40400 IF(PROP(GRATE).EQ.0.AND..NOT.HERE(KEYS))GOTO 40010
      GOTO 40020

40500 IF(HERE(BIRD).AND.TOTING(ROD).AND.OLDOBJ.EQ.BIRD)GOTO 40010
      GOTO 40030

40600 IF(HERE(SNAKE).AND..NOT.HERE(BIRD))GOTO 40010
      GOTO 40020

40700 IF(ATLOC(LOC).EQ.0.AND.ATLOC(OLDLOC).EQ.0  &
              .AND.ATLOC(OLDLC2).EQ.0.AND.HOLDNG.GT.1)GOTO 40010
      GOTO 40020

40800 IF(PROP(EMRALD).NE.-1.AND.PROP(PYRAM).EQ.-1)GOTO 40010
      GOTO 40020

40900 GOTO 40010
!  CAVE CLOSING AND SCORING


!  THESE SECTIONS HANDLE THE CLOSING OF THE CAVE.  THE CAVE CLOSES "CLOCK1"
!  TURNS AFTER THE LAST TREASURE HAS BEEN LOCATED (INCLUDING THE PIRATE'S
!  CHEST, WHICH MAY OF COURSE NEVER SHOW UP).  NOTE THAT THE TREASURES NEED NOT
!  HAVE BEEN TAKEN YET, JUST LOCATED.  HENCE CLOCK1 MUST BE LARGE ENOUGH TO GET
!  OUT OF THE CAVE (IT ONLY TICKS WHILE INSIDE THE CAVE).  WHEN IT HITS ZERO,
!  WE BRANCH TO 10000 TO START CLOSING THE CAVE, AND THEN SIT BACK AND WAIT FOR
!  HIM TO TRY TO GET OUT.  IF HE DOESN'T WITHIN CLOCK2 TURNS, WE CLOSE THE
!  CAVE; IF HE DOES TRY, WE ASSUME HE PANICS, AND GIVE HIM A FEW ADDITIONAL
!  TURNS TO GET FRANTIC BEFORE WE CLOSE.  WHEN CLOCK2 HITS ZERO, WE BRANCH TO
!  11000 TO TRANSPORT HIM INTO THE FINAL PUZZLE.  NOTE THAT THE PUZZLE DEPENDS
!  UPON ALL SORTS OF RANDOM THINGS.  FOR INSTANCE, THERE MUST BE NO WATER OR
!  OIL, SINCE THERE ARE BEANSTALKS WHICH WE DON'T WANT TO BE ABLE TO WATER,
!  SINCE THE CODE CAN'T HANDLE IT.  ALSO, WE CAN HAVE NO KEYS, SINCE THERE IS A
!  GRATE (HAVING MOVED THE FIXED OBJECT!) THERE SEPARATING HIM FROM ALL THE
!  TREASURES.  MOST OF THESE PROBLEMS ARISE FROM THE USE OF NEGATIVE PROP
!  NUMBERS TO SUPPRESS THE OBJECT DESCRIPTIONS UNTIL HE'S ACTUALLY MOVED THE
!  OBJECTS.

!  WHEN THE FIRST WARNING COMES, WE LOCK THE GRATE, DESTROY THE BRIDGE, KILL
!  ALL THE DWARVES (AND THE PIRATE), REMOVE THE TROLL AND BEAR (UNLESS DEAD),
!  AND SET "CLOSNG" TO TRUE.  LEAVE THE DRAGON; TOO MUCH TROUBLE TO MOVE IT.
!  FROM NOW UNTIL CLOCK2 RUNS OUT, HE CANNOT UNLOCK THE GRATE, MOVE TO ANY
!  LOCATION OUTSIDE THE CAVE (LOC<9), OR CREATE THE BRIDGE.  NOR CAN HE BE
!  RESURRECTED IF HE DIES.  NOTE THAT THE SNAKE IS ALREADY GONE, SINCE HE GOT
!  TO THE TREASURE ACCESSIBLE ONLY VIA THE HALL OF THE MT. KING.  ALSO, HE'S
!  BEEN IN GIANT ROOM (TO GET EGGS), SO WE CAN REFER TO IT.  ALSO ALSO, HE'S
!  GOTTEN THE PEARL, SO WE KNOW THE BIVALVE IS AN OYSTER.  *AND*, THE DWARVES
!  MUST HAVE BEEN ACTIVATED, SINCE WE'VE FOUND CHEST.

10000 PROP(GRATE)=0
      PROP(FISSUR)=0
      DO I=1,6
         DSEEN(I)=.FALSE.
         DLOC(I)=0
      END DO
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL MOVE(TROLL2,PLAC(TROLL))
      CALL MOVE(TROLL2+100,FIXD(TROLL))
      CALL JUGGLE(CHASM)
      IF(PROP(BEAR).NE.3)CALL DSTROY(BEAR)
      PROP(CHAIN)=0
      FIXED(CHAIN)=0
      PROP(AXE)=0
      FIXED(AXE)=0
      CALL RSPEAK(129)
      CLOCK1=-1
      CLOSNG=.TRUE.
      GOTO 19999

!  ONCE HE'S PANICKED, AND CLOCK2 HAS RUN OUT, WE COME HERE TO SET UP THE
!  STORAGE ROOM.  THE ROOM HAS TWO LOCS, HARDWIRED AS 115 (NE) AND 116 (SW).
!  AT THE NE END, WE PLACE EMPTY BOTTLES, A NURSERY OF PLANTS, A BED OF
!  OYSTERS, A PILE OF LAMPS, RODS WITH STARS, SLEEPING DWARVES, AND HIM.  AT
!  THE SW END WE PLACE GRATE OVER TREASURES, SNAKE PIT, COVEY OF CAGED BIRDS,
!  MORE RODS, AND PILLOWS.  A MIRROR STRETCHES ACROSS ONE WALL.  MANY OF THE
!  OBJECTS COME FROM KNOWN LOCATIONS AND/OR STATES (E.G. THE SNAKE IS KNOWN TO
!  HAVE BEEN DESTROYED AND NEEDN'T BE CARRIED AWAY FROM ITS OLD "PLACE"),
!  MAKING THE VARIOUS OBJECTS BE HANDLED DIFFERENTLY.  WE ALSO DROP ALL OTHER
!  OBJECTS HE MIGHT BE CARRYING (LEST HE HAVE SOME WHICH COULD CAUSE TROUBLE,
!  SUCH AS THE KEYS).  WE DESCRIBE THE FLASH OF LIGHT AND TRUNDLE BACK.

11000 PROP(BOTTLE)=PUT(BOTTLE,115,1)
      PROP(PLANT)=PUT(PLANT,115,0)
      PROP(OYSTER)=PUT(OYSTER,115,0)
      PROP(LAMP)=PUT(LAMP,115,0)
      PROP(ROD)=PUT(ROD,115,0)
      PROP(DWARF)=PUT(DWARF,115,0)
      LOC=115
      OLDLOC=115
      NEWLOC=115

!  LEAVE THE GRATE WITH NORMAL (NON-NEGATIVE) PROPERTY.

      FOO=PUT(GRATE,116,0)
      PROP(SNAKE)=PUT(SNAKE,116,1)
      PROP(BIRD)=PUT(BIRD,116,1)
      PROP(CAGE)=PUT(CAGE,116,0)
      PROP(ROD2)=PUT(ROD2,116,0)
      PROP(PILLOW)=PUT(PILLOW,116,0)

      PROP(MIRROR)=PUT(MIRROR,115,0)
      FIXED(MIRROR)=116

      DO I=1,100
         IF(TOTING(I))CALL DSTROY(I)
      END DO

      CALL RSPEAK(132)
      CLOSED=.TRUE.
      GOTO 2

!  ANOTHER WAY WE CAN FORCE AN END TO THINGS IS BY HAVING THE LAMP GIVE OUT.
!  WHEN IT GETS CLOSE, WE COME HERE TO WARN HIM.  WE GO TO 12000 IF THE LAMP
!  AND FRESH BATTERIES ARE HERE, IN WHICH CASE WE REPLACE THE BATTERIES AND
!  CONTINUE.  12200 IS FOR OTHER CASES OF LAMP DYING.  12400 IS WHEN IT GOES
!  OUT, AND 12600 IS IF HE'S WANDERED OUTSIDE AND THE LAMP IS USED UP, IN WHICH
!  CASE WE FORCE HIM TO GIVE UP.

12000 CALL RSPEAK(188)
      PROP(BATTER)=1
      IF(TOTING(BATTER))CALL DROP(BATTER,LOC)
      LIMIT=LIMIT+2500
      LMWARN=.FALSE.
      GOTO 19999

12200 IF(LMWARN.OR..NOT.HERE(LAMP))GOTO 19999
      LMWARN=.TRUE.
      SPK=187
      IF(PLACE(BATTER).EQ.0)SPK=183
      IF(PROP(BATTER).EQ.1)SPK=189
      CALL RSPEAK(SPK)
      GOTO 19999

12400 LIMIT=-1
      PROP(LAMP)=0
      IF(HERE(LAMP))CALL RSPEAK(184)
      GOTO 19999

12600 CALL RSPEAK(185)
      GAVEUP=.TRUE.
      GOTO 20000

!  AND, OF COURSE, DEMO GAMES ARE ENDED BY THE WIZARD.

13000 CALL MSPEAK(1)
      GAVEUP=.TRUE.
      GOTO 20000

!  OH DEAR, HE'S DISTURBED THE DWARVES.

19000 CALL RSPEAK(136)

!  EXIT CODE.

!  THE PRESENT SCORING ALGORITHM IS AS FOLLOWS:
!     OBJECTIVE:          POINTS:      PRESENT TOTAL POSSIBLE:
!  GETTING WELL INTO CAVE   25                  25
!  EACH TREASURE < CHEST    12                  60
!  TREASURE CHEST ITSELF    14                  14
!  EACH TREASURE > CHEST    16                 144
!  SURVIVING             (MAX-NUM)*10           30
!  NOT QUITTING              4                   4
!  REACHING "CLOSNG"        25                  25
!  "CLOSED": QUIT/KILLED    10
!            KLUTZED        25
!            WRONG WAY      30
!            SUCCESS        45                  45
!  CAME TO WITT'S END        1                   1
!  ROUND OUT THE TOTAL       2                   2
!                                      TOTAL:  350
!  (POINTS CAN ALSO BE DEDUCTED FOR USING HINTS.)

20000 SCORE=0
      MXSCOR=0

!  FIRST TALLY UP THE TREASURES.  MUST BE IN BUILDING AND NOT BROKEN.
!  GIVE THE POOR GUY 2 POINTS JUST FOR FINDING EACH TREASURE.

      DO I=50,MAXTRS
         IF (PTEXT(I).EQ.0) CYCLE
         K=12
         IF(I.EQ.CHEST)K=14
         IF(I.GT.CHEST)K=16
         IF(PROP(I).GE.0)SCORE=SCORE+2
         IF(PLACE(I).EQ.3.AND.PROP(I).EQ.0)SCORE=SCORE+K-2
         MXSCOR=MXSCOR+K
      END DO

!  NOW LOOK AT HOW HE FINISHED AND HOW FAR HE GOT.  MAXDIE AND NUMDIE TELL US
!  HOW WELL HE SURVIVED.  GAVEUP SAYS WHETHER HE EXITED VIA QUIT.  DFLAG WILL
!  TELL US IF HE EVER GOT SUITABLY DEEP INTO THE CAVE.  CLOSNG STILL INDICATES
!  WHETHER HE REACHED THE ENDGAME.  AND IF HE GOT AS FAR AS "CAVE CLOSED"
!  (INDICATED BY "CLOSED"), THEN BONUS IS ZERO FOR MUNDANE EXITS OR 133, 134,
!  135 IF HE BLEW IT (SO TO SPEAK).

      SCORE=SCORE+(MAXDIE-NUMDIE)*10
      MXSCOR=MXSCOR+MAXDIE*10
      IF(.NOT.(SCORNG.OR.GAVEUP))SCORE=SCORE+4
      MXSCOR=MXSCOR+4
      IF(DFLAG.NE.0)SCORE=SCORE+25
      MXSCOR=MXSCOR+25
      IF(CLOSNG)SCORE=SCORE+25
      MXSCOR=MXSCOR+25
      IF (CLOSED) THEN
         IF(BONUS.EQ.0)SCORE=SCORE+10
         IF(BONUS.EQ.135)SCORE=SCORE+25
         IF(BONUS.EQ.134)SCORE=SCORE+30
         IF(BONUS.EQ.133)SCORE=SCORE+45
      END IF
      MXSCOR=MXSCOR+45

!  DID HE COME TO WITT'S END AS HE SHOULD?

      IF(PLACE(MAGZIN).EQ.108)SCORE=SCORE+1
      MXSCOR=MXSCOR+1

!  ROUND IT OFF.

      SCORE=SCORE+2
      MXSCOR=MXSCOR+2

!  DEDUCT POINTS FOR HINTS.  HINTS < 4 ARE SPECIAL; SEE DATABASE DESCRIPTION.

      DO I=1,HNTMAX
         IF(HINTED(I))SCORE=SCORE-HINTS(I,2)
      END DO

!  RETURN TO SCORE COMMAND IF THAT'S WHERE WE CAME FROM.

      IF(SCORNG)GOTO 8241

!  THAT SHOULD BE GOOD ENOUGH.  LET'S TELL HIM ALL ABOUT IT.

      PRINT 20100,SCORE,MXSCOR,TURNS
20100 FORMAT(///' YOU SCORED',I4,' OUT OF A POSSIBLE',I4,  &
              ', USING',I5,' TURNS.')

      DO I=1,CLSSES
         IF(CVAL(I).GE.SCORE)GOTO 20210
      END DO
      PRINT 20202
20202 FORMAT(/' YOU JUST WENT OFF MY SCALE!!'/)
      GOTO 25000

20210 CALL SPEAK(CTEXT(I))
      IF (I.NE.CLSSES-1) THEN
         K=CVAL(I)+1-SCORE
         WORD='S.'
         IF(K.EQ.1)WORD='. '
         PRINT 20212,K,WORD
20212    FORMAT(/' TO ACHIEVE THE NEXT HIGHER RATING, YOU NEED',I3,  &
               ' MORE POINT',A2/)
      ELSE
         PRINT 20222
20222    FORMAT(/' TO ACHIEVE THE NEXT HIGHER RATING ',  &
               'WOULD BE A NEAT TRICK!'//' CONGRATULATIONS!!'/)
      END IF

25000 STOP


contains

!  TOTING(OBJ)  = TRUE IF THE OBJ IS BEING CARRIED
   logical function TOTING(OBJ)
      integer, intent(in) :: OBJ
      TOTING = PLACE(OBJ) == -1
      return
   end function

!  HERE(OBJ)    = TRUE IF THE OBJ IS AT "LOC" (OR IS BEING CARRIED)
   logical function HERE(OBJ)
      integer, intent(in) :: OBJ
      HERE = PLACE(OBJ) == LOC .or. TOTING(OBJ)
      return
   end function

!  AT(OBJ)      = TRUE IF ON EITHER SIDE OF TWO-PLACED OBJECT
   logical function AT(OBJ)
      integer, intent(in) :: OBJ
      AT = PLACE(OBJ) == LOC .or. FIXED(OBJ) == LOC
      return
   end function

   integer function LIQ2(PBOTL)
      integer, intent(in) :: PBOTL
      LIQ2 = (1 - PBOTL) * WATER + (PBOTL / 2) * (WATER + OIL)
      return
   end function

!  LIQ()   = OBJECT NUMBER OF LIQUID IN BOTTLE
   integer function LIQ()
      LIQ = LIQ2(MAX(PROP(BOTTLE), -1-PROP(BOTTLE)))
      return
   end function

!  LIQLOC(LOC)  = OBJECT NUMBER OF LIQUID (IF ANY) AT LOC
   integer function LIQLOC(LOC)
      integer, intent(in) :: LOC
      LIQLOC = LIQ2((MOD(COND(LOC) / 2 * 2, 8) - 5)  &
            * MOD(COND(LOC) / 4, 2) + 1)
      return
   end function

!  BITSET(L,N)  = TRUE IF COND(L) HAS BIT N SET (BIT 0 IS UNITS BIT)
   logical function BITSET(L, N)
      integer, intent(in) :: L, N
      BITSET = IAND(COND(L), ISHFT(1, N)) /= 0
      return
   end function

!  FORCED(LOC)  = TRUE IF LOC MOVES WITHOUT ASKING FOR INPUT (COND=2)
   logical function FORCED(LOC)
      integer, intent(in) :: LOC
      FORCED = COND(LOC) == 2
      return
   end function

!  DARK()  = TRUE IF LOCATION "LOC" IS DARK
   logical function DARK()
      DARK = MOD(COND(LOC), 2) == 0 .and.  &
            (PROP(LAMP) == 0 .or. .not. HERE(LAMP))
      return
   end function

!  PCT(N)       = TRUE N% OF THE TIME (N INTEGER FROM 0 TO 100)
   logical function PCT(N)
      integer, intent(in) :: N
      PCT = RANI(100) < N
      return
   end function

end program
