!  ADVENTURES

!  CURRENT LIMITS:
!    9650 WORDS OF MESSAGE TEXT (LINES, LINSIZ).
!     750 TRAVEL OPTIONS (TRAVEL, TRVSIZ).
!     300 VOCABULARY WORDS (KTAB, ATAB, TABSIZ).
!     150 LOCATIONS (LTEXT, STEXT, KEY, COND, ABB, ATLOC, LOCSIZ).
!     100 OBJECTS (PLAC, PLACE, FIXD, FIXED, LINK (TWICE), PTEXT, PROP).
!      35 "ACTION" VERBS (ACTSPK, VRBSIZ).
!     205 RANDOM MESSAGES (RTEXT, RTXSIZ).
!      12 DIFFERENT PLAYER CLASSIFICATIONS (CTEXT, CVAL, CLSMAX).
!      20 HINTS, LESS 3 (HINTLC, HINTED, HINTS, HNTSIZ).
!      35 MAGIC MESSAGES (MTEXT, MAGSIZ).
!  THERE ARE ALSO LIMITS WHICH CANNOT BE EXCEEDED DUE TO THE STRUCTURE OF
!  THE DATABASE.  (E.G., THE VOCABULARY USES N/1000 TO DETERMINE WORD TYPE,
!  SO THERE CAN'T BE MORE THAN 1000 WORDS.)  THESE UPPER LIMITS ARE:
!    1000 NON-SYNONYMOUS VOCABULARY WORDS
!     300 LOCATIONS
!     100 OBJECTS

program advent
      use pdp10
      use places
      use text
      use words
      use wizcom
      use advn2
      implicit none

      CHARACTER*80 TXT
      CHARACTER*5 WORD

      COMMON /BLKCOM/ BLKLIN
      LOGICAL BLKLIN
      DATA BLKLIN/.true./

      INTEGER TRAVEL,LTEXT,STEXT,KEY,COND
      INTEGER PLAC,FIXD,PROP
      INTEGER ACTSPK,CTEXT,CVAL
      INTEGER HINTLC,HINTS
      LOGICAL HINTED,QHINT
      INTEGER TK,DLOC,ODLOC
      LOGICAL DSEEN
      CHARACTER*1 CAT
      DIMENSION TRAVEL(750)
      DIMENSION LTEXT(LOCSIZ),STEXT(LOCSIZ),KEY(LOCSIZ),COND(LOCSIZ)
      DIMENSION PLAC(100),FIXD(100),PROP(100)
      DIMENSION ACTSPK(35)
      DIMENSION CTEXT(12),CVAL(12)
      DIMENSION HINTLC(20),HINTED(20),HINTS(20,4)
      DIMENSION TK(20),DSEEN(6),DLOC(6),ODLOC(6)
      DIMENSION CAT(20)

      INTEGER TRVSIZ,VRBSIZ,CLSMAX,HNTSIZ
      DATA TRVSIZ/750/,VRBSIZ/35/,CLSMAX/12/,HNTSIZ/20/

      INTEGER FOO,FOOBAR
      INTEGER I,J,K,L,M,N
      INTEGER KK,K2,KQ,LL
      INTEGER EOL,IOE,TAB
      INTEGER ABBNUM,ATTACK,BONUS,CLSSES,DETAIL
      INTEGER OBJ,OLDOBJ
      INTEGER LOC,CHLOC,CHLOC2,KNFLOC,NEWLOC,OLDLOC,OLDLC2
      INTEGER CLOCK1,CLOCK2
      INTEGER DFLAG,DKILL,DTOTAL,DALTLC,STICK
      INTEGER NUMDIE,MAXDIE
      INTEGER SCORE,MXSCOR
      INTEGER HINT,HNTMAX
      INTEGER WD1,WD1X,WD2,WD2X
      INTEGER IWEST,LIMIT,LINUSE,MAXTRS,SECT,SPK,TABNDX
      INTEGER TALLY,TALLY2,TRVS,TURNS,VERB
      INTEGER XXD,XXT,YYD,YYT

!  WZDARK SAYS WHETHER THE LOC HE'S LEAVING WAS DARK
!  LMWARN SAYS WHETHER HE'S BEEN WARNED ABOUT LAMP GOING DIM
!  CLOSNG SAYS WHETHER ITS CLOSING TIME YET
!  PANIC SAYS WHETHER HE'S FOUND OUT HE'S TRAPPED IN THE CAVE
!  CLOSED SAYS WHETHER WE'RE ALL THE WAY CLOSED
!  GAVEUP SAYS WHETHER HE EXITED VIA "QUIT"
!  SCORNG INDICATES TO THE SCORE ROUTINE WHETHER WE'RE DOING A "SCORE" COMMAND
!  DEMO IS TRUE IF THIS IS A PRIME-TIME DEMONSTRATION GAME
!  YEA IS RANDOM YES/NO REPLY

      LOGICAL WZDARK,LMWARN,CLOSNG,PANIC,CLOSED,GAVEUP,SCORNG,DEMO,YEA

!  DESCRIPTION OF THE DATABASE FORMAT
!
!
!  THE DATA FILE CONTAINS SEVERAL SECTIONS.  EACH BEGINS WITH A LINE CONTAINING
!  A NUMBER IDENTIFYING THE SECTION, AND ENDS WITH A LINE CONTAINING "-1".
!
!  SECTION 1: LONG FORM DESCRIPTIONS.  EACH LINE CONTAINS A LOCATION NUMBER,
!     A TAB, AND A LINE OF TEXT.  THE SET OF (NECESSARILY ADJACENT) LINES
!     WHOSE NUMBERS ARE X FORM THE LONG DESCRIPTION OF LOCATION X.
!  SECTION 2: SHORT FORM DESCRIPTIONS.  SAME FORMAT AS LONG FORM.  NOT ALL
!     PLACES HAVE SHORT DESCRIPTIONS.
!  SECTION 3: TRAVEL TABLE.  EACH LINE CONTAINS A LOCATION NUMBER (X), A SECOND
!     LOCATION NUMBER (Y), AND A LIST OF MOTION NUMBERS (SEE SECTION 4).
!     EACH MOTION REPRESENTS A VERB WHICH WILL GO TO Y IF CURRENTLY AT X.
!     Y, IN TURN, IS INTERPRETED AS FOLLOWS.  LET M=Y/1000, N=Y MOD 1000.
!           IF N<=300       IT IS THE LOCATION TO GO TO.
!           IF 300<N<=500   N-300 IS USED IN A COMPUTED GOTO TO
!                                 A SECTION OF SPECIAL CODE.
!           IF N>500        MESSAGE N-500 FROM SECTION 6 IS PRINTED,
!                                 AND HE STAYS WHEREVER HE IS.
!     MEANWHILE, M SPECIFIES THE CONDITIONS ON THE MOTION.
!           IF M=0          IT'S UNCONDITIONAL.
!           IF 0<M<100      IT IS DONE WITH M% PROBABILITY.
!           IF M=100        UNCONDITIONAL, BUT FORBIDDEN TO DWARVES.
!           IF 100<M<=200   HE MUST BE CARRYING OBJECT M-100.
!           IF 200<M<=300   MUST BE CARRYING OR IN SAME ROOM AS M-200.
!           IF 300<M<=400   PROP(M MOD 100) MUST *NOT* BE 0.
!           IF 400<M<=500   PROP(M MOD 100) MUST *NOT* BE 1.
!           IF 500<M<=600   PROP(M MOD 100) MUST *NOT* BE 2, ETC.
!     IF THE CONDITION (IF ANY) IS NOT MET, THEN THE NEXT *DIFFERENT*
!     "DESTINATION" VALUE IS USED (UNLESS IT FAILS TO MEET *ITS* CONDITIONS,
!     IN WHICH CASE THE NEXT IS FOUND, ETC.).  TYPICALLY, THE NEXT DEST WILL
!     BE FOR ONE OF THE SAME VERBS, SO THAT ITS ONLY USE IS AS THE ALTERNATE
!     DESTINATION FOR THOSE VERBS.  FOR INSTANCE:
!           15      110022  29      31      34      35      23      43
!           15      14      29
!     THIS SAYS THAT, FROM LOC 15, ANY OF THE VERBS 29, 31, ETC., WILL TAKE
!     HIM TO 22 IF HE'S CARRYING OBJECT 10, AND OTHERWISE WILL GO TO 14.
!           11      303008  49
!           11      9       50
!     THIS SAYS THAT, FROM 11, 49 TAKES HIM TO 8 UNLESS PROP(3)=0, IN WHICH
!     CASE HE GOES TO 9.  VERB 50 TAKES HIM TO 9 REGARDLESS OF PROP(3).
!  SECTION 4: VOCABULARY.  EACH LINE CONTAINS A NUMBER (N), A TAB, AND A
!     FIVE-LETTER WORD.  CALL M=N/1000.  IF M=0, THEN THE WORD IS A MOTION
!     VERB FOR USE IN TRAVELLING (SEE SECTION 3).  ELSE, IF M=1, THE WORD IS
!     AN OBJECT.  ELSE, IF M=2, THE WORD IS AN ACTION VERB (SUCH AS "CARRY"
!     OR "ATTACK").  ELSE, IF M=3, THE WORD IS A SPECIAL CASE VERB (SUCH AS
!     "DIG") AND N MOD 1000 IS AN INDEX INTO SECTION 6.  OBJECTS FROM 50 TO
!     (CURRENTLY, ANYWAY) 79 ARE CONSIDERED TREASURES (FOR PIRATE, CLOSEOUT).
!  SECTION 5: OBJECT DESCRIPTIONS.  EACH LINE CONTAINS A NUMBER (N), A TAB,
!     AND A MESSAGE.  IF N IS FROM 1 TO 100, THE MESSAGE IS THE "INVENTORY"
!     MESSAGE FOR OBJECT N.  OTHERWISE, N SHOULD BE 000, 100, 200, ETC., AND
!     THE MESSAGE SHOULD BE THE DESCRIPTION OF THE PRECEDING OBJECT WHEN ITS
!     PROP VALUE IS N/100.  THE N/100 IS USED ONLY TO DISTINGUISH MULTIPLE
!     MESSAGES FROM MULTI-LINE MESSAGES; THE PROP INFO ACTUALLY REQUIRES ALL
!     MESSAGES FOR AN OBJECT TO BE PRESENT AND CONSECUTIVE.  PROPERTIES WHICH
!     PRODUCE NO MESSAGE SHOULD BE GIVEN THE MESSAGE ">$<".
!  SECTION 6: ARBITRARY MESSAGES.  SAME FORMAT AS SECTIONS 1, 2, AND 5, EXCEPT
!     THE NUMBERS BEAR NO RELATION TO ANYTHING (EXCEPT FOR SPECIAL VERBS
!     IN SECTION 4).
!  SECTION 7: OBJECT LOCATIONS.  EACH LINE CONTAINS AN OBJECT NUMBER AND ITS
!     INITIAL LOCATION (ZERO (OR OMITTED) IF NONE).  IF THE OBJECT IS
!     IMMOVABLE, THE LOCATION IS FOLLOWED BY A "-1".  IF IT HAS TWO LOCATIONS
!     (E.G. THE GRATE) THE FIRST LOCATION IS FOLLOWED WITH THE SECOND, AND
!     THE OBJECT IS ASSUMED TO BE IMMOVABLE.
!  SECTION 8: ACTION DEFAULTS.  EACH LINE CONTAINS AN "ACTION-VERB" NUMBER AND
!     THE INDEX (IN SECTION 6) OF THE DEFAULT MESSAGE FOR THE VERB.
!  SECTION 9: LIQUID ASSETS, ETC.  EACH LINE CONTAINS A NUMBER (N) AND UP TO 20
!     LOCATION NUMBERS.  BIT N (WHERE 0 IS THE UNITS BIT) IS SET IN COND(LOC)
!     FOR EACH LOC GIVEN.  THE COND BITS CURRENTLY ASSIGNED ARE:
!           0     LIGHT
!           1     IF BIT 2 IS ON: ON FOR OIL, OFF FOR WATER
!           2     LIQUID ASSET, SEE BIT 1
!           3     PIRATE DOESN'T GO HERE UNLESS FOLLOWING PLAYER
!     OTHER BITS ARE USED TO INDICATE AREAS OF INTEREST TO "HINT" ROUTINES:
!           4     TRYING TO GET INTO CAVE
!           5     TRYING TO CATCH BIRD
!           6     TRYING TO DEAL WITH SNAKE
!           7     LOST IN MAZE
!           8     PONDERING DARK ROOM
!           9     AT WITT'S END
!     COND(LOC) IS SET TO 2, OVERRIDING ALL OTHER BITS, IF LOC HAS FORCED
!     MOTION.
!  SECTION 10: CLASS MESSAGES.  EACH LINE CONTAINS A NUMBER (N), A TAB, AND A
!     MESSAGE DESCRIBING A CLASSIFICATION OF PLAYER.  THE SCORING SECTION
!     SELECTS THE APPROPRIATE MESSAGE, WHERE EACH MESSAGE IS CONSIDERED TO
!     APPLY TO PLAYERS WHOSE SCORES ARE HIGHER THAN THE PREVIOUS N BUT NOT
!     HIGHER THAN THIS N.  NOTE THAT THESE SCORES PROBABLY CHANGE WITH EVERY
!     MODIFICATION (AND PARTICULARLY EXPANSION) OF THE PROGRAM.
!  SECTION 11: HINTS.  EACH LINE CONTAINS A HINT NUMBER (CORRESPONDING TO A
!     COND BIT, SEE SECTION 9), THE NUMBER OF TURNS HE MUST BE AT THE RIGHT
!     LOC(S) BEFORE TRIGGERING THE HINT, THE POINTS DEDUCTED FOR TAKING THE
!     HINT, THE MESSAGE NUMBER (SECTION 6) OF THE QUESTION, AND THE MESSAGE
!     NUMBER OF THE HINT.  THESE VALUES ARE STASHED IN THE "HINTS" ARRAY.
!     HNTMAX IS SET TO THE MAX HINT NUMBER (<= HNTSIZ).  NUMBERS 1-3 ARE
!     UNUSABLE SINCE COND BITS ARE OTHERWISE ASSIGNED, SO 2 IS USED TO
!     REMEMBER IF HE'S READ THE CLUE IN THE REPOSITORY, AND 3 IS USED TO
!     REMEMBER WHETHER HE ASKED FOR INSTRUCTIONS (GETS MORE TURNS, BUT LOSES
!     POINTS).
!  SECTION 12: MAGIC MESSAGES. IDENTICAL TO SECTION 6 EXCEPT PUT IN A SEPARATE
!     SECTION FOR EASIER REFERENCE.  MAGIC MESSAGES ARE USED BY THE STARTUP,
!     MAINTENANCE MODE, AND RELATED ROUTINES.
!  SECTION 0: END OF DATABASE.
!  READ THE DATABASE IF WE HAVE NOT YET DONE SO

      if (SETUP /= 0) GOTO 1100
      PRINT 1000
1000  FORMAT(' INITIALISING...')

!  CLEAR OUT THE VARIOUS TEXT-POINTER ARRAYS.  ALL TEXT IS STORED IN ARRAY
!  LINES; EACH LINE IS PRECEDED BY A WORD POINTING TO THE NEXT POINTER (I.E.
!  THE WORD FOLLOWING THE END OF THE LINE).  THE POINTER IS NEGATIVE IF THIS IS
!  FIRST LINE OF A MESSAGE.  THE TEXT-POINTER ARRAYS CONTAIN INDICES OF
!  POINTER-WORDS IN LINES.  STEXT(N) IS SHORT DESCRIPTION OF LOCATION N.
!  LTEXT(N) IS LONG DESCRIPTION.  PTEXT(N) POINTS TO MESSAGE FOR PROP(N)=0.
!  SUCCESSIVE PROP MESSAGES ARE FOUND BY CHASING POINTERS.  RTEXT CONTAINS
!  SECTION 6'S STUFF.  CTEXT(N) POINTS TO A PLAYER-CLASS MESSAGE.  MTEXT IS FOR
!  SECTION 12.  WE ALSO CLEAR COND.  SEE DESCRIPTION OF SECTION 9 FOR DETAILS.

      do I=1,300
         if (I <= 100) PTEXT(I)=0
         if (I <= RTXSIZ) RTEXT(I)=0
         if (I <= CLSMAX) CTEXT(I)=0
         if (I <= MAGSIZ) MTEXT(I)=0
         if (I > LOCSIZ) cycle
         STEXT(I)=0
         LTEXT(I)=0
         COND(I)=0
         KEY(I)=0
      end do
      PLAC=0
      FIXD=0

      OPEN(1,FILE='adven.dat')
      SETUP=1
      LINUSE=1
      TRVS=1
      CLSSES=1

!  START NEW DATA SECTION.  SECT IS THE SECTION NUMBER.

1002  READ(1,1003)SECT
1003  FORMAT(I2)
      OLDLOC=-1
      select case (SECT)
         case (0)
            GOTO 1100
         case (3)
            GOTO 1030
         case (4)
            GOTO 1040
         case (7)
            GOTO 1050
         case (8)
            GOTO 1060
         case (9)
            GOTO 1070
         case (11)
            GOTO 1080
         case (1, 2, 5, 6, 10, 12)
            GOTO 1004
         case default
            CALL BUG(9)
      end select

!  SECTIONS 1, 2, 5, 6, 10, 12.  READ MESSAGES AND SET UP POINTERS.

1004  do
         READ(1,'(A80)')TXT
         TAB=INDEX(TXT,ACHAR(9))
         if (TAB == 0) TAB=LEN_TRIM(TXT)+1
         EOL=LEN_TRIM(TXT)
         READ(TXT(:TAB-1),'(I4)')LOC
         if (LOC == -1) exit
         do J=1,15
            K=TAB+5*J
            if (K-4 > EOL) then
               LINES(LINUSE+J)=IA5('     ')
            else
               WORD=TXT(K-4:K)
               LINES(LINUSE+J)=IA5(WORD)
            end if
         end do
         KK=LINES(LINUSE+15)
!        READ(TXT(TAB+1:),1005)(LINES(J),J=LINUSE+1,LINUSE+14),KK
!1005    FORMAT(15A5)
         if (KK /= IA5('     ')) CALL BUG(0)
         do K=1,14
            KK=LINUSE+15-K
            if (LINES(KK) /= IA5('     ')) GOTO 1007
         end do
         ! AVOID F40 BUG IF CRLF BROKEN ACROSS RECORD BOUNDARY
         if (LOC == 0) cycle
         CALL BUG(1)
1007     LINES(LINUSE)=KK+1
         if (LOC /= OLDLOC) then
            LINES(LINUSE)=-LINES(LINUSE)
            select case (SECT)
               case (1)
                  LTEXT(LOC)=LINUSE
               case (5)
                  if (LOC > 0 .and. LOC <= 100) PTEXT(LOC)=LINUSE
               case (6)
                  if (LOC > RTXSIZ) CALL BUG(6)
                  RTEXT(LOC)=LINUSE
               case (10)
                  CTEXT(CLSSES)=LINUSE
                  CVAL(CLSSES)=LOC
                  CLSSES=CLSSES+1
               case (12)
                  if (LOC > MAGSIZ) CALL BUG(6)
                  MTEXT(LOC)=LINUSE
               case default
                  STEXT(LOC)=LINUSE
            end select
         end if

         LINUSE=KK+1
         LINES(LINUSE)=-1
         OLDLOC=LOC
         if (LINUSE+14 > LINSIZ) CALL BUG(2)
      end do
      GOTO 1002

!  THE STUFF FOR SECTION 3 IS ENCODED HERE.  EACH "FROM-LOCATION" GETS A
!  CONTIGUOUS SECTION OF THE "TRAVEL" ARRAY.  EACH ENTRY IN TRAVEL IS
!  NEWLOC*1000 + KEYWORD (FROM SECTION 4, MOTION VERBS), AND IS NEGATED IF
!  THIS IS THE LAST ENTRY FOR THIS LOCATION.  KEY(N) IS THE INDEX IN TRAVEL
!  OF THE FIRST OPTION AT LOCATION N.

1030  do
         READ(1,'(A80)')TXT
         TK=0
         READ(TXT,*,IOSTAT=IOE)LOC,NEWLOC,TK
1031     FORMAT(99I4)
         if (LOC == 0) cycle  ! AVOID AFOREMENTIONED F40 BUG
         if (LOC == -1) exit
         if (KEY(LOC) == 0) then
            KEY(LOC)=TRVS
         else
            TRAVEL(TRVS-1)=-TRAVEL(TRVS-1)
         end if
         do L=1,20
            if (TK(L) == 0) exit
            TRAVEL(TRVS)=NEWLOC*1000+TK(L)
            TRVS=TRVS+1
            if (TRVS == TRVSIZ) CALL BUG(3)
         end do
         TRAVEL(TRVS-1)=-TRAVEL(TRVS-1)
      end do
      GOTO 1002

!  HERE WE READ IN THE VOCABULARY.  KTAB(N) IS THE WORD NUMBER, ATAB(N) IS
!  THE CORRESPONDING WORD.  THE -1 AT THE END OF SECTION 4 IS LEFT IN KTAB
!  AS AN END-MARKER.  THE WORDS ARE GIVEN A MINIMAL HASH TO MAKE READING THE
!  CORE-IMAGE HARDER.  NOTE THAT '/7-08' HAD BETTER NOT BE IN THE LIST, SINCE
!  IT COULD HASH TO -1.

1040  do TABNDX=1,TABSIZ
1043     READ(1,'(A80)')TXT
         READ(TXT,*,IOSTAT=IOE)KTAB(TABNDX),WORD
         ATAB(TABNDX)=IA5(WORD)
1041     FORMAT(I2,A5)
         if (KTAB(TABNDX) == 0) GOTO 1043  ! AVOID AFOREMENTIONED F40 BUG
         if (KTAB(TABNDX) == -1) GOTO 1002
         ATAB(TABNDX)=IEOR(ATAB(TABNDX),IA5('PHROG'))
      end do
      CALL BUG(4)

!  READ IN THE INITIAL LOCATIONS FOR EACH OBJECT.  ALSO THE IMMOVABILITY INFO.
!  PLAC CONTAINS INITIAL LOCATIONS OF OBJECTS.  FIXD IS -1 FOR IMMOVABLE
!  OBJECTS (INCLUDING THE SNAKE), OR = SECOND LOC FOR TWO-PLACED OBJECTS.

1050  do
         READ(1,'(A80)')TXT
         K=0
         READ(TXT,*,IOSTAT=IOE)OBJ,J,K
         if (OBJ == -1) exit
         PLAC(OBJ)=J
         FIXD(OBJ)=K
      end do
      GOTO 1002

!  READ DEFAULT MESSAGE NUMBERS FOR ACTION VERBS, STORE IN ACTSPK.

1060  do
         READ(1,'(A80)')TXT
         READ(TXT,*,IOSTAT=IOE)VERB,J
         if (VERB == -1) exit
         ACTSPK(VERB)=J
      end do
      GOTO 1002

!  READ INFO ABOUT AVAILABLE LIQUIDS AND OTHER CONDITIONS, STORE IN COND.

1070  do
         READ(1,'(A80)')TXT
         TK=0
         READ(TXT,*,IOSTAT=IOE)K,TK
         if (K == -1) exit
         do I=1,20
            LOC=TK(I)
            if (LOC == 0) exit
            if (BITSET(LOC,K)) CALL BUG(8)
            COND(LOC)=COND(LOC)+ISHFT(1,K)
         end do
      end do
      GOTO 1002

!  READ DATA FOR HINTS.

1080  HNTMAX=0
      do
         READ(1,'(A80)')TXT
         TK=0
         READ(TXT,*,IOSTAT=IOE)K,TK
         if (K == -1) exit
         if (K == 0) cycle
         if (K < 0 .or. K > HNTSIZ) CALL BUG(7)
         do I=1,4
            HINTS(K,I)=TK(I)
         end do
         HNTMAX=MAX(HNTMAX,K)
      end do
      GOTO 1002

!  FINISH CONSTRUCTING INTERNAL DATA FORMAT

!  IF SETUP=2 WE DON'T NEED TO DO THIS.  IT'S ONLY NECESSARY IF WE HAVEN'T DONE
!  IT AT ALL OR IF THE PROGRAM HAS BEEN RUN SINCE THEN.

1100  if (SETUP == 2) GOTO 1
      if (SETUP == -1) GOTO 8305

!  HAVING READ IN THE DATABASE, CERTAIN THINGS ARE NOW CONSTRUCTED.  PROPS ARE
!  SET TO ZERO.  WE FINISH SETTING UP COND BY CHECKING FOR FORCED-MOTION TRAVEL
!  ENTRIES.  THE PLAC AND FIXD ARRAYS ARE USED TO SET UP ATLOC(N) AS THE FIRST
!  OBJECT AT LOCATION N, AND LINK(OBJ) AS THE NEXT OBJECT AT THE SAME LOCATION
!  AS OBJ.  (OBJ>100 INDICATES THAT FIXED(OBJ-100)=LOC; LINK(OBJ) IS STILL THE
!  CORRECT LINK TO USE.)  ABB IS ZEROED; IT CONTROLS WHETHER THE ABBREVIATED
!  DESCRIPTION IS PRINTED.  COUNTS MOD 5 UNLESS "LOOK" IS USED.

      do I=1,100
         PLACE(I)=0
         PROP(I)=0
         LINK(I)=0
         LINK(I+100)=0
      end do

      do I=1,LOCSIZ
         ABB(I)=0
         if (LTEXT(I) /= 0  .and.  KEY(I) /= 0) then
            K=KEY(I)
            if (MOD(ABS(TRAVEL(K)),1000) == 1) COND(I)=2
         end if
         ATLOC(I)=0
      end do

!  SET UP THE ATLOC AND LINK ARRAYS AS DESCRIBED ABOVE.  WE'LL USE THE DROP
!  SUBROUTINE, WHICH PREFACES NEW OBJECTS ON THE LISTS.  SINCE WE WANT THINGS
!  IN THE OTHER ORDER, WE'LL RUN THE LOOP BACKWARDS.  IF THE OBJECT IS IN TWO
!  LOCS, WE DROP IT TWICE.  THIS ALSO SETS UP "PLACE" AND "FIXED" AS COPIES OF
!  "PLAC" AND "FIXD".  ALSO, SINCE TWO-PLACED OBJECTS ARE TYPICALLY BEST
!  DESCRIBED LAST, WE'LL DROP THEM FIRST.

      do I=1,100
         K=101-I
         if (FIXD(K) <= 0) cycle
         CALL DROP(K+100,FIXD(K))
         CALL DROP(K,PLAC(K))
      end do

      do I=1,100
         K=101-I
         FIXED(K)=FIXD(K)
         if (PLAC(K) /= 0 .and. FIXD(K) <= 0) CALL DROP(K,PLAC(K))
      end do

!  TREASURES, AS NOTED EARLIER, ARE OBJECTS 50 THROUGH MAXTRS (CURRENTLY 79).
!  THEIR PROPS ARE INITIALLY -1, AND ARE SET TO 0 THE FIRST TIME THEY ARE
!  DESCRIBED.  TALLY KEEPS TRACK OF HOW MANY ARE NOT YET FOUND, SO WE KNOW
!  WHEN TO CLOSE THE CAVE.  TALLY2 COUNTS HOW MANY CAN NEVER BE FOUND (E.G. IF
!  LOST BIRD OR BRIDGE).

      MAXTRS=79
      TALLY=0
      TALLY2=0
      do I=50,MAXTRS
         if (PTEXT(I) /= 0) PROP(I)=-1
         TALLY=TALLY-PROP(I)
      end do

!  CLEAR THE HINT STUFF.  HINTLC(I) IS HOW LONG HE'S BEEN AT LOC WITH COND BIT
!  I.  HINTED(I) IS TRUE IFF HINT I HAS BEEN USED.

      do I=1,HNTMAX
         HINTED(I)=.false.
         HINTLC(I)=0
      end do

      call SETWORDS()

!  INITIALISE THE DWARVES.  DLOC IS LOC OF DWARVES, HARD-WIRED IN.  ODLOC IS
!  PRIOR LOC OF EACH DWARF, INITIALLY GARBAGE.  DALTLC IS ALTERNATE INITIAL LOC
!  FOR DWARF, IN CASE ONE OF THEM STARTS OUT ON TOP OF THE ADVENTURER.  (NO 2
!  OF THE 5 INITIAL LOCS ARE ADJACENT.)  DSEEN IS TRUE IF DWARF HAS SEEN HIM.
!  DFLAG CONTROLS THE LEVEL OF ACTIVATION OF ALL THIS:
!     0     NO DWARF STUFF YET (WAIT UNTIL REACHES HALL OF MISTS)
!     1     REACHED HALL OF MISTS, BUT HASN'T MET FIRST DWARF
!     2     MET FIRST DWARF, OTHERS START MOVING, NO KNIVES THROWN YET
!     3     A KNIFE HAS BEEN THROWN (FIRST SET ALWAYS MISSES)
!     3+    DWARVES ARE MAD (INCREASES THEIR ACCURACY)
!  SIXTH DWARF IS SPECIAL (THE PIRATE).  HE ALWAYS STARTS AT HIS CHEST'S
!  EVENTUAL LOCATION INSIDE THE MAZE.  THIS LOC IS SAVED IN CHLOC FOR REF.
!  THE DEAD END IN THE OTHER MAZE HAS ITS LOC STORED IN CHLOC2.

      CHLOC=114
      CHLOC2=140
      do I=1,6
         DSEEN(I)=.false.
      end do
      DFLAG=0
      DLOC(1)=19
      DLOC(2)=27
      DLOC(3)=33
      DLOC(4)=44
      DLOC(5)=64
      DLOC(6)=CHLOC
      DALTLC=18

!  OTHER RANDOM FLAGS AND COUNTERS, AS FOLLOWS:
!     TURNS   TALLIES HOW MANY COMMANDS HE'S GIVEN (IGNORES YES/NO)
!     LIMIT   LIFETIME OF LAMP (NOT SET HERE)
!     IWEST   HOW MANY TIMES HE'S SAID "WEST" INSTEAD OF "W"
!     KNFLOC  0 IF NO KNIFE HERE, LOC IF KNIFE HERE, -1 AFTER CAVEAT
!     DETAIL  HOW OFTEN WE'VE SAID "NOT ALLOWED TO GIVE MORE DETAIL"
!     ABBNUM  HOW OFTEN WE SHOULD PRINT NON-ABBREVIATED DESCRIPTIONS
!     MAXDIE  NUMBER OF REINCARNATION MESSAGES AVAILABLE (UP TO 5)
!     NUMDIE  NUMBER OF TIMES KILLED SO FAR
!     HOLDNG  NUMBER OF OBJECTS BEING CARRIED
!     DKILL   NUMBER OF DWARVES KILLED (UNUSED IN SCORING, NEEDED FOR MSG)
!     FOOBAR  CURRENT PROGRESS IN SAYING "FEE FIE FOE FOO".
!     BONUS   USED TO DETERMINE AMOUNT OF BONUS IF HE REACHES CLOSING
!     CLOCK1  NUMBER OF TURNS FROM FINDING LAST TREASURE TILL CLOSING
!     CLOCK2  NUMBER OF TURNS FROM FIRST WARNING TILL BLINDING FLASH
!     LOGICALS WERE EXPLAINED EARLIER

      TURNS=0
      LMWARN=.false.
      IWEST=0
      KNFLOC=0
      DETAIL=0
      ABBNUM=5
      do I=0,4
         if (RTEXT(2*I+81) /= 0) MAXDIE=I+1
      end do
      NUMDIE=0
      HOLDNG=0
      DKILL=0
      FOOBAR=0
      BONUS=0
      CLOCK1=30
      CLOCK2=50
      SAVED=0
      CLOSNG=.false.
      PANIC=.false.
      CLOSED=.false.
      GAVEUP=.false.
      SCORNG=.false.

!  IF SETUP=1, REPORT ON AMOUNT OF ARRAYS ACTUALLY USED, TO PERMIT REDUCTIONS.

      if (SETUP /= 1) GOTO 1
      SETUP=2

      do K=1,LOCSIZ
         KK=LOCSIZ+1-K
         if (LTEXT(KK) /= 0) exit
      end do

      OBJ=0
      do K=1,100
         if (PTEXT(K) /= 0) OBJ=OBJ+1
      end do

      do K=1,TABNDX
         if (KTAB(K)/1000 == 2) VERB=KTAB(K)-2000
      end do

      do K=1,RTXSIZ
         J=RTXSIZ+1-K
         if (RTEXT(J) /= 0) exit
      end do

      do K=1,MAGSIZ
         I=MAGSIZ+1-K
         if (MTEXT(I) /= 0) exit
      end do

      K=100
      PRINT 1999,LINUSE,LINSIZ,TRVS,TRVSIZ,TABNDX,TABSIZ,KK  &
              ,LOCSIZ,OBJ,K,VERB,VRBSIZ,J,RTXSIZ,CLSSES,CLSMAX  &
              ,HNTMAX,HNTSIZ,I,MAGSIZ
1999  FORMAT (' TABLE SPACE USED:'/  &
              ' ',I6,' OF ',I6,' WORDS OF MESSAGES'/  &
              ' ',I6,' OF ',I6,' TRAVEL OPTIONS'/  &
              ' ',I6,' OF ',I6,' VOCABULARY WORDS'/  &
              ' ',I6,' OF ',I6,' LOCATIONS'/  &
              ' ',I6,' OF ',I6,' OBJECTS'/  &
              ' ',I6,' OF ',I6,' ACTION VERBS'/  &
              ' ',I6,' OF ',I6,' RTEXT MESSAGES'/  &
              ' ',I6,' OF ',I6,' CLASS MESSAGES'/  &
              ' ',I6,' OF ',I6,' HINTS'/  &
              ' ',I6,' OF ',I6,' MAGIC MESSAGES'/  &
              )

!  FINALLY, SINCE WE'RE CLEARLY SETTING THINGS UP FOR THE FIRST TIME...

      CALL POOF

!  START-UP, DWARF STUFF

1     DEMO=START()
      CALL MOTD(.false.)
      I=RANI(1)
      HINTED(3)=YES(65,1,0)
      NEWLOC=1
      LOC=1
      SETUP=3
      LIMIT=330
      if (HINTED(3)) LIMIT=1000

!  CAN'T LEAVE CAVE ONCE IT'S CLOSING (EXCEPT BY MAIN OFFICE).

2     if (NEWLOC < 9 .and. NEWLOC /= 0 .and. CLOSNG) then
         CALL RSPEAK(130)
         NEWLOC=LOC
         if (.not.PANIC) CLOCK2=15
         PANIC=.true.
      end if

!  SEE IF A DWARF HAS SEEN HIM AND HAS COME FROM WHERE HE WANTS TO GO.  IF SO,
!  THE DWARF'S BLOCKING HIS WAY.  IF COMING FROM PLACE FORBIDDEN TO PIRATE
!  (DWARVES ROOTED IN PLACE) LET HIM GET OUT (AND ATTACKED).

      if (.not.(NEWLOC == LOC .or. FORCED(LOC) .or. BITSET(LOC,3))) then
         do I=1,5
            if (ODLOC(I) /= NEWLOC .or. .not.DSEEN(I)) cycle
            NEWLOC=LOC
            CALL RSPEAK(2)
            exit
         end do
      end if
      LOC=NEWLOC

!  DWARF STUFF.  SEE EARLIER COMMENTS FOR DESCRIPTION OF VARIABLES.  REMEMBER
!  SIXTH DWARF IS PIRATE AND IS THUS VERY DIFFERENT EXCEPT FOR MOTION RULES.

!  FIRST OFF, DON'T LET THE DWARVES FOLLOW HIM INTO A PIT OR A WALL.  ACTIVATE
!  THE WHOLE MESS THE FIRST TIME HE GETS AS FAR AS THE HALL OF MISTS (LOC 15).
!  IF NEWLOC IS FORBIDDEN TO PIRATE (IN PARTICULAR, IF IT'S BEYOND THE TROLL
!  BRIDGE), BYPASS DWARF STUFF.  THAT WAY PIRATE CAN'T STEAL RETURN TOLL, AND
!  DWARVES CAN'T MEET THE BEAR.  ALSO MEANS DWARVES WON'T FOLLOW HIM INTO DEAD
!  END IN MAZE, BUT C'EST LA VIE.  THEY'LL WAIT FOR HIM OUTSIDE THE DEAD END.

      if (LOC == 0 .or. FORCED(LOC) .or. BITSET(NEWLOC,3)) GOTO 2000
      if (DFLAG /= 0) GOTO 6000
      if (LOC >= 15) DFLAG=1
      GOTO 2000

!  WHEN WE ENCOUNTER THE FIRST DWARF, WE KILL 0, 1, OR 2 OF THE 5 DWARVES.  IF
!  ANY OF THE SURVIVORS IS AT LOC, REPLACE HIM WITH THE ALTERNATE.

6000  if (DFLAG == 1) then
         if (LOC < 15 .or. PCT(95)) GOTO 2000
         DFLAG=2
         do I=1,2
            J=1+RANI(5)
!  IF SAVED NOT = -1, HE BYPASSED THE "START" CALL.
            if (PCT(50) .and. SAVED == -1) DLOC(J)=0
         end do
         do I=1,5
            if (DLOC(I) == LOC) DLOC(I)=DALTLC
            ODLOC(I)=DLOC(I)
         end do
         CALL RSPEAK(3)
         CALL DROP(AXE,LOC)
         GOTO 2000
      end if

!  THINGS ARE IN FULL SWING.  MOVE EACH DWARF AT RANDOM, EXCEPT IF HE'S SEEN US
!  HE STICKS WITH US.  DWARVES NEVER GO TO LOCS <15.  IF WANDERING AT RANDOM,
!  THEY DON'T BACK UP UNLESS THERE'S NO ALTERNATIVE.  IF THEY DON'T HAVE TO
!  MOVE, THEY ATTACK.  AND, OF COURSE, DEAD DWARVES DON'T DO MUCH OF ANYTHING.

6010  DTOTAL=0
      ATTACK=0
      STICK=0
      do I=1,6
         if (DLOC(I) == 0) cycle
         J=1
         KK=DLOC(I)
         KK=KEY(KK)
         if (KK /= 0) then
            do
               NEWLOC=MOD(ABS(TRAVEL(KK))/1000,1000)
               if (.not.(NEWLOC > 300 .or. NEWLOC < 15 .or. NEWLOC == ODLOC(I)  &
                      .or. (J > 1 .and. NEWLOC == TK(J-1)) .or. J >= 20  &
                      .or. NEWLOC == DLOC(I) .or. FORCED(NEWLOC)  &
                      .or. (I == 6 .and. BITSET(NEWLOC,3))  &
                      .or. ABS(TRAVEL(KK))/1000000 == 100)) then
                  TK(J)=NEWLOC
                  J=J+1
               end if
               KK=KK+1
               if (TRAVEL(KK-1) < 0) exit
            end do
         end if
         TK(J)=ODLOC(I)
         if (J >= 2) J=J-1
         J=1+RANI(J)
         ODLOC(I)=DLOC(I)
         DLOC(I)=TK(J)
         DSEEN(I)=(DSEEN(I) .and. LOC >= 15)  &
                .or. (DLOC(I) == LOC .or. ODLOC(I) == LOC)
         if (.not.DSEEN(I)) cycle
         DLOC(I)=LOC
         if (I /= 6) GOTO 6027

!  THE PIRATE'S SPOTTED HIM.  HE LEAVES HIM ALONE ONCE WE'VE FOUND CHEST.
!  K COUNTS IF A TREASURE IS HERE.  IF NOT, AND TALLY=TALLY2 PLUS ONE FOR AN
!  UNSEEN CHEST, LET THE PIRATE BE SPOTTED.  USE PLACE(MESSAG) TO DETERMINE IF
!  PIRATE'S BEEN SEEN, SINCE PLACE(CHEST)=0 COULD MEAN HE THREW IT TO TROLL.

         if (LOC == CHLOC .or. PROP(CHEST) >= 0) cycle
         K=0
         do J=50,MAXTRS
!  PIRATE WON'T TAKE PYRAMID FROM PLOVER ROOM OR DARK ROOM (TOO EASY!).
            if (J /= PYRAM .or. (LOC /= PLAC(PYRAM)  &
                   .and. LOC /= PLAC(EMRALD))) then
               if (TOTING(J)) GOTO 6022
            end if
            if (HERE(J))K=1
         end do
         if (TALLY == TALLY2+1 .and. K == 0 .and. PLACE(MESSAG) == 0  &
                .and. HERE(LAMP) .and. PROP(LAMP) == 1) GOTO 6025
         if (ODLOC(6) /= DLOC(6) .and. PCT(20)) CALL RSPEAK(127)
         cycle

6022     CALL RSPEAK(128)
         if (PLACE(MESSAG) == 0) CALL MOVE(CHEST,CHLOC)
         CALL MOVE(MESSAG,CHLOC2)
         do J=50,MAXTRS
            if (J == PYRAM .and. (LOC == PLAC(PYRAM)  &
                   .or. LOC == PLAC(EMRALD))) cycle
            if (AT(J) .and. FIXED(J) == 0) CALL CARRY(J,LOC)
            if (TOTING(J)) CALL DROP(J,CHLOC)
         end do
6024     DLOC(6)=CHLOC
         ODLOC(6)=CHLOC
         DSEEN(6)=.false.
         cycle

6025     CALL RSPEAK(186)
         CALL MOVE(CHEST,CHLOC)
         CALL MOVE(MESSAG,CHLOC2)
         GOTO 6024

!  THIS THREATENING LITTLE DWARF IS IN THE ROOM WITH HIM!

6027     DTOTAL=DTOTAL+1
         if (ODLOC(I) == DLOC(I)) then
            ATTACK=ATTACK+1
            if (KNFLOC >= 0) KNFLOC=LOC
            if (RANI(1000) < 95*(DFLAG-2))STICK=STICK+1
         end if
      end do

!  NOW WE KNOW WHAT'S HAPPENING.  LET'S TELL THE POOR SUCKER ABOUT IT.

      if (DTOTAL == 0) GOTO 2000
      if (DTOTAL /= 1) then
         PRINT 67,DTOTAL
67       FORMAT(/' THERE ARE ',I1,' THREATENING LITTLE DWARVES IN THE'  &
               ,' ROOM WITH YOU.')
      else
         CALL RSPEAK(4)
      end if
      if (ATTACK == 0) GOTO 2000
      if (DFLAG == 2) DFLAG=3
!  IF SAVED NOT = -1, HE BYPASSED THE "START" CALL.  DWARVES GET *VERY* MAD!
      if (SAVED /= -1) DFLAG=20
      if (ATTACK == 1) then
         CALL RSPEAK(5)
         K=52
      else
         PRINT 78,ATTACK
78       FORMAT(/' ',I1,' OF THEM THROW KNIVES AT YOU!')
         K=6
      end if
      if (STICK <= 1) then
         CALL RSPEAK(K+STICK)
         if (STICK == 0) GOTO 2000
      else
         PRINT 68,STICK
68       FORMAT(/' ',I1,' OF THEM GET YOU!')
      end if
      OLDLC2=LOC
      GOTO 99

!  DESCRIBE THE CURRENT LOCATION AND (MAYBE) GET NEXT COMMAND.

!  PRINT TEXT FOR CURRENT LOC.

2000  if (LOC == 0) GOTO 99
      KK=STEXT(LOC)
      if (MOD(ABB(LOC),ABBNUM) == 0 .or. KK == 0) KK=LTEXT(LOC)
      if (.not.FORCED(LOC) .and. DARK()) then
         if (WZDARK .and. PCT(35)) GOTO 90
         KK=RTEXT(16)
      end if
      if (TOTING(BEAR)) CALL RSPEAK(141)
      CALL SPEAK(KK)
      K=1
      if (FORCED(LOC)) GOTO 8
      if (LOC == 33 .and. PCT(25) .and. .not.CLOSNG) CALL RSPEAK(8)

!  PRINT OUT DESCRIPTIONS OF OBJECTS AT THIS LOCATION.  IF NOT CLOSING AND
!  PROPERTY VALUE IS NEGATIVE, TALLY OFF ANOTHER TREASURE.  RUG IS SPECIAL
!  CASE; ONCE SEEN, ITS PROP IS 1 (DRAGON ON IT) TILL DRAGON IS KILLED.
!  SIMILARLY FOR CHAIN; PROP IS INITIALLY 1 (LOCKED TO BEAR).  THESE HACKS
!  ARE BECAUSE PROP=0 IS NEEDED TO GET FULL SCORE.

      if (DARK()) GOTO 2012
      ABB(LOC)=ABB(LOC)+1
      I=ATLOC(LOC)
2004  if (I == 0) GOTO 2012
      OBJ=I
      if (OBJ > 100) OBJ=OBJ-100
      if (OBJ == STEPS .and. TOTING(NUGGET)) GOTO 2008
      if (PROP(OBJ) >= 0) GOTO 2006
      if (CLOSED) GOTO 2008
      PROP(OBJ)=0
      if (OBJ == RUG .or. OBJ == CHAIN) PROP(OBJ)=1
      TALLY=TALLY-1
!  IF REMAINING TREASURES TOO ELUSIVE, ZAP HIS LAMP.
      if (TALLY == TALLY2 .and. TALLY /= 0) LIMIT=MIN(35,LIMIT)
2006  KK=PROP(OBJ)
      if (OBJ == STEPS .and. LOC == FIXED(STEPS)) KK=1
      CALL PSPEAK(OBJ,KK)
2008  I=LINK(I)
      GOTO 2004

2009  K=54
2010  SPK=K
2011  CALL RSPEAK(SPK)

2012  VERB=0
      OLDOBJ=OBJ
      OBJ=0

!  CHECK IF THIS LOC IS ELIGIBLE FOR ANY HINTS.  IF BEEN HERE LONG ENOUGH,
!  BRANCH TO HELP SECTION (ON LATER PAGE).  HINTS ALL COME BACK HERE EVENTUALLY
!  TO FINISH THE LOOP.  IGNORE "HINTS" < 4 (SPECIAL STUFF, SEE DATABASE NOTES).

2600  do HINT=4,HNTMAX
         if (HINTED(HINT)) cycle
         if (.not.BITSET(LOC,HINT)) HINTLC(HINT)=-1
         HINTLC(HINT)=HINTLC(HINT)+1
         if (HINTLC(HINT) >= HINTS(HINT,1)) GOTO 40000
      end do
2602  CONTINUE

!  KICK THE RANDOM NUMBER GENERATOR JUST TO ADD VARIETY TO THE CHASE.  ALSO,
!  IF CLOSING TIME, CHECK FOR ANY OBJECTS BEING TOTED WITH PROP < 0 AND SET
!  THE PROP TO -1-PROP.  THIS WAY OBJECTS WON'T BE DESCRIBED UNTIL THEY'VE
!  BEEN PICKED UP AND PUT DOWN SEPARATE FROM THEIR RESPECTIVE PILES.  DON'T
!  TICK CLOCK1 UNLESS WELL INTO CAVE (AND NOT AT Y2).

      if (CLOSED) then
         if (PROP(OYSTER) < 0 .and. TOTING(OYSTER))  &
               CALL PSPEAK(OYSTER,1)
         do I=1,100
            if (TOTING(I) .and. PROP(I) < 0) PROP(I)=-1-PROP(I)
         end do
      end if
      WZDARK=DARK()
      if (KNFLOC > 0 .and. KNFLOC /= LOC) KNFLOC=0
      I=RANI(1)
      CALL GETIN(WD1,WD1X,WD2,WD2X)

!  EVERY INPUT, CHECK "FOOBAR" FLAG.  IF ZERO, NOTHING'S GOING ON.  IF POS,
!  MAKE NEG.  IF NEG, HE SKIPPED A WORD, SO MAKE IT ZERO.

2608  FOOBAR=MIN(0,-FOOBAR)
      if (TURNS == 0 .and. WD1 == IA5('MAGIC') .and. WD2 == IA5('MODE ')) then
         ABB(1) = 0
         CALL MAINT
      end if
      TURNS=TURNS+1
      if (DEMO .and. TURNS >= SHORT) GOTO 13000
      if (TURNS == 3) CALL DATIME(XXD,XXT)
      if (TURNS == 45) then
!  SEE IF TIMER UUO HAS BEEN ZAPPED; IF SO, HE'S CHEATING.
         CALL DATIME(YYD,YYT)
         if (XXD == YYD .and. XXT == YYT) SAVED=0
      end if
      if (VERB == SAY .and. WD2 /= 0) VERB=0
      if (VERB == SAY) GOTO 4090
      if (TALLY == 0 .and. LOC >= 15 .and. LOC /= 33) CLOCK1=CLOCK1-1
      if (CLOCK1 == 0) GOTO 10000
      if (CLOCK1 < 0) CLOCK2=CLOCK2-1
      if (CLOCK2 == 0) GOTO 11000
      if (PROP(LAMP) == 1) LIMIT=LIMIT-1
      if (LIMIT <= 30 .and. HERE(BATTER) .and. PROP(BATTER) == 0  &
            .and. HERE(LAMP)) GOTO 12000
      if (LIMIT == 0) GOTO 12400
      if (LIMIT < 0 .and. LOC <= 8) GOTO 12600
      if (LIMIT <= 30) GOTO 12200
19999 K=43
      if (LIQLOC(LOC) == WATER) K=70
      if (WD1 == IA5('ENTER') .and. (WD2 == IA5('STREA')  &
            .or. WD2 == IA5('WATER'))) GOTO 2010
      if (WD1 == IA5('ENTER') .and. WD2 /= 0) GOTO 2800
      if ((WD1 == IA5('WATER') .or. WD1 == IA5('OIL  '))  &
            .and. (WD2 == IA5('PLANT') .or. WD2 == IA5('DOOR '))) then
         if (AT(VOCAB(WD2,1)))WD2=IA5('POUR ')
      end if
2610  if (WD1 == IA5('WEST ')) then
         IWEST=IWEST+1
         if (IWEST == 10) CALL RSPEAK(17)
      end if
2630  I=VOCAB(WD1,-1)
      if (I == -1) GOTO 3000
      K=MOD(I,1000)
      KQ=I/1000+1
      select case (KQ)
         case (1)
            GOTO 8
         case (2)
            GOTO 5000
         case (3)
            GOTO 4000
         case (4)
            GOTO 2010
         case default
            CALL BUG(22)
      end select

!  GET SECOND WORD FOR ANALYSIS.

2800  WD1=WD2
      WD1X=WD2X
      WD2=0
      GOTO 2610

!  GEE, I DON'T UNDERSTAND.

3000  CALL A5TOA1(WD1,WD1X,IA5('".   '),CAT,K)
      PRINT 3001,(CAT(I),I=1,K)
3001  FORMAT(/' SORRY, I DON''T KNOW THE WORD "',20A1)
      GOTO 2600

!  ANALYSE A VERB.  REMEMBER WHAT IT WAS, GO BACK FOR OBJECT IF SECOND WORD
!  UNLESS VERB IS "SAY", WHICH SNARFS ARBITRARY SECOND WORD.

4000  VERB=K
      SPK=ACTSPK(VERB)
      if (WD2 /= 0 .and. VERB /= SAY) GOTO 2800
      if (VERB == SAY) OBJ=WD2
      if (OBJ /= 0) GOTO 4090

!  ANALYSE AN INTRANSITIVE VERB (IE, NO OBJECT GIVEN YET).

      select case (VERB)
         case (1)  ! TAKE
            GOTO 8010
         case (4, 6)  ! OPEN, LOCK
            GOTO 8040
         case (5)  ! NOTHING
            GOTO 2009
         case (7)  ! ON
            GOTO 9070
         case (8)  ! OFF
            GOTO 9080
         case (11)  ! WALK
            GOTO 2011
         case (12)  ! KILL
            GOTO 9120
         case (13)  ! POUR
            GOTO 9130
         case (14)  ! EAT
            GOTO 8140
         case (15)  ! DRINK
            GOTO 9150
         case (18)  ! QUIT
            GOTO 8180
         case (20)  ! INVEN
            GOTO 8200
         case (22)  ! FILL
            GOTO 9220
         case (23)  ! BLAST
            GOTO 9230
         case (24)  ! SCORE
            GOTO 8240
         case (25)  ! FOO
            GOTO 8250
         case (26)  ! BRIEF
            GOTO 8260
         case (27)  ! READ
            GOTO 8270
         case (30)  ! SUSPEND
            GOTO 8300
         case (31)  ! HOURS
            GOTO 8310
         case (2, 3, 9, 10, 16, 17, 19, 21, 28, 29)
            GOTO 8000
         case default
            CALL BUG(23)
      end select

!  ANALYSE A TRANSITIVE VERB.

4090  select case (VERB)
         case (1)  ! TAKE
            GOTO 9010
         case (2)  ! DROP
            GOTO 9020
         case (3)  ! SAY
            GOTO 9030
         case (4, 6)  ! OPEN, LOCK
            GOTO 9040
         case (5)  ! NOTHING
            GOTO 2009
         case (7)  ! ON
            GOTO 9070
         case (8)  ! OFF
            GOTO 9080
         case (9)  ! WAVE
            GOTO 9090
         case (12)  ! KILL
            GOTO 9120
         case (13)  ! POUR
            GOTO 9130
         case (14)  ! EAT
            GOTO 9140
         case (15)  ! DRINK
            GOTO 9150
         case (16)  ! RUB
            GOTO 9160
         case (17)  ! TOSS
            GOTO 9170
         case (19,20)  ! FIND, INVEN
            GOTO 9190
         case (21)  ! FEED
            GOTO 9210
         case (22)  ! FILL
            GOTO 9220
         case (23)  ! BLAST
            GOTO 9230
         case (27)  ! READ
            GOTO 9270
         case (28)  ! BREAK
            GOTO 9280
         case (29)  ! WAKE
            GOTO 9290
         case (10, 11, 18, 24, 25, 26, 30, 31)
            GOTO 2011
         case default
            CALL BUG(24)
      end select

!  ANALYSE AN OBJECT WORD.  SEE IF THE THING IS HERE, WHETHER WE'VE GOT A VERB
!  YET, AND SO ON.  OBJECT MUST BE HERE UNLESS VERB IS "FIND" OR "INVENT(ORY)"
!  (AND NO NEW VERB YET TO BE ANALYSED).  WATER AND OIL ARE ALSO FUNNY, SINCE
!  THEY ARE NEVER ACTUALLY DROPPED AT ANY LOCATION, BUT MIGHT BE HERE INSIDE
!  THE BOTTLE OR AS A FEATURE OF THE LOCATION.

5000  OBJ=K
      if (FIXED(K) /= LOC .and. .not.HERE(K)) GOTO 5100
5010  if (WD2 /= 0) GOTO 2800
      if (VERB /= 0) GOTO 4090
      CALL A5TOA1(WD1,WD1X,IA5('?    '),CAT,K)
      PRINT 5015,(CAT(I),I=1,K)
5015  FORMAT(/' WHAT DO YOU WANT TO DO WITH THE ',20A1)
      GOTO 2600

5100  if (K == GRATE) then
         if (LOC == 1 .or. LOC == 4 .or. LOC == 7) K=DPRSSN
         if (LOC > 9 .and. LOC < 15) K=ENTRNC
         if (K /= GRATE) GOTO 8
      else if (K == DWARF) then
         do I=1,5
            if (DLOC(I) == LOC .and. DFLAG >= 2) GOTO 5010
         end do
      end if
      if ((LIQ() == K .and. HERE(BOTTLE)) .or. K == LIQLOC(LOC)) GOTO 5010
      if (OBJ == PLANT .and. AT(PLANT2) .and. PROP(PLANT2) /= 0) then
         OBJ=PLANT2
         GOTO 5010
      else if (OBJ == KNIFE .and. KNFLOC == LOC) then
         KNFLOC=-1
         SPK=116
         GOTO 2011
      else if (OBJ == ROD .and. HERE(ROD2)) then
         OBJ=ROD2
         GOTO 5010
      end if
5190  if ((VERB == FIND .or. VERB == INVENT) .and. WD2 == 0) GOTO 5010
      CALL A5TOA1(WD1,WD1X,IA5('HERE.'),CAT,K)
      PRINT 5199,(CAT(I),I=1,K)
5199  FORMAT(/' I SEE NO ',20A1)
      GOTO 2012
!  FIGURE OUT THE NEW LOCATION
!
!  GIVEN THE CURRENT LOCATION IN "LOC", AND A MOTION VERB NUMBER IN "K", PUT
!  THE NEW LOCATION IN "NEWLOC".  THE CURRENT LOC IS SAVED IN "OLDLOC" IN CASE
!  HE WANTS TO RETREAT.  THE CURRENT OLDLOC IS SAVED IN OLDLC2, IN CASE HE
!  DIES.  (IF HE DOES, NEWLOC WILL BE LIMBO, AND OLDLOC WILL BE WHAT KILLED
!  HIM, SO WE NEED OLDLC2, WHICH IS THE LAST PLACE HE WAS SAFE.)

8     KK=KEY(LOC)
      NEWLOC=LOC
      if (KK == 0) CALL BUG(26)
      if (K == NOOP) GOTO 2
      if (K == BACK) GOTO 20
      if (K == LOOK) GOTO 30
      if (K == CAVE) GOTO 40
      OLDLC2=OLDLOC
      OLDLOC=LOC

9     LL=ABS(TRAVEL(KK))
      if (MOD(LL,1000) == 1 .or. MOD(LL,1000) == K) GOTO 10
      if (TRAVEL(KK) < 0) GOTO 50
      KK=KK+1
      GOTO 9

10    LL=LL/1000
11    NEWLOC=LL/1000
      K=MOD(NEWLOC,100)
      if (NEWLOC <= 300) GOTO 13
      if (PROP(K) /= NEWLOC/100-3) GOTO 16
12    if (TRAVEL(KK) < 0) CALL BUG(25)
      KK=KK+1
      NEWLOC=ABS(TRAVEL(KK))/1000
      if (NEWLOC == LL) GOTO 12
      LL=NEWLOC
      GOTO 11

13    if (NEWLOC <= 100) GOTO 14
      if (TOTING(K) .or. (NEWLOC > 200 .and. AT(K))) GOTO 16
      GOTO 12

14    if (NEWLOC /= 0 .and. .not.PCT(NEWLOC)) GOTO 12
16    NEWLOC=MOD(LL,1000)
      if (NEWLOC <= 300) GOTO 2
      if (NEWLOC <= 500) GOTO 30000
      CALL RSPEAK(NEWLOC-500)
      NEWLOC=LOC
      GOTO 2

!  SPECIAL MOTIONS COME HERE.  LABELLING CONVENTION: STATEMENT NUMBERS NNNXX
!  (XX=00-99) ARE USED FOR SPECIAL CASE NUMBER NNN (NNN=301-500).

30000 NEWLOC=NEWLOC-300
      select case (NEWLOC)
         case (1)
            GOTO 30100
         case (2)
            GOTO 30200
         case (3)
            GOTO 30300
         case default
            CALL BUG(20)
      end select

!  TRAVEL 301.  PLOVER-ALCOVE PASSAGE.  CAN CARRY ONLY EMERALD.  NOTE: TRAVEL
!  TABLE MUST INCLUDE "USELESS" ENTRIES GOING THROUGH PASSAGE, WHICH CAN NEVER
!  BE USED FOR ACTUAL MOTION, BUT CAN BE SPOTTED BY "GO BACK".

30100 NEWLOC=99+100-LOC
      if (.not.(HOLDNG == 0 .or. (HOLDNG == 1 .and. TOTING(EMRALD)))) then
         NEWLOC=LOC
         CALL RSPEAK(117)
      end if
      GOTO 2

!  TRAVEL 302.  PLOVER TRANSPORT.  DROP THE EMERALD (ONLY USE SPECIAL TRAVEL IF
!  TOTING IT), SO HE'S FORCED TO USE THE PLOVER-PASSAGE TO GET IT OUT.  HAVING
!  DROPPED IT, GO BACK AND PRETEND HE WASN'T CARRYING IT AFTER ALL.

30200 CALL DROP(EMRALD,LOC)
      GOTO 12

!  TRAVEL 303.  TROLL BRIDGE.  MUST BE DONE ONLY AS SPECIAL MOTION SO THAT
!  DWARVES WON'T WANDER ACROSS AND ENCOUNTER THE BEAR.  (THEY WON'T FOLLOW THE
!  PLAYER THERE BECAUSE THAT REGION IS FORBIDDEN TO THE PIRATE.)  IF
!  PROP(TROLL)=1, HE'S CROSSED SINCE PAYING, SO STEP OUT AND BLOCK HIM.
!  (STANDARD TRAVEL ENTRIES CHECK FOR PROP(TROLL)=0.)  SPECIAL STUFF FOR BEAR.

30300 if (PROP(TROLL) == 1) then
         CALL PSPEAK(TROLL,1)
         PROP(TROLL)=0
         CALL MOVE(TROLL2,0)
         CALL MOVE(TROLL2+100,0)
         CALL MOVE(TROLL,PLAC(TROLL))
         CALL MOVE(TROLL+100,FIXD(TROLL))
         CALL JUGGLE(CHASM)
         NEWLOC=LOC
         GOTO 2
      else
         NEWLOC=PLAC(TROLL)+FIXD(TROLL)-LOC
         if (PROP(TROLL) == 0) PROP(TROLL)=1
         if (.not.TOTING(BEAR)) GOTO 2
         CALL RSPEAK(162)
         PROP(CHASM)=1
         PROP(TROLL)=2
         CALL DROP(BEAR,NEWLOC)
         FIXED(BEAR)=-1
         PROP(BEAR)=3
         if (PROP(SPICES) < 0) TALLY2=TALLY2+1
         OLDLC2=NEWLOC
         GOTO 99
      end if

!  END OF SPECIALS.

!  HANDLE "GO BACK".  LOOK FOR VERB WHICH GOES FROM LOC TO OLDLOC, OR TO OLDLC2
!  IF OLDLOC HAS FORCED-MOTION.  K2 SAVES ENTRY -> FORCED LOC -> PREVIOUS LOC.

20    K=OLDLOC
      if (FORCED(K)) K=OLDLC2
      OLDLC2=OLDLOC
      OLDLOC=LOC
      K2=0
      if (K == LOC) then
         CALL RSPEAK(91)
         GOTO 2
      end if

21    LL=MOD((ABS(TRAVEL(KK))/1000),1000)
      if (LL /= K) then
         if (LL <= 300) then
            J=KEY(LL)
            if (FORCED(LL) .and. MOD((ABS(TRAVEL(J))/1000),1000) == K) K2=KK
         end if
         if (TRAVEL(KK) >= 0) then
            KK=KK+1
            GOTO 21
         end if

         KK=K2
         if (KK == 0) then
            CALL RSPEAK(140)
            GOTO 2
         end if
      end if

      K=MOD(ABS(TRAVEL(KK)),1000)
      KK=KEY(LOC)
      GOTO 9

!  LOOK.  CAN'T GIVE MORE DETAIL.  PRETEND IT WASN'T DARK (THOUGH IT MAY "NOW"
!  BE DARK) SO HE WON'T FALL INTO A PIT WHILE STARING INTO THE GLOOM.

30    if (DETAIL < 3) CALL RSPEAK(15)
      DETAIL=DETAIL+1
      WZDARK=.false.
      ABB(LOC)=0
      GOTO 2

!  CAVE.  DIFFERENT MESSAGES DEPENDING ON WHETHER ABOVE GROUND.

40    if (LOC < 8) CALL RSPEAK(57)
      if (LOC >= 8) CALL RSPEAK(58)
      GOTO 2

!  NON-APPLICABLE MOTION.  VARIOUS MESSAGES DEPENDING ON WORD GIVEN.

50    SPK=12
      if (K >= 43 .and. K <= 50) SPK=9
      if (K == 29 .or. K == 30) SPK=9
      if (K == 7 .or. K == 36 .or. K == 37) SPK=10
      if (K == 11 .or. K == 19) SPK=11
      if (VERB == FIND .or. VERB == INVENT) SPK=59
      if (K == 62 .or. K == 65) SPK=42
      if (K == 17) SPK=80
      CALL RSPEAK(SPK)
      GOTO 2
!  "YOU'RE DEAD, JIM."
!
!  IF THE CURRENT LOC IS ZERO, IT MEANS THE CLOWN GOT HIMSELF KILLED.  WE'LL
!  ALLOW THIS MAXDIE TIMES.  MAXDIE IS AUTOMATICALLY SET BASED ON THE NUMBER OF
!  SNIDE MESSAGES AVAILABLE.  EACH DEATH RESULTS IN A MESSAGE (81, 83, ETC.)
!  WHICH OFFERS REINCARNATION; IF ACCEPTED, THIS RESULTS IN MESSAGE 82, 84,
!  ETC.  THE LAST TIME, IF HE WANTS ANOTHER CHANCE, HE GETS A SNIDE REMARK AS
!  WE EXIT.  WHEN REINCARNATED, ALL OBJECTS BEING CARRIED GET DROPPED AT OLDLC2
!  (PRESUMABLY THE LAST PLACE PRIOR TO BEING KILLED) WITHOUT CHANGE OF PROPS.
!  THE LOOP RUNS BACKWARDS TO ASSURE THAT THE BIRD IS DROPPED BEFORE THE CAGE.
!  (THIS KLUGE COULD BE CHANGED ONCE WE'RE SURE ALL REFERENCES TO BIRD AND CAGE
!  ARE DONE BY KEYWORDS.)  THE LAMP IS A SPECIAL CASE (IT WOULDN'T DO TO LEAVE
!  IT IN THE CAVE).  IT IS TURNED OFF AND LEFT OUTSIDE THE BUILDING (ONLY IF HE
!  WAS CARRYING IT, OF COURSE).  HE HIMSELF IS LEFT INSIDE THE BUILDING (AND
!  HEAVEN HELP HIM IF HE TRIES TO XYZZY BACK INTO THE CAVE WITHOUT THE LAMP!).
!  OLDLOC IS ZAPPED SO HE CAN'T JUST "RETREAT".

!  THE EASIEST WAY TO GET KILLED IS TO FALL INTO A PIT IN PITCH DARKNESS.

90    CALL RSPEAK(23)
      OLDLC2=LOC

!  OKAY, HE'S DEAD.  LET'S GET ON WITH IT.

99    if (.not.CLOSNG) then
         YEA=YES(81+NUMDIE*2,82+NUMDIE*2,54)
         NUMDIE=NUMDIE+1
         if (NUMDIE == MAXDIE .or. .not.YEA) GOTO 20000
         PLACE(WATER)=0
         PLACE(OIL)=0
         if (TOTING(LAMP)) PROP(LAMP)=0
         do J=1,100
            I=101-J
            if (.not.TOTING(I)) cycle
            K=OLDLC2
            if (I == LAMP) K=1
            CALL DROP(I,K)
         end do
         LOC=3
         OLDLOC=LOC
         GOTO 2000
      else

!  HE DIED DURING CLOSING TIME.  NO RESURRECTION.  TALLY UP A DEATH AND EXIT.

         CALL RSPEAK(131)
         NUMDIE=NUMDIE+1
         GOTO 20000
      end if

!  ROUTINES FOR PERFORMING THE VARIOUS ACTION VERBS

!  STATEMENT NUMBERS IN THIS SECTION ARE 8000 FOR INTRANSITIVE VERBS, 9000 FOR
!  TRANSITIVE, PLUS TEN TIMES THE VERB NUMBER.  MANY INTRANSITIVE VERBS USE THE
!  TRANSITIVE CODE, AND SOME VERBS USE CODE FOR OTHER VERBS, AS NOTED BELOW.

!  RANDOM INTRANSITIVE VERBS COME HERE.  CLEAR OBJ JUST IN CASE (SEE "ATTACK").

8000  CALL A5TOA1(WD1,WD1X,IA5('WHAT?'),CAT,K)
      PRINT 8002,(CAT(I),I=1,K)
8002  FORMAT(/' ',20A1)
      OBJ=0
      GOTO 2600

!  CARRY, NO OBJECT GIVEN YET.  OK IF ONLY ONE OBJECT PRESENT.

8010  if (ATLOC(LOC) == 0 .or. LINK(ATLOC(LOC)) /= 0) GOTO 8000
      do I=1,5
         if (DLOC(I) == LOC .and. DFLAG >= 2) GOTO 8000
      end do
      OBJ=ATLOC(LOC)

!  CARRY AN OBJECT.  SPECIAL CASES FOR BIRD AND CAGE (IF BIRD IN CAGE, CAN'T
!  TAKE ONE WITHOUT THE OTHER).  LIQUIDS ALSO SPECIAL, SINCE THEY DEPEND ON
!  STATUS OF BOTTLE.  ALSO VARIOUS SIDE EFFECTS, ETC.

9010  if (TOTING(OBJ)) GOTO 2011
      SPK=25
      if (OBJ == PLANT .and. PROP(PLANT) <= 0) SPK=115
      if (OBJ == BEAR .and. PROP(BEAR) == 1) SPK=169
      if (OBJ == CHAIN .and. PROP(BEAR) /= 0) SPK=170
      if (FIXED(OBJ) /= 0) GOTO 2011
      if (OBJ /= WATER .and. OBJ /= OIL) GOTO 9017
      if (HERE(BOTTLE) .and. LIQ() == OBJ) GOTO 9018
      OBJ=BOTTLE
      if (TOTING(BOTTLE) .and. PROP(BOTTLE) == 1) GOTO 9220
      if (PROP(BOTTLE) /= 1) SPK=105
      if (.not.TOTING(BOTTLE)) SPK=104
      GOTO 2011
9018  OBJ=BOTTLE
9017  if (HOLDNG >= 7) then
         CALL RSPEAK(92)
         GOTO 2012
      end if
      if (OBJ == BIRD  .and.  PROP(BIRD) == 0) then
         if (TOTING(ROD)) then
            CALL RSPEAK(26)
            GOTO 2012
         end if
         if (.not.TOTING(CAGE)) then
            CALL RSPEAK(27)
            GOTO 2012
         end if
         PROP(BIRD)=1
      end if
      if ((OBJ == BIRD .or. OBJ == CAGE) .and. PROP(BIRD) /= 0)  &
            CALL CARRY(BIRD+CAGE-OBJ,LOC)
      CALL CARRY(OBJ,LOC)
      K=LIQ()
      if (OBJ == BOTTLE .and. K /= 0) PLACE(K)=-1
      GOTO 2009

!  DISCARD OBJECT.  "THROW" ALSO COMES HERE FOR MOST OBJECTS.  SPECIAL CASES FOR
!  BIRD (MIGHT ATTACK SNAKE OR DRAGON) AND CAGE (MIGHT CONTAIN BIRD) AND VASE.
!  DROP COINS AT VENDING MACHINE FOR EXTRA BATTERIES.

9020  if (TOTING(ROD2) .and. OBJ == ROD .and. .not.TOTING(ROD)) OBJ=ROD2
      if (.not.TOTING(OBJ)) GOTO 2011
      if (OBJ /= BIRD .or. .not.HERE(SNAKE)) GOTO 9024
      CALL RSPEAK(30)
      if (CLOSED) GOTO 19000
      CALL DSTROY(SNAKE)
!  SET PROP FOR USE BY TRAVEL OPTIONS
      PROP(SNAKE)=1
9021  K=LIQ()
      if (K == OBJ) OBJ=BOTTLE
      if (OBJ == BOTTLE .and. K /= 0) PLACE(K)=0
      if (OBJ == CAGE .and. PROP(BIRD) /= 0) CALL DROP(BIRD,LOC)
      if (OBJ == BIRD) PROP(BIRD)=0
      CALL DROP(OBJ,LOC)
      GOTO 2012

9024  if (OBJ == COINS .and. HERE(VEND)) then
         CALL DSTROY(COINS)
         CALL DROP(BATTER,LOC)
         CALL PSPEAK(BATTER,0)
         GOTO 2012
      else if (OBJ == BIRD .and. AT(DRAGON) .and. PROP(DRAGON) == 0) then
         CALL RSPEAK(154)
         CALL DSTROY(BIRD)
         PROP(BIRD)=0
         if (PLACE(SNAKE) == PLAC(SNAKE)) TALLY2=TALLY2+1
         GOTO 2012
      else if (OBJ == BEAR .and. AT(TROLL)) then
         CALL RSPEAK(163)
         CALL MOVE(TROLL,0)
         CALL MOVE(TROLL+100,0)
         CALL MOVE(TROLL2,PLAC(TROLL))
         CALL MOVE(TROLL2+100,FIXD(TROLL))
         CALL JUGGLE(CHASM)
         PROP(TROLL)=2
         GOTO 9021
      else if (OBJ == VASE .and. LOC /= PLAC(PILLOW)) then
         PROP(VASE)=2
         if (AT(PILLOW)) PROP(VASE)=0
         CALL PSPEAK(VASE,PROP(VASE)+1)
         if (PROP(VASE) /= 0) FIXED(VASE)=-1
         GOTO 9021
      else
         CALL RSPEAK(54)
         GOTO 9021
      end if

!  SAY.  ECHO WD2 (OR WD1 IF NO WD2 (SAY WHAT?, ETC.).)  MAGIC WORDS OVERRIDE.

9030  CALL A5TOA1(WD2,WD2X,IA5('".   '),CAT,K)
      if (WD2 == 0) CALL A5TOA1(WD1,WD1X,IA5('".   '),CAT,K)
      if (WD2 /= 0) WD1=WD2
      I=VOCAB(WD1,-1)
      if (.not.(I == 62 .or. I == 65 .or. I == 71 .or. I == 2025)) then
         PRINT 9032,(CAT(I),I=1,K)
9032     FORMAT(/' OKAY, "',20A1)
         GOTO 2012
      else
         WD2=0
         OBJ=0
         GOTO 2630
      end if

!  LOCK, UNLOCK, NO OBJECT GIVEN.  ASSUME VARIOUS THINGS IF PRESENT.

8040  SPK=28
      if (HERE(CLAM)) OBJ=CLAM
      if (HERE(OYSTER)) OBJ=OYSTER
      if (AT(DOOR)) OBJ=DOOR
      if (AT(GRATE)) OBJ=GRATE
      if (OBJ /= 0 .and. HERE(CHAIN)) GOTO 8000
      if (HERE(CHAIN)) OBJ=CHAIN
      if (OBJ == 0) GOTO 2011

!  LOCK, UNLOCK OBJECT.  SPECIAL STUFF FOR OPENING CLAM/OYSTER AND FOR CHAIN.

9040  if (OBJ == CLAM .or. OBJ == OYSTER) GOTO 9046
      if (OBJ == DOOR) SPK=111
      if (OBJ == DOOR .and. PROP(DOOR) == 1) SPK=54
      if (OBJ == CAGE) SPK=32
      if (OBJ == KEYS) SPK=55
      if (OBJ == GRATE .or. OBJ == CHAIN) SPK=31
      if (SPK /= 31 .or. .not.HERE(KEYS)) GOTO 2011
      if (OBJ == CHAIN) GOTO 9048
      if (CLOSNG) then
         K=130
         if (.not.PANIC) CLOCK2=15
         PANIC=.true.
         GOTO 2010
      end if

9043  K=34+PROP(GRATE)
      PROP(GRATE)=1
      if (VERB == LOCK) PROP(GRATE)=0
      K=K+2*PROP(GRATE)
      GOTO 2010

!  CLAM/OYSTER.
9046  K=0
      if (OBJ == OYSTER) K=1
      SPK=124+K
      if (TOTING(OBJ)) SPK=120+K
      if (.not.TOTING(TRIDNT)) SPK=122+K
      if (VERB == LOCK) SPK=61
      if (SPK /= 124) GOTO 2011
      CALL DSTROY(CLAM)
      CALL DROP(OYSTER,LOC)
      CALL DROP(PEARL,105)
      GOTO 2011

!  CHAIN.
9048  if (VERB /= LOCK) then
         SPK=171
         if (PROP(BEAR) == 0) SPK=41
         if (PROP(CHAIN) == 0) SPK=37
         if (SPK /= 171) GOTO 2011
         PROP(CHAIN)=0
         FIXED(CHAIN)=0
         if (PROP(BEAR) /= 3) PROP(BEAR)=2
         FIXED(BEAR)=2-PROP(BEAR)
         GOTO 2011
      else
         SPK=172
         if (PROP(CHAIN) /= 0) SPK=34
         if (LOC /= PLAC(CHAIN)) SPK=173
         if (SPK /= 172) GOTO 2011
         PROP(CHAIN)=2
         if (TOTING(CHAIN)) CALL DROP(CHAIN,LOC)
         FIXED(CHAIN)=-1
         GOTO 2011
      end if

!  LIGHT LAMP

9070  if (.not.HERE(LAMP)) GOTO 2011
      SPK=184
      if (LIMIT < 0) GOTO 2011
      PROP(LAMP)=1
      CALL RSPEAK(39)
      if (WZDARK) GOTO 2000
      GOTO 2012

!  LAMP OFF

9080  if (.not.HERE(LAMP)) GOTO 2011
      PROP(LAMP)=0
      CALL RSPEAK(40)
      if (DARK()) CALL RSPEAK(16)
      GOTO 2012

!  WAVE.  NO EFFECT UNLESS WAVING ROD AT FISSURE.

9090  if ((.not.TOTING(OBJ)) .and. (OBJ /= ROD .or. .not.TOTING(ROD2)))  &
            SPK=29
      if (OBJ /= ROD .or. .not.AT(FISSUR) .or. .not.TOTING(OBJ)  &
            .or. CLOSNG) GOTO 2011
      PROP(FISSUR)=1-PROP(FISSUR)
      CALL PSPEAK(FISSUR,2-PROP(FISSUR))
      GOTO 2012

!  ATTACK.  ASSUME TARGET IF UNAMBIGUOUS.  "THROW" ALSO LINKS HERE.  ATTACKABLE
!  OBJECTS FALL INTO TWO CATEGORIES: ENEMIES (SNAKE, DWARF, ETC.)  AND OTHERS
!  (BIRD, CLAM).  AMBIGUOUS IF TWO ENEMIES, OR IF NO ENEMIES BUT TWO OTHERS.

9120  do I=1,5
         if (DLOC(I) == LOC .and. DFLAG >= 2) GOTO 9122
      end do
      I=0
9122  if (OBJ == 0) then
         if (I /= 0) OBJ=DWARF
         if (HERE(SNAKE)) OBJ=OBJ*100+SNAKE
         if (AT(DRAGON) .and. PROP(DRAGON) == 0) OBJ=OBJ*100+DRAGON
         if (AT(TROLL)) OBJ=OBJ*100+TROLL
         if (HERE(BEAR) .and. PROP(BEAR) == 0) OBJ=OBJ*100+BEAR
         if (OBJ > 100) GOTO 8000
      end if
      if (OBJ == 0) then
!  CAN'T ATTACK BIRD BY THROWING AXE.
         if (HERE(BIRD) .and. VERB /= THROW) OBJ=BIRD
!  CLAM AND OYSTER BOTH TREATED AS CLAM FOR INTRANSITIVE CASE; NO HARM DONE.
         if (HERE(CLAM) .or. HERE(OYSTER)) OBJ=100*OBJ+CLAM
         if (OBJ > 100) GOTO 8000
      end if
      if (OBJ == BIRD) then
         SPK=137
         if (CLOSED) GOTO 2011
         CALL DSTROY(BIRD)
         PROP(BIRD)=0
         if (PLACE(SNAKE) == PLAC(SNAKE)) TALLY2=TALLY2+1
         SPK=45
      end if
      if (OBJ == 0) SPK=44
      if (OBJ == CLAM .or. OBJ == OYSTER) SPK=150
      if (OBJ == SNAKE) SPK=46
      if (OBJ == DWARF) SPK=49
      if (OBJ == DWARF .and. CLOSED) GOTO 19000
      if (OBJ == DRAGON) SPK=167
      if (OBJ == TROLL) SPK=157
      if (OBJ == BEAR) SPK=165+(PROP(BEAR)+1)/2
      if (OBJ /= DRAGON .or. PROP(DRAGON) /= 0) GOTO 2011
!  FUN STUFF FOR DRAGON.  IF HE INSISTS ON ATTACKING IT, WIN!  SET PROP TO DEAD,
!  MOVE DRAGON TO CENTRAL LOC (STILL FIXED), MOVE RUG THERE (NOT FIXED), AND
!  MOVE HIM THERE, TOO.  THEN DO A NULL MOTION TO GET NEW DESCRIPTION.
      CALL RSPEAK(49)
      VERB=0
      OBJ=0
      CALL GETIN(WD1,WD1X,WD2,WD2X)
      if (WD1 /= IA5('Y    ') .and. WD1 /= IA5('YES  ')) GOTO 2608
      CALL PSPEAK(DRAGON,1)
      PROP(DRAGON)=2
      PROP(RUG)=0
      K=(PLAC(DRAGON)+FIXD(DRAGON))/2
      CALL MOVE(DRAGON+100,-1)
      CALL MOVE(RUG+100,0)
      CALL MOVE(DRAGON,K)
      CALL MOVE(RUG,K)
      do OBJ=1,100
         if (PLACE(OBJ) == PLAC(DRAGON) .or. PLACE(OBJ) == FIXD(DRAGON))  &
               CALL MOVE(OBJ,K)
      end do
      LOC=K
      K=NOOP
      GOTO 8

!  POUR.  IF NO OBJECT, OR OBJECT IS BOTTLE, ASSUME CONTENTS OF BOTTLE.
!  SPECIAL TESTS FOR POURING WATER OR OIL ON PLANT OR RUSTY DOOR.

9130  if (OBJ == BOTTLE .or. OBJ == 0) OBJ=LIQ()
      if (OBJ == 0) GOTO 8000
      if (.not.TOTING(OBJ)) GOTO 2011
      SPK=78
      if (OBJ /= OIL .and. OBJ /= WATER) GOTO 2011
      PROP(BOTTLE)=1
      PLACE(OBJ)=0
      SPK=77

      if (AT(PLANT)) then
         SPK=112
         if (OBJ /= WATER) GOTO 2011
         CALL PSPEAK(PLANT,PROP(PLANT)+1)
         PROP(PLANT)=MOD(PROP(PLANT)+2,6)
         PROP(PLANT2)=PROP(PLANT)/2
         K=NOOP
         GOTO 8
      else if (AT(DOOR)) then
         PROP(DOOR)=0
         if (OBJ == OIL) PROP(DOOR)=1
         SPK=113+PROP(DOOR)
         GOTO 2011
      else
         GOTO 2011
      end if

!  EAT.  INTRANSITIVE: ASSUME FOOD IF PRESENT, ELSE ASK WHAT.  TRANSITIVE: FOOD
!  OK, SOME THINGS LOSE APPETITE, REST ARE RIDICULOUS.

8140  if (.not.HERE(FOOD)) GOTO 8000
8142  CALL DSTROY(FOOD)
      SPK=72
      GOTO 2011

9140  if (OBJ == FOOD) GOTO 8142
      if (OBJ == BIRD .or. OBJ == SNAKE .or. OBJ == CLAM .or. OBJ == OYSTER  &
            .or. OBJ == DWARF .or. OBJ == DRAGON .or. OBJ == TROLL  &
            .or. OBJ == BEAR) SPK=71
      GOTO 2011

!  DRINK.  IF NO OBJECT, ASSUME WATER AND LOOK FOR IT HERE.  IF WATER IS IN
!  THE BOTTLE, DRINK THAT, ELSE MUST BE AT A WATER LOC, SO DRINK STREAM.

9150  if (OBJ == 0 .and. LIQLOC(LOC) /= WATER .and. (LIQ() /= WATER  &
            .or. .not.HERE(BOTTLE))) GOTO 8000
      if (OBJ /= 0 .and. OBJ /= WATER) SPK=110
      if (SPK == 110 .or. LIQ() /= WATER .or. .not.HERE(BOTTLE)) GOTO 2011
      PROP(BOTTLE)=1
      PLACE(WATER)=0
      SPK=74
      GOTO 2011

!  RUB.  YIELDS VARIOUS SNIDE REMARKS.

9160  if (OBJ /= LAMP) SPK=76
      GOTO 2011

!  THROW.  SAME AS DISCARD UNLESS AXE.  THEN SAME AS ATTACK EXCEPT IGNORE BIRD,
!  AND IF DWARF IS PRESENT THEN ONE MIGHT BE KILLED.  (ONLY WAY TO DO SO!)
!  AXE ALSO SPECIAL FOR DRAGON, BEAR, AND TROLL.  TREASURES SPECIAL FOR TROLL.

9170  if (TOTING(ROD2) .and. OBJ == ROD .and. .not.TOTING(ROD)) OBJ=ROD2
      if (.not.TOTING(OBJ)) GOTO 2011
      if (OBJ >= 50 .and. OBJ <= MAXTRS .and. AT(TROLL)) GOTO 9178
      if (OBJ == FOOD .and. HERE(BEAR)) GOTO 9177
      if (OBJ /= AXE) GOTO 9020
      do I=1,5
!  NEEDN'T CHECK DFLAG IF AXE IS HERE.
         if (DLOC(I) == LOC) GOTO 9172
      end do
      SPK=152
      if (AT(DRAGON) .and. PROP(DRAGON) == 0) GOTO 9175
      SPK=158
      if (AT(TROLL)) GOTO 9175
      if (HERE(BEAR) .and. PROP(BEAR) == 0) GOTO 9176
      OBJ=0
      GOTO 9120

9172  SPK=48
!  IF SAVED NOT = -1, HE BYPASSED THE "START" CALL.
      if (RANI(3) == 0 .or. SAVED /= -1) GOTO 9175
      DSEEN(I)=.false.
      DLOC(I)=0
      SPK=47
      DKILL=DKILL+1
      if (DKILL == 1) SPK=149
9175  CALL RSPEAK(SPK)
      CALL DROP(AXE,LOC)
      K=NOOP
      GOTO 8

!  THIS'LL TEACH HIM TO THROW THE AXE AT THE BEAR!
9176  SPK=164
      CALL DROP(AXE,LOC)
      FIXED(AXE)=-1
      PROP(AXE)=1
      CALL JUGGLE(BEAR)
      GOTO 2011

!  BUT THROWING FOOD IS ANOTHER STORY.
9177  OBJ=BEAR
      GOTO 9210

9178  SPK=159
!  SNARF A TREASURE FOR THE TROLL.
      CALL DROP(OBJ,0)
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL DROP(TROLL2,PLAC(TROLL))
      CALL DROP(TROLL2+100,FIXD(TROLL))
      CALL JUGGLE(CHASM)
      GOTO 2011

!  QUIT.  INTRANSITIVE ONLY.  VERIFY INTENT AND EXIT IF THAT'S WHAT HE WANTS.

8180  GAVEUP=YES(22,54,54)
8185  if (GAVEUP) GOTO 20000
      GOTO 2012

!  FIND.  MIGHT BE CARRYING IT, OR IT MIGHT BE HERE.  ELSE GIVE CAVEAT.

9190  if (AT(OBJ) .or. (LIQ() == OBJ .and. AT(BOTTLE))  &
            .or. K == LIQLOC(LOC)) SPK=94
      do I=1,5
         if (DLOC(I) == LOC .and. DFLAG >= 2 .and. OBJ == DWARF) SPK=94
      end do
      if (CLOSED) SPK=138
      if (TOTING(OBJ)) SPK=24
      GOTO 2011

!  INVENTORY.  IF OBJECT, TREAT SAME AS FIND.  ELSE REPORT ON CURRENT BURDEN.

8200  SPK=98
      do I=1,100
         if (I == BEAR .or. .not.TOTING(I)) cycle
         if (SPK == 98) CALL RSPEAK(99)
         BLKLIN=.false.
         CALL PSPEAK(I,-1)
         BLKLIN=.true.
         SPK=0
      end do
      if (TOTING(BEAR)) SPK=141
      GOTO 2011

!  FEED.  IF BIRD, NO SEED.  SNAKE, DRAGON, TROLL: QUIP.  IF DWARF, MAKE HIM
!  MAD.  BEAR, SPECIAL.

9210  if (OBJ == BIRD) then
         SPK=100
      else if (OBJ == SNAKE .or. OBJ == DRAGON .or. OBJ == TROLL) then
         SPK=102
         if (OBJ == DRAGON .and. PROP(DRAGON) /= 0) SPK=110
         if (OBJ == TROLL) SPK=182
         if (OBJ /= SNAKE .or. CLOSED .or. .not.HERE(BIRD)) GOTO 2011
         SPK=101
         CALL DSTROY(BIRD)
         PROP(BIRD)=0
         TALLY2=TALLY2+1
      else if (OBJ == DWARF) then
         if (.not.HERE(FOOD)) GOTO 2011
         SPK=103
         DFLAG=DFLAG+1
      else if (OBJ == BEAR) then
         if (PROP(BEAR) == 0) SPK=102
         if (PROP(BEAR) == 3) SPK=110
         if (.not.HERE(FOOD)) GOTO 2011
         CALL DSTROY(FOOD)
         PROP(BEAR)=1
         FIXED(AXE)=0
         PROP(AXE)=0
         SPK=168
      else
         SPK=14
      end if
      GOTO 2011

!  FILL.  BOTTLE MUST BE EMPTY, AND SOME LIQUID AVAILABLE.  (VASE IS NASTY.)

9220  if (OBJ /= VASE) then
         if (OBJ /= 0 .and. OBJ /= BOTTLE) GOTO 2011
         if (OBJ == 0 .and. .not.HERE(BOTTLE)) GOTO 8000
         SPK=107
         if (LIQLOC(LOC) == 0) SPK=106
         if (LIQ() /= 0) SPK=105
         if (SPK /= 107) GOTO 2011
         PROP(BOTTLE)=MOD(COND(LOC),4)/2*2
         K=LIQ()
         if (TOTING(BOTTLE)) PLACE(K)=-1
         if (K == OIL) SPK=108
         GOTO 2011
      else
         SPK=29
         if (LIQLOC(LOC) == 0) SPK=144
         if (LIQLOC(LOC) == 0 .or. .not.TOTING(VASE)) GOTO 2011
         CALL RSPEAK(145)
         PROP(VASE)=2
         FIXED(VASE)=-1
         GOTO 9021
      end if

!  BLAST.  NO EFFECT UNLESS YOU'VE GOT DYNAMITE, WHICH IS A NEAT TRICK!

9230  if (PROP(ROD2) < 0 .or. .not.CLOSED) GOTO 2011
      BONUS=133
      if (LOC == 115) BONUS=134
      if (HERE(ROD2)) BONUS=135
      CALL RSPEAK(BONUS)
      GOTO 20000

!  SCORE.  GO TO SCORING SECTION, WHICH WILL RETURN TO 8241 IF SCORNG IS TRUE.

8240  SCORNG=.true.
      GOTO 20000

8241  SCORNG=.false.
      PRINT 8243,SCORE,MXSCOR
8243  FORMAT(/' IF YOU WERE TO QUIT NOW, YOU WOULD SCORE',I4  &
              ,' OUT OF A POSSIBLE',I4,'.')
      GAVEUP=YES(143,54,54)
      GOTO 8185

!  FEE FIE FOE FOO (AND FUM).  ADVANCE TO NEXT STATE IF GIVEN IN PROPER ORDER.
!  LOOK UP WD1 IN SECTION 3 OF VOCAB TO DETERMINE WHICH WORD WE'VE GOT.  LAST
!  WORD ZIPS THE EGGS BACK TO THE GIANT ROOM (UNLESS ALREADY THERE).

8250  K=VOCAB(WD1,3)
      SPK=42
      if (FOOBAR /= 1-K) then
         if (FOOBAR /= 0) SPK=151
         GOTO 2011
      end if

      FOOBAR=K
      if (K /= 4) GOTO 2009
      FOOBAR=0
      if (PLACE(EGGS) == PLAC(EGGS)  &
            .or. (TOTING(EGGS) .and. LOC == PLAC(EGGS))) GOTO 2011
!  BRING BACK TROLL IF WE STEAL THE EGGS BACK FROM HIM BEFORE CROSSING.
      if (PLACE(EGGS) == 0 .and. PLACE(TROLL) == 0 .and. PROP(TROLL) == 0)  &
            PROP(TROLL)=1
      K=2
      if (HERE(EGGS)) K=1
      if (LOC == PLAC(EGGS)) K=0
      CALL MOVE(EGGS,PLAC(EGGS))
      CALL PSPEAK(EGGS,K)
      GOTO 2012

!  BRIEF.  INTRANSITIVE ONLY.  SUPPRESS LONG DESCRIPTIONS AFTER FIRST TIME.

8260  SPK=156
      ABBNUM=10000
      DETAIL=3
      GOTO 2011

!  READ.  MAGAZINES IN DWARVISH, MESSAGE WE'VE SEEN, AND . . . OYSTER?

8270  if (HERE(MAGZIN)) OBJ=MAGZIN
      if (HERE(TABLET)) OBJ=OBJ*100+TABLET
      if (HERE(MESSAG)) OBJ=OBJ*100+MESSAG
      if (CLOSED .and. TOTING(OYSTER)) OBJ=OYSTER
      if (OBJ > 100 .or. OBJ == 0 .or. DARK()) GOTO 8000

9270  if (DARK()) GOTO 5190
      if (OBJ == MAGZIN) SPK=190
      if (OBJ == TABLET) SPK=196
      if (OBJ == MESSAG) SPK=191
      if (OBJ == OYSTER .and. HINTED(2) .and. TOTING(OYSTER)) SPK=194
      if (OBJ /= OYSTER .or. HINTED(2) .or. .not.TOTING(OYSTER)  &
            .or. .not.CLOSED) GOTO 2011
      HINTED(2)=YES(192,193,54)
      GOTO 2012

!  BREAK.  ONLY WORKS FOR MIRROR IN REPOSITORY AND, OF COURSE, THE VASE.

9280  if (OBJ == MIRROR) SPK=148
      if (OBJ /= VASE .or. PROP(VASE) /= 0) then
         if (OBJ /= MIRROR .or. .not.CLOSED) GOTO 2011
         CALL RSPEAK(197)
         GOTO 19000
      else
         SPK=198
         if (TOTING(VASE)) CALL DROP(VASE,LOC)
         PROP(VASE)=2
         FIXED(VASE)=-1
         GOTO 2011
      end if

!  WAKE.  ONLY USE IS TO DISTURB THE DWARVES.

9290  if (OBJ /= DWARF .or. .not.CLOSED) GOTO 2011
      CALL RSPEAK(199)
      GOTO 19000

!  SUSPEND.  OFFER TO EXIT LEAVING THINGS RESTARTABLE, BUT REQUIRING A DELAY
!  BEFORE RESTARTING (SO CAN'T SAVE THE WORLD BEFORE TRYING SOMETHING RISKY).
!  UPON RESTARTING, SETUP=-1 CAUSES RETURN TO 8305 TO PICK UP AGAIN.

8300  SPK=201
      if (DEMO) GOTO 2011
      PRINT 8302,LATNCY
8302  FORMAT(/' I CAN SUSPEND YOUR ADVENTURE FOR YOU SO THAT YOU CAN',  &
              ' RESUME LATER, BUT'/' YOU WILL HAVE TO WAIT AT LEAST',  &
              I3,' MINUTES BEFORE CONTINUING.')
      if (.not.YES(200,54,54)) GOTO 2012
      CALL DATIME(SAVED,SAVET)
      SETUP=-1
      CALL CIAO

8305  YEA=START()
      SETUP=3
      K=NOOP
      GOTO 8

!  HOURS.  REPORT CURRENT NON-PRIME-TIME HOURS.

8310  CALL MSPEAK(6)
      CALL HOURS
      GOTO 2012

!  HINTS

!  COME HERE IF HE'S BEEN LONG ENOUGH AT REQUIRED LOC(S) FOR SOME UNUSED HINT.
!  HINT NUMBER IS IN VARIABLE "HINT".  BRANCH TO QUICK TEST FOR ADDITIONAL
!  CONDITIONS, THEN COME BACK TO DO NEAT STUFF.  GOTO 40010 IF CONDITIONS ARE
!  MET AND WE WANT TO OFFER THE HINT.  GOTO 40020 TO CLEAR HINTLC BACK TO ZERO,
!  40030 TO TAKE NO ACTION YET.

40000 select case (HINT)
!  NOW FOR THE QUICK TESTS.  SEE DATABASE DESCRIPTION FOR ONE-LINE NOTES.
         case (4)  ! CAVE
            QHINT = PROP(GRATE) == 0 .and. .not.HERE(KEYS)
         case (5)  ! BIRD
            QHINT = HERE(BIRD) .and. TOTING(ROD) .and. OLDOBJ == BIRD
         case (6)  ! SNAKE
            QHINT = HERE(SNAKE) .and. .not.HERE(BIRD)
         case (7)  ! MAZE
            QHINT = ATLOC(LOC) == 0 .and. ATLOC(OLDLOC) == 0  &
                   .and. ATLOC(OLDLC2) == 0 .and. HOLDNG > 1
         case (8)  ! DARK
            QHINT = PROP(EMRALD) /= -1 .and. PROP(PYRAM) == -1
         case (9)  ! WITT
            QHINT = .true.
         case default
            CALL BUG(27)
      end select

      if (QHINT) then
         HINTLC(HINT)=0
         if (YES(HINTS(HINT,3),0,54)) then
            PRINT 40012,HINTS(HINT,2)
40012       FORMAT(/' I AM PREPARED TO GIVE YOU A HINT, BUT IT WILL COST YOU',  &
                  I2,' POINTS.')
            HINTED(HINT)=YES(175,HINTS(HINT,4),54)
            if (HINTED(HINT) .and. LIMIT > 30) LIMIT=LIMIT+30*HINTS(HINT,2)
         end if
      else
         if (HINT /= 5) HINTLC(HINT)=0
      end if
      GOTO 2602


!  CAVE CLOSING AND SCORING


!  THESE SECTIONS HANDLE THE CLOSING OF THE CAVE.  THE CAVE CLOSES "CLOCK1"
!  TURNS AFTER THE LAST TREASURE HAS BEEN LOCATED (INCLUDING THE PIRATE'S
!  CHEST, WHICH MAY OF COURSE NEVER SHOW UP).  NOTE THAT THE TREASURES NEED NOT
!  HAVE BEEN TAKEN YET, JUST LOCATED.  HENCE CLOCK1 MUST BE LARGE ENOUGH TO GET
!  OUT OF THE CAVE (IT ONLY TICKS WHILE INSIDE THE CAVE).  WHEN IT HITS ZERO,
!  WE BRANCH TO 10000 TO START CLOSING THE CAVE, AND THEN SIT BACK AND WAIT FOR
!  HIM TO TRY TO GET OUT.  IF HE DOESN'T WITHIN CLOCK2 TURNS, WE CLOSE THE
!  CAVE; IF HE DOES TRY, WE ASSUME HE PANICS, AND GIVE HIM A FEW ADDITIONAL
!  TURNS TO GET FRANTIC BEFORE WE CLOSE.  WHEN CLOCK2 HITS ZERO, WE BRANCH TO
!  11000 TO TRANSPORT HIM INTO THE FINAL PUZZLE.  NOTE THAT THE PUZZLE DEPENDS
!  UPON ALL SORTS OF RANDOM THINGS.  FOR INSTANCE, THERE MUST BE NO WATER OR
!  OIL, SINCE THERE ARE BEANSTALKS WHICH WE DON'T WANT TO BE ABLE TO WATER,
!  SINCE THE CODE CAN'T HANDLE IT.  ALSO, WE CAN HAVE NO KEYS, SINCE THERE IS A
!  GRATE (HAVING MOVED THE FIXED OBJECT!) THERE SEPARATING HIM FROM ALL THE
!  TREASURES.  MOST OF THESE PROBLEMS ARISE FROM THE USE OF NEGATIVE PROP
!  NUMBERS TO SUPPRESS THE OBJECT DESCRIPTIONS UNTIL HE'S ACTUALLY MOVED THE
!  OBJECTS.

!  WHEN THE FIRST WARNING COMES, WE LOCK THE GRATE, DESTROY THE BRIDGE, KILL
!  ALL THE DWARVES (AND THE PIRATE), REMOVE THE TROLL AND BEAR (UNLESS DEAD),
!  AND SET "CLOSNG" TO TRUE.  LEAVE THE DRAGON; TOO MUCH TROUBLE TO MOVE IT.
!  FROM NOW UNTIL CLOCK2 RUNS OUT, HE CANNOT UNLOCK THE GRATE, MOVE TO ANY
!  LOCATION OUTSIDE THE CAVE (LOC<9), OR CREATE THE BRIDGE.  NOR CAN HE BE
!  RESURRECTED IF HE DIES.  NOTE THAT THE SNAKE IS ALREADY GONE, SINCE HE GOT
!  TO THE TREASURE ACCESSIBLE ONLY VIA THE HALL OF THE MT. KING.  ALSO, HE'S
!  BEEN IN GIANT ROOM (TO GET EGGS), SO WE CAN REFER TO IT.  ALSO ALSO, HE'S
!  GOTTEN THE PEARL, SO WE KNOW THE BIVALVE IS AN OYSTER.  *AND*, THE DWARVES
!  MUST HAVE BEEN ACTIVATED, SINCE WE'VE FOUND CHEST.

10000 PROP(GRATE)=0
      PROP(FISSUR)=0
      do I=1,6
         DSEEN(I)=.false.
         DLOC(I)=0
      end do
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL MOVE(TROLL2,PLAC(TROLL))
      CALL MOVE(TROLL2+100,FIXD(TROLL))
      CALL JUGGLE(CHASM)
      if (PROP(BEAR) /= 3) CALL DSTROY(BEAR)
      PROP(CHAIN)=0
      FIXED(CHAIN)=0
      PROP(AXE)=0
      FIXED(AXE)=0
      CALL RSPEAK(129)
      CLOCK1=-1
      CLOSNG=.true.
      GOTO 19999

!  ONCE HE'S PANICKED, AND CLOCK2 HAS RUN OUT, WE COME HERE TO SET UP THE
!  STORAGE ROOM.  THE ROOM HAS TWO LOCS, HARDWIRED AS 115 (NE) AND 116 (SW).
!  AT THE NE END, WE PLACE EMPTY BOTTLES, A NURSERY OF PLANTS, A BED OF
!  OYSTERS, A PILE OF LAMPS, RODS WITH STARS, SLEEPING DWARVES, AND HIM.  AT
!  THE SW END WE PLACE GRATE OVER TREASURES, SNAKE PIT, COVEY OF CAGED BIRDS,
!  MORE RODS, AND PILLOWS.  A MIRROR STRETCHES ACROSS ONE WALL.  MANY OF THE
!  OBJECTS COME FROM KNOWN LOCATIONS AND/OR STATES (E.G. THE SNAKE IS KNOWN TO
!  HAVE BEEN DESTROYED AND NEEDN'T BE CARRIED AWAY FROM ITS OLD "PLACE"),
!  MAKING THE VARIOUS OBJECTS BE HANDLED DIFFERENTLY.  WE ALSO DROP ALL OTHER
!  OBJECTS HE MIGHT BE CARRYING (LEST HE HAVE SOME WHICH COULD CAUSE TROUBLE,
!  SUCH AS THE KEYS).  WE DESCRIBE THE FLASH OF LIGHT AND TRUNDLE BACK.

11000 PROP(BOTTLE)=PUT(BOTTLE,115,1)
      PROP(PLANT)=PUT(PLANT,115,0)
      PROP(OYSTER)=PUT(OYSTER,115,0)
      PROP(LAMP)=PUT(LAMP,115,0)
      PROP(ROD)=PUT(ROD,115,0)
      PROP(DWARF)=PUT(DWARF,115,0)
      LOC=115
      OLDLOC=115
      NEWLOC=115

!  LEAVE THE GRATE WITH NORMAL (NON-NEGATIVE) PROPERTY.

      FOO=PUT(GRATE,116,0)
      PROP(SNAKE)=PUT(SNAKE,116,1)
      PROP(BIRD)=PUT(BIRD,116,1)
      PROP(CAGE)=PUT(CAGE,116,0)
      PROP(ROD2)=PUT(ROD2,116,0)
      PROP(PILLOW)=PUT(PILLOW,116,0)

      PROP(MIRROR)=PUT(MIRROR,115,0)
      FIXED(MIRROR)=116

      do I=1,100
         if (TOTING(I)) CALL DSTROY(I)
      end do

      CALL RSPEAK(132)
      CLOSED=.true.
      GOTO 2

!  ANOTHER WAY WE CAN FORCE AN END TO THINGS IS BY HAVING THE LAMP GIVE OUT.
!  WHEN IT GETS CLOSE, WE COME HERE TO WARN HIM.  WE GO TO 12000 IF THE LAMP
!  AND FRESH BATTERIES ARE HERE, IN WHICH CASE WE REPLACE THE BATTERIES AND
!  CONTINUE.  12200 IS FOR OTHER CASES OF LAMP DYING.  12400 IS WHEN IT GOES
!  OUT, AND 12600 IS IF HE'S WANDERED OUTSIDE AND THE LAMP IS USED UP, IN WHICH
!  CASE WE FORCE HIM TO GIVE UP.

12000 CALL RSPEAK(188)
      PROP(BATTER)=1
      if (TOTING(BATTER)) CALL DROP(BATTER,LOC)
      LIMIT=LIMIT+2500
      LMWARN=.false.
      GOTO 19999

12200 if (LMWARN .or. .not.HERE(LAMP)) GOTO 19999
      LMWARN=.true.
      SPK=187
      if (PLACE(BATTER) == 0) SPK=183
      if (PROP(BATTER) == 1) SPK=189
      CALL RSPEAK(SPK)
      GOTO 19999

12400 LIMIT=-1
      PROP(LAMP)=0
      if (HERE(LAMP)) CALL RSPEAK(184)
      GOTO 19999

12600 CALL RSPEAK(185)
      GAVEUP=.true.
      GOTO 20000

!  AND, OF COURSE, DEMO GAMES ARE ENDED BY THE WIZARD.

13000 CALL MSPEAK(1)
      GAVEUP=.true.
      GOTO 20000

!  OH DEAR, HE'S DISTURBED THE DWARVES.

19000 CALL RSPEAK(136)

!  EXIT CODE.

!  THE PRESENT SCORING ALGORITHM IS AS FOLLOWS:
!     OBJECTIVE:          POINTS:      PRESENT TOTAL POSSIBLE:
!  GETTING WELL INTO CAVE   25                  25
!  EACH TREASURE < CHEST    12                  60
!  TREASURE CHEST ITSELF    14                  14
!  EACH TREASURE > CHEST    16                 144
!  SURVIVING             (MAX-NUM)*10           30
!  NOT QUITTING              4                   4
!  REACHING "CLOSNG"        25                  25
!  "CLOSED": QUIT/KILLED    10
!            KLUTZED        25
!            WRONG WAY      30
!            SUCCESS        45                  45
!  CAME TO WITT'S END        1                   1
!  ROUND OUT THE TOTAL       2                   2
!                                      TOTAL:  350
!  (POINTS CAN ALSO BE DEDUCTED FOR USING HINTS.)

20000 SCORE=0
      MXSCOR=0

!  FIRST TALLY UP THE TREASURES.  MUST BE IN BUILDING AND NOT BROKEN.
!  GIVE THE POOR GUY 2 POINTS JUST FOR FINDING EACH TREASURE.

      do I=50,MAXTRS
         if (PTEXT(I) == 0) cycle
         K=12
         if (I == CHEST) K=14
         if (I > CHEST) K=16
         if (PROP(I) >= 0) SCORE=SCORE+2
         if (PLACE(I) == 3 .and. PROP(I) == 0) SCORE=SCORE+K-2
         MXSCOR=MXSCOR+K
      end do

!  NOW LOOK AT HOW HE FINISHED AND HOW FAR HE GOT.  MAXDIE AND NUMDIE TELL US
!  HOW WELL HE SURVIVED.  GAVEUP SAYS WHETHER HE EXITED VIA QUIT.  DFLAG WILL
!  TELL US IF HE EVER GOT SUITABLY DEEP INTO THE CAVE.  CLOSNG STILL INDICATES
!  WHETHER HE REACHED THE ENDGAME.  AND IF HE GOT AS FAR AS "CAVE CLOSED"
!  (INDICATED BY "CLOSED"), THEN BONUS IS ZERO FOR MUNDANE EXITS OR 133, 134,
!  135 IF HE BLEW IT (SO TO SPEAK).

      SCORE=SCORE+(MAXDIE-NUMDIE)*10
      MXSCOR=MXSCOR+MAXDIE*10
      if (.not.(SCORNG .or. GAVEUP)) SCORE=SCORE+4
      MXSCOR=MXSCOR+4
      if (DFLAG /= 0) SCORE=SCORE+25
      MXSCOR=MXSCOR+25
      if (CLOSNG) SCORE=SCORE+25
      MXSCOR=MXSCOR+25
      if (CLOSED) then
         if (BONUS == 0) SCORE=SCORE+10
         if (BONUS == 135) SCORE=SCORE+25
         if (BONUS == 134) SCORE=SCORE+30
         if (BONUS == 133) SCORE=SCORE+45
      end if
      MXSCOR=MXSCOR+45

!  DID HE COME TO WITT'S END AS HE SHOULD?

      if (PLACE(MAGZIN) == 108) SCORE=SCORE+1
      MXSCOR=MXSCOR+1

!  ROUND IT OFF.

      SCORE=SCORE+2
      MXSCOR=MXSCOR+2

!  DEDUCT POINTS FOR HINTS.  HINTS < 4 ARE SPECIAL; SEE DATABASE DESCRIPTION.

      do I=1,HNTMAX
         if (HINTED(I)) SCORE=SCORE-HINTS(I,2)
      end do

!  RETURN TO SCORE COMMAND IF THAT'S WHERE WE CAME FROM.

      if (SCORNG) GOTO 8241

!  THAT SHOULD BE GOOD ENOUGH.  LET'S TELL HIM ALL ABOUT IT.

      PRINT 20100,SCORE,MXSCOR,TURNS
20100 FORMAT(///' YOU SCORED',I4,' OUT OF A POSSIBLE',I4,  &
              ', USING',I5,' TURNS.')

      do I=1,CLSSES
         if (CVAL(I) >= SCORE) GOTO 20210
      end do
      PRINT 20202
20202 FORMAT(/' YOU JUST WENT OFF MY SCALE!!'/)
      GOTO 25000

20210 CALL SPEAK(CTEXT(I))
      if (I /= CLSSES-1) then
         K=CVAL(I)+1-SCORE
         WORD='S.'
         if (K == 1) WORD='. '
         PRINT 20212,K,WORD
20212    FORMAT(/' TO ACHIEVE THE NEXT HIGHER RATING, YOU NEED',I3,  &
               ' MORE POINT',A2/)
      else
         PRINT 20222
20222    FORMAT(/' TO ACHIEVE THE NEXT HIGHER RATING ',  &
               'WOULD BE A NEAT TRICK!'//' CONGRATULATIONS!!'/)
      end if

25000 STOP


contains

!  TOTING(OBJ)  = TRUE IF THE OBJ IS BEING CARRIED
   logical function TOTING(OBJ)
      integer, intent(in) :: OBJ
      TOTING = PLACE(OBJ) == -1
      return
   end function

!  HERE(OBJ)    = TRUE IF THE OBJ IS AT "LOC" (OR IS BEING CARRIED)
   logical function HERE(OBJ)
      integer, intent(in) :: OBJ
      HERE = PLACE(OBJ) == LOC .or. TOTING(OBJ)
      return
   end function

!  AT(OBJ)      = TRUE IF ON EITHER SIDE OF TWO-PLACED OBJECT
   logical function AT(OBJ)
      integer, intent(in) :: OBJ
      AT = PLACE(OBJ) == LOC .or. FIXED(OBJ) == LOC
      return
   end function

   integer function LIQ2(PBOTL)
      integer, intent(in) :: PBOTL
      LIQ2 = (1 - PBOTL) * WATER + (PBOTL / 2) * (WATER + OIL)
      return
   end function

!  LIQ()   = OBJECT NUMBER OF LIQUID IN BOTTLE
   integer function LIQ()
      LIQ = LIQ2(MAX(PROP(BOTTLE), -1-PROP(BOTTLE)))
      return
   end function

!  LIQLOC(LOC)  = OBJECT NUMBER OF LIQUID (IF ANY) AT LOC
   integer function LIQLOC(LOC)
      integer, intent(in) :: LOC
      LIQLOC = LIQ2((MOD(COND(LOC) / 2 * 2, 8) - 5)  &
            * MOD(COND(LOC) / 4, 2) + 1)
      return
   end function

!  BITSET(L,N)  = TRUE IF COND(L) HAS BIT N SET (BIT 0 IS UNITS BIT)
   logical function BITSET(L, N)
      integer, intent(in) :: L, N
      BITSET = BTEST(COND(L), N)
      return
   end function

!  FORCED(LOC)  = TRUE IF LOC MOVES WITHOUT ASKING FOR INPUT (COND=2)
   logical function FORCED(LOC)
      integer, intent(in) :: LOC
      FORCED = COND(LOC) == 2
      return
   end function

!  DARK()  = TRUE IF LOCATION "LOC" IS DARK
   logical function DARK()
      DARK = MOD(COND(LOC), 2) == 0 .and.  &
            (PROP(LAMP) == 0 .or. .not. HERE(LAMP))
      return
   end function

!  PCT(N)       = TRUE N% OF THE TIME (N INTEGER FROM 0 TO 100)
   logical function PCT(N)
      integer, intent(in) :: N
      PCT = RANI(100) < N
      return
   end function

end program
