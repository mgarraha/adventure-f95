C  UTILITY ROUTINES TO BRIDGE THE GAP BETWEEN PDP-10 AND MODERN SYSTEMS

      INTEGER FUNCTION IA5(CHARS)

C  PACKS THE 7-BIT CHARACTERS IN CHARS INTO A 36-BIT INTEGER.

      CHARACTER*5 CHARS
      INTEGER CH
      INTEGER I

      IA5=0
      DO I=1,5
         CH=ICHAR(CHARS(I:I))
         CH=ISHFT(CH,36-7*I)
         IA5=IOR(IA5,CH)
      ENDDO
      RETURN
      END

      CHARACTER*5 FUNCTION A5I(BITS)

C  CONVERTS A 36-BIT INTEGER TO 7-BIT CHARACTERS.

      INTEGER BITS
      INTEGER CH
      INTEGER I

      DO I=1,5
         CH=IAND(ISHFT(BITS,7*I-36),O"177")
         A5I(I:I)=ACHAR(CH)
      ENDDO
      RETURN
      END

      SUBROUTINE DATE(DMY)

C  STORES THE CURRENT DATE IN DMY IN 'DD-MMM-YY' FORMAT.
C  EMULATES A DEC FORTRAN INTRINSIC.

      CHARACTER*9 DMY
      INTEGER DTTM(8)
      CHARACTER*5 MONTHS(12)
      DATA MONTHS/'JAN','FEB','MAR','APR','MAY','JUN',
     1            'JUL','AUG','SEP','OCT','NOV','DEC'/

      CALL DATE_AND_TIME(VALUES=DTTM)
      WRITE(DMY,31)DTTM(3),MONTHS(DTTM(2)),MOD(DTTM(1),100)
31    FORMAT(I2.2,'-',A3,'-',I2.2)
      RETURN
      END


      SUBROUTINE TIME(HMS)

C  STORES THE CURRENT TIME IN HMS IN 'HH:MM:SS' FORMAT.
C  EMULATES A DEC FORTRAN INTRINSIC.

      CHARACTER*8 HMS
      INTEGER DTTM(8)

      CALL DATE_AND_TIME(VALUES=DTTM)
      WRITE(HMS,24)DTTM(5),DTTM(6),DTTM(7)
24    FORMAT(I2.2,':',I2.2,':',I2.2)
      RETURN
      END
